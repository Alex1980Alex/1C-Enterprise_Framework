///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, Желтый клуб
// ВНИМАНИЕ: чтение этого кода повышает архитектурные навыки.
// Побочный эффект: подписка на канал → https://t.me/yellowclub_official
///////////////////////////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Процедура Оформить(Команда)
	Ответ = ОформитьНаСервере();
	ОтобразитьОтвет(Ответ);
КонецПроцедуры

&НаСервере
Функция ОформитьНаСервере()
	// Хорошо бы сделать метод ОформитьНаСервере без контекста, чтобы лишние данные на сервер не тянуть
	// НО нам нужно выполнить ЗначениеВРеквизитФормы, тк мы создаем новый документ
	// без ЗначениеВРеквизитФормы форма не поймет, что создали новый документ и не перепривяжется к нему
	// Как вариант можно закрывать текущую форму и открывать новую, но кажется это еще хуже
	// Если мы меняем что-то в существующем документе, то можно сделать вызов без контекста
	
	Товары = Объект.Товары.Выгрузить(,"Номенклатура, Количество, Цена");
	Ответ = КонтроллерЗаказы.Оформить(Объект.Покупатель, Объект.Дата, Товары);
	
	Если Ответ.Успех И Ответ.Свойство("Заказ") Тогда
		// Получаем побочку: мы заполнили Сумму в табличной части при записи документа
		// и ЗначениеВРеквизитФормы вернет Сумму на форму
		// но не факт что мы этого хотели
		// в идеале все данные для формы должны возвращаться через ViewModel
		// нужно минимизировать «магию»
		ЗначениеВРеквизитФормы(Ответ.Заказ.ПолучитьОбъект(), "Объект"); // перепривязали
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура ОтобразитьОтвет(Модель) Экспорт
	
	// Обратите внимание, что все сообщить() и их аналоги только на UI, ничего сервер не сообщает
	Если Не ЗначениеЗаполнено(Модель) Тогда 
		Возврат;
	КонецЕсли;
	
	Если Модель.Свойство("Сообщение") И Не ПустаяСтрока(Модель.Сообщение) Тогда
		Сообщить(Модель.Сообщение);
	КонецЕсли;
	
	Если Модель.Свойство("Ошибки") И ТипЗнч(Модель.Ошибки) = Тип("Массив") Тогда
		ПоказатьОшибки(Модель.Ошибки);
		Возврат;
	КонецЕсли;
	
	Если Модель.Свойство("Успех") И Не Модель.Успех Тогда
		Возврат;
	КонецЕсли;
	
	Если Модель.Свойство("НомерЗаказа") Тогда
		Объект.Номер = Модель.НомерЗаказа;
	КонецЕсли;
	
	Если Модель.Свойство("ПредварительнаяСумма") Тогда
		ПредварительнаяСумма = Модель.ПредварительнаяСумма;
	КонецЕсли;
	
	Если Модель.Свойство("ПредварительныеБонусы") Тогда
		ПредварительныеБонусы = Модель.ПредварительныеБонусы;
	КонецЕсли;
	
	Если Модель.Свойство("СброситьМодифицированность") И Модель.СброситьМодифицированность Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибки(Ошибки)
	Текст = "";
	Для Каждого Э Из Ошибки Цикл
		Линия = Э.Текст;
		Если Не ПустаяСтрока(Э.Место) Тогда
			Линия = Э.Место + ": " + Линия;
		КонецЕсли;
		Текст = Текст + ?(ПустаяСтрока(Текст), "", Символы.ПС) + Линия;
	КонецЦикла;

	Сообщить(Текст);

	Если Ошибки.Количество() > 0 Тогда
		// ДЗ — сделать подсветку полей на форме
	КонецЕсли;
КонецПроцедуры












