#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИнициализироватьРеквизитыФормы();
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеФормой();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборТочкаМаршрутаПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидПеревозкиПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Подключить = ПроверитьОсновныеОтборы();
	ОбработчикОжидания(Подключить);
	
КонецПроцедуры

&НаКлиенте
Процедура СуткиПриИзменении(Элемент)
	ОбновитьПериодИФорму();
КонецПроцедуры


&НаКлиенте
Процедура ОтборНоменклатураПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьВсеУсиленныйКонтрольПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьВсеПроизвольныйКомпозитПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокУсиленныйКонтроль

&НаКлиенте
Процедура СписокСписокУсиленныйКонтрольВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле = Элементы.СписокСписокУсиленныйКонтрольПометка Тогда
		НовоеЗначениеПометки = НЕ ТекущиеДанные.Пометка;
		УстановитьГрупповуюПометкуУсиленныйКонтроль(ТекущиеДанные.ГруппаТС, ТекущиеДанные.Показатель,
			НовоеЗначениеПометки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПроизвольныйКомпозит

&НаКлиенте
Процедура СписокПроизвольныйКомпозитВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	
	Если Поле = Элементы.СписокПроизвольныйКомпозитПометка Тогда
		
		// Проверка совпадения номенклатуры и контрагента при активации пометки
		Если НЕ ТекущиеДанные.Пометка Тогда
			ПерваяОтмеченнаяСтрока = НайтиПервуюОтмеченнуюСтроку();
			
			Если ПерваяОтмеченнаяСтрока <> Неопределено Тогда
				МожноПродолжить = ПроверитьСовпадениеДанныхСПервойСтрокой(ТекущиеДанные, ПерваяОтмеченнаяСтрока);
				Если НЕ МожноПродолжить Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ТекущиеДанные.Пометка = НЕ ТекущиеДанные.Пометка;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокПромежуточныеПробы
&НаКлиенте
Процедура СписокПромежуточныеПробыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.СписокПромежуточныеПробы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ДокументОткрытия = Неопределено;
	ИмяФормыДокумента = Неопределено;
	
	// Определяем документ и форму в зависимости от поля
	Если Поле.Имя = "СписокПромежуточныеПробыЛабораторныйАнализНомер" Тогда
		ДокументОткрытия = ТекущиеДанные.ПромежуточныйКомпозит;
		ИмяФормыДокумента = "Документ.гкс_ЛабораторныйАнализ.Форма.ФормаДокумента";
	ИначеЕсли Поле.Имя = "СписокПромежуточныеПробыНомерПробы" Тогда
		ДокументОткрытия = ТекущиеДанные.ДокументКомпозитнаяПроба;
		ИмяФормыДокумента = "Документ.гкс_ФормированиеНомераПробы.Форма.ФормаДокумента";
	КонецЕсли;
	
	// Открываем документ, если он заполнен
	Если ЗначениеЗаполнено(ДокументОткрытия) И ЗначениеЗаполнено(ИмяФормыДокумента) Тогда
		гкс_ПриемкаНаПЛККлиент.ОткрытьДокумент(ЭтотОбъект, ДокументОткрытия, ИмяФормыДокумента, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПромежуточныеПробыПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.СписокПромежуточныеПробы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ДокументФормированиеНомераПробы", ТекущиеДанные.ДокументКомпозитнаяПроба);
	
	СписокРегистрацииПромежуточныхПроб(ПараметрыЗапроса);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаУбавитьСутки(Команда)
	
	ДобавитьСутки(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьСутки(Команда)
	
	ДобавитьСутки(1);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформироватьПробы(ТипСписка = "ПроизвольныйКомпозит")

	// Выбор списка в зависимости от типа
	Если ТипСписка = "УсиленныйКонтроль" Тогда
		СписокДанных = СписокУсиленныйКонтроль;
	Иначе
		СписокДанных = СписокПроизвольныйКомпозит;
	КонецЕсли;

	// Получаем все отмеченные строки
	ОтборПоПометке = Новый Структура("Пометка", Истина);
	НайденныеСтроки = СписокДанных.НайтиСтроки(ОтборПоПометке);

	Если НайденныеСтроки.Количество() = 0 Тогда
		гкс_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не выбрано ни одной строки для формирования проб'"));
		Возврат;
	КонецЕсли;

	// Проверки для усиленного контроля
	Если ТипСписка = "УсиленныйКонтроль" Тогда
		МассивНесовпадений = Новый Массив;
		Результат = ПроверитьСовпадениеПоказателяУсиленныйКонтроль(НайденныеСтроки, МассивНесовпадений);
		Если НЕ Результат Тогда
			Возврат;
		КонецЕсли;

		МассивНесовпадений = Новый Массив;
		Результат = ПроверитьСостояниеКачестваУсиленныйКонтроль(НайденныеСтроки, МассивНесовпадений);
		Если НЕ Результат Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;

	// Анализируем отмеченные строки
	ОтмеченныеСтроки = Новый Массив;
	ДокументКомпозитнаяПроба = Неопределено;

	Для Каждого СтрокаСписка Из НайденныеСтроки Цикл
		Если ЗначениеЗаполнено(СтрокаСписка.ДокументКомпозитнаяПроба) Тогда
			ДокументКомпозитнаяПроба = СтрокаСписка.ДокументКомпозитнаяПроба;
			Прервать;
		Иначе
			ОтмеченныеСтроки.Добавить(СтрокаСписка);
		КонецЕсли;
	КонецЦикла;

	ПараметрыОткрытия = Новый Структура;
	ДопПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения("ОбработчикОжиданияСписок", ЭтотОбъект);

	Если ЗначениеЗаполнено(ДокументКомпозитнаяПроба) Тогда
		// Открываем существующий документ
		ПараметрыОткрытия.Вставить("Ключ", ДокументКомпозитнаяПроба);
		ИмяФормыДокумента = "Документ.гкс_ФормированиеНомераПробы.Форма.ФормаДокумента";

	Иначе
		// Создаем новый документ
		ТипПробыКомпозитная = ПредопределенноеЗначение("Перечисление.гкс_ТипыПроб.Композитная");

		// Формируем массив структур из всех отмеченных строк
		МассивСтрокДанных = Новый Массив;
		Для Каждого СтрокаДанных Из ОтмеченныеСтроки Цикл
			СтруктураСтроки = Новый Структура;
			СтруктураСтроки.Вставить("ДокументРегистрации", СтрокаДанных.ДокументРегистрации);
			МассивСтрокДанных.Добавить(СтруктураСтроки);
		КонецЦикла;

		// Общие параметры
		ДопПараметры.Вставить("Номенклатура", ОтмеченныеСтроки[0].Номенклатура);
		ДопПараметры.Вставить("Контрагент", ОтмеченныеСтроки[0].Контрагент);
		ДопПараметры.Вставить("Организация", ОтмеченныеСтроки[0].Организация);
		ДопПараметры.Вставить("ТочкаМаршрута", Объект.ТочкаМаршрута);
		ДопПараметры.Вставить("ВидПеревозки", Объект.ВидПеревозки);
		ДопПараметры.Вставить("МассивСтрокДанных", МассивСтрокДанных);
		ДопПараметры.Вставить("НачалоПериода", НачалоПериода);
		ДопПараметры.Вставить("КонецПериода", КонецПериода);
		ДопПараметры.Вставить("ПроизводственныеСуткиРегистрации", Сутки);
		ДопПараметры.Вставить("ТипПробы", ТипПробыКомпозитная);

		// Общие для обоих типов - данные о погрузке и станции
		ДопПараметры.Вставить("ПунктПогрузки", ОтмеченныеСтроки[0].ПунктПогрузки);
		ДопПараметры.Вставить("СтанцияОтправления", ОтмеченныеСтроки[0].СтанцияОтправления);
		ДопПараметры.Вставить("СтанцияОтправленияПредставление", ОтмеченныеСтроки[0].СтанцияОтправленияПредставление);

		// Специфичные параметры для усиленного контроля
		Если ТипСписка = "УсиленныйКонтроль" Тогда
			ДопПараметры.Вставить("Показатель", ОтмеченныеСтроки[0].Показатель);
			ДопПараметры.Вставить("НазначениеИспользованияКачества",
				ПредопределенноеЗначение("Перечисление.гкс_НазначенияИспользованияКачества.ПромежуточныйКомпозит"));
		КонецЕсли;

		ПараметрыОткрытия.Вставить("ДопПараметры", ДопПараметры);

		// Параметры страницы
		ТекущаяСтраница = Элементы.ГруппаСтраницы.ТекущаяСтраница;

		Если ТипСписка = "УсиленныйКонтроль" Тогда
			ПараметрыОткрытия.Вставить("СтраницаУсиленныйКонтроль", ТекущаяСтраница.Имя = "СтраницаУсиленныйКонтроль");
		Иначе
			ПараметрыОткрытия.Вставить("СтраницаПроизвольныйКомпозит", ТекущаяСтраница.Имя = "СтраницаПроизвольныйКомпозит");
			ПараметрыОткрытия.Вставить("СтраницаПромежуточныеПробы", ТекущаяСтраница.Имя = "СтраницаПромежуточныеПробы");
		КонецЕсли;

		ИмяФормыДокумента = "Обработка.гкс_АРМПромежуточныйКомпозит.Форма.ФормаФормированиеКомпозитнойФормы";
	КонецЕсли;

	ОткрытьФорму(ИмяФормыДокумента, ПараметрыОткрытия, ЭтотОбъект, ЭтотОбъект, , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура КомандаСформироватьПробыУсиленныйКонтроль(Команда)
	
	КомандаСформироватьПробы("УсиленныйКонтроль");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьУсиленныйКонтроль(Команда)
	ОбновитьПериодИФорму();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьПроизвольныйКомпозит(Команда)
	ОбновитьПериодИФорму();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечатьЭтикетки(Команда)
	ТекущиеДанные = Неопределено;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "СтраницаПроизвольныйКомпозит" Тогда
		ТекущиеДанные = Элементы.СписокПроизвольныйКомпозит.ТекущиеДанные;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "СтраницаПромежуточныеПробы" Тогда
		ТекущиеДанные = Элементы.СписокПромежуточныеПробы.ТекущиеДанные;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "СтраницаУсиленныйКонтроль" Тогда
		ТекущиеДанные = Элементы.СписокУсиленныйКонтроль.ТекущиеДанные;
	КонецЕсли;
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не отмечена проба'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	КомпозитнаяПроба = ТекущиеДанные.ДокументКомпозитнаяПроба;
	Если НЕ ЗначениеЗаполнено(КомпозитнаяПроба) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена композитная проба'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(КомпозитнаяПроба);
	
	АвтоматическаяПечатьЭтикеток = гкс_ОбщегоНазначенияВызовСервера.ЗначениеКонстанты(
			"гкс_АвтоматическаяПечатьЭтикеток");
	
	Если АвтоматическаяПечатьЭтикеток Тогда
		ПараметрыПечати = Новый Структура;
		ПараметрыПечати.Вставить("АвтоматическаяПечатьЭтикеток", АвтоматическаяПечатьЭтикеток);
		
		гкс_УправлениеПечатьюКлиент.ПечатьЭтикеткаНаПринтер(
			"Документ.гкс_ФормированиеНомераПробы", "ПФ_MXL_Этикетка", МассивОбъектов, ПараметрыПечати);
	Иначе
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.гкс_ФормированиеНомераПробы",
			"ПФ_MXL_Этикетка", МассивОбъектов, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформироватьПробыПроизвольныйКомпозит(Команда)
	
	КомандаСформироватьПробы("ПроизвольныйКомпозит");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСформироватьЛабораторныйАнализ(Команда)
	ТекущиеДанные = Элементы.СписокПромежуточныеПробы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКлючевойОперации = "АРМКомпозитныеПробы.ДокументЛабораторныйАнализ.ОткрытиеФормы";
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикОжиданияСписок", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ПромежуточныйКомпозит) Тогда
		
		ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.ПромежуточныйКомпозит);
		
		ОценкаПроизводительностиКлиент.ЗамерВремени(ИмяКлючевойОперации);
		
		ОткрытьФорму("Документ.гкс_ЛабораторныйАнализ.ФормаОбъекта", ПараметрыОткрытия, ЭтотОбъект, ЭтотОбъект, , ,
			Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьПромежуточныеПробы(Команда)
	ОбновитьПериодИФорму();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция НайтиПервуюОтмеченнуюСтроку()
	
	Для Каждого СтрокаСписка Из СписокПроизвольныйКомпозит Цикл
		Если СтрокаСписка.Пометка Тогда
			Возврат СтрокаСписка;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Функция ПроверитьСовпадениеДанныхСПервойСтрокой(ТекущиеДанные, ПерваяОтмеченнаяСтрока)
	
	МассивНесовпадений = Новый Массив;
	
	// Проверяем номенклатуру
	Если ТекущиеДанные.Номенклатура <> ПерваяОтмеченнаяСтрока.Номенклатура Тогда
		// Формируем текст сообщения по аналогии с контрагентом
		ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Выбранная строка имеет отличающиеся данные от первой отмеченной строки:%1- Номенклатура'"),
				Символы.ПС);
		ПоказатьПредупреждение( , ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	// Проверяем контрагента
	Если ТекущиеДанные.Контрагент <> ПерваяОтмеченнаяСтрока.Контрагент Тогда
		МассивНесовпадений.Добавить("Контрагент");
	КонецЕсли;
	
	Если ТекущиеДанные.Организация <> ПерваяОтмеченнаяСтрока.Организация Тогда
		МассивНесовпадений.Добавить("Организация");
	КонецЕсли;
	
	// Выводим сообщение при наличии несовпадений
	Если МассивНесовпадений.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Выбранная строка имеет отличающиеся данные от первой отмеченной строки:'");
		
		Для Каждого Несовпадение Из МассивНесовпадений Цикл
			ТекстСообщения = СтрШаблон("%1%2- %3", ТекстСообщения, Символы.ПС, Несовпадение);
		КонецЦикла;
		
		ТекстСообщения = СтрШаблон("%1%2%3", ТекстСообщения, Символы.ПС, НСтр("ru = 'Продолжить?'"));
		
		ДополнительныеПараметры = Новый Структура("ТекущиеДанные", ТекущиеДанные);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаРезультатаВопроса", ЭтотОбъект,
				ДополнительныеПараметры);
		Режим = РежимДиалогаВопрос.ДаНет;
		ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения, Режим, 0, , "Несовпадение данных");
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаРезультатаВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.Пометка = НЕ ТекущиеДанные.Пометка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПериодИФорму()
	УстановитьПериод();
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	
	Видимость = ПроверитьОсновныеОтборы();
	
	Элементы.ГруппаСтраницы.Видимость = Видимость;
	Элементы.ГруппаОбщиеОтборы.Видимость = Видимость;
	
	ОбработчикОжидания(Видимость);
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжидания(Подключить)
	Если Подключить Тогда
		ИнтервалОбработки = 0.5;
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьСписок", ИнтервалОбработки, Истина);
	Иначе
		ОтключитьОбработчикОжидания("Подключаемый_ОбновитьСписок");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьОсновныеОтборы()
	Возврат ЗначениеЗаполнено(Объект.ТочкаМаршрута)
	И ЗначениеЗаполнено(Объект.ВидПеревозки)
	И ЗначениеЗаполнено(Сутки)
	И ЗначениеЗаполнено(НачалоПериода)
	И ЗначениеЗаполнено(КонецПериода);
КонецФункции

&НаСервере
Процедура ИнициализироватьРеквизитыФормы()
	
	Сутки = НачалоДня(НачалоДня(ТекущаяДатаСеанса()) - 1);
	
	СписокСостояний = Объект.СписокСостояний;
	СписокСостояний.Добавить(Перечисления.гкс_СостоянияРегистрации.Прибыл);
	СписокСостояний.Добавить(Перечисления.гкс_СостоянияРегистрации.ГотовКОтборуПроб);
	СписокСостояний.Добавить(Перечисления.гкс_СостоянияРегистрации.КачествоНеПринято);
	СписокСостояний.Добавить(Перечисления.гкс_СостоянияРегистрации.КачествоПринято);
	СписокСостояний.Добавить(Перечисления.гкс_СостоянияРегистрации.ВыгрузкаРазрешена);
	СписокСостояний.Добавить(Перечисления.гкс_СостоянияРегистрации.РезультатыПробПолучены);
	
	УстановитьПериод();
	
	ИнициализироватьОтборТочкаМаршрута();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОтборТочкаМаршрута()
	
	ИспользоватьНесколькоАЛЦ = ПолучитьФункциональнуюОпцию("гкс_ИспользоватьНесколькоАЛЦ");
	
	Элементы.ОтборТочкаМаршрута.Доступность = ИспользоватьНесколькоАЛЦ;
	Элементы.ОтборТочкаМаршрута.РежимВыбораИзСписка = ИспользоватьНесколькоАЛЦ;
	
	АЛЦПользователя = Новый Массив;
	Объект.ТочкаМаршрута =
		гкс_ПриемкаТранспорта.ПЛКизНастроекБазыПользователя(ИспользоватьНесколькоАЛЦ, АЛЦПользователя);
	Если ИспользоватьНесколькоАЛЦ
		И ЗначениеЗаполнено(АЛЦПользователя) Тогда
		СписокВыбора = Элементы.ОтборТочкаМаршрута.СписокВыбора;
		СписокВыбора.ЗагрузитьЗначения(АЛЦПользователя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСутки(КоличествоСуток)
	
	Сутки = Сутки + КоличествоСуток * гкс_ОбщегоНазначенияКлиентСервер.СекундВДне();
	
	ОбновитьПериодИСписки();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПериод()
	
	НачалоПериода = гкс_ПриемкаТранспорта.НачалоПроизводственныхСуток(Сутки);
	КонецПериода = гкс_ПриемкаТранспорта.ОкончаниеПроизводственныхСуток(Сутки);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПериодИСписки()
	
	// Устанавливаем период
	УстановитьПериод();
	
	// Заполняем списоки
	ОбновитьСписки();
	
КонецПроцедуры

#Область ОбработчикиОжидания

&НаКлиенте
Процедура ОбработчикОжиданияСписок(Результат, ДополнительныеПараметры) Экспорт
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьСписок()
	ОбновитьСписки();
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписки()
	ЗаполнитьСписокПроизвольныйКомпозит();
	ЗаполнитьСписокПромежуточныеПробы();
	ЗаполнитьСписокУсиленныйКонтроль();
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьСписокУсиленныйКонтроль()
	Если НЕ Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "СтраницаУсиленныйКонтроль" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыЗапроса.Вставить("КонецПериода", КонецПериода);
	ПараметрыЗапроса.Вставить("ПроизводственныеСуткиРегистрации", Сутки);
	ПараметрыЗапроса.Вставить("ПоказыватьВсе", ПоказыватьВсеУсиленныйКонтроль);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	ДанныеДляСписокУсиленныйКонтроль = Обработка.ДанныеДляСписка(ПараметрыЗапроса, "УсиленныйКонтроль");
	
	СписокУсиленныйКонтроль.Очистить();
	СписокУсиленныйКонтроль.Загрузить(ДанныеДляСписокУсиленныйКонтроль);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПроизвольныйКомпозит()
	Если НЕ Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "СтраницаПроизвольныйКомпозит" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыЗапроса.Вставить("КонецПериода", КонецПериода);
	ПараметрыЗапроса.Вставить("ПроизводственныеСуткиРегистрации", Сутки);
	ПараметрыЗапроса.Вставить("ПоказыватьВсе", ПоказыватьВсеПроизвольныйКомпозит);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	ДанныеДляСписокПроизвольныйКомпозит = Обработка.ДанныеДляСписка(ПараметрыЗапроса, "ПроизвольныйКомпозит");
	
	СписокПроизвольныйКомпозит.Очистить();
	СписокПроизвольныйКомпозит.Загрузить(ДанныеДляСписокПроизвольныйКомпозит);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПромежуточныеПробы()
	Если НЕ Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя = "СтраницаПромежуточныеПробы" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыЗапроса.Вставить("КонецПериода", КонецПериода);
	ПараметрыЗапроса.Вставить("ПроизводственныеСуткиРегистрации", Сутки);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	ДанныеДляСписокПромежуточныеПробы = Обработка.ДанныеДляСписокПромежуточныеПробы(ПараметрыЗапроса);
	
	СписокПромежуточныеПробы.Очистить();
	СписокПромежуточныеПробы.Загрузить(ДанныеДляСписокПромежуточныеПробы);
	
КонецПроцедуры

&НаСервере
Процедура СписокРегистрацииПромежуточныхПроб(ПараметрыЗапроса)
	
	
	Обработка = РеквизитФормыВЗначение("Объект");
	ДанныеСписокРегистрацииПромежуточныхПроб = Обработка.ДанныеДляСписокРегистрацииПромежуточныхПроб(ПараметрыЗапроса);
	
	СписокРегистрацииПромежуточныхПроб.Очистить();
	СписокРегистрацииПромежуточныхПроб.Загрузить(ДанныеСписокРегистрацииПромежуточныхПроб);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГрупповуюПометкуУсиленныйКонтроль(ГруппаТС, Показатель, НовоеЗначениеПометки)
	
	Отбор = Новый Структура("ГруппаТС, Показатель", ГруппаТС, Показатель);
	НайденныеСтроки = СписокУсиленныйКонтроль.НайтиСтроки(Отбор);
	
	Для Каждого СтрокаСписка Из НайденныеСтроки Цикл
		СтрокаСписка.Пометка = НовоеЗначениеПометки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьСовпадениеПоказателяУсиленныйКонтроль(НайденныеСтроки, МассивНесовпадений)
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПервыйПоказатель = НайденныеСтроки[0].Показатель;
	
	Для Каждого СтрокаСписка Из НайденныеСтроки Цикл
		Если СтрокаСписка.Показатель <> ПервыйПоказатель Тогда
			МассивНесовпадений.Добавить(НСтр("ru = 'Показатель'"));
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если МассивНесовпадений.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Отмеченные строки имеют разные показатели."
				+ "Необходимо выбрать строки с одинаковым показателем.'");
		гкс_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция ПроверитьСостояниеКачестваУсиленныйКонтроль(НайденныеСтроки, МассивНесовпадений)
	
	// Создаем массив допустимых состояний
	ДопустимыеСостояния = Новый СписокЗначений;
	ДопустимыеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияКачества.ПустаяСсылка"));
	ДопустимыеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияКачества.ОтборПроб"));
	
	Для Каждого СтрокаСписка Из НайденныеСтроки Цикл
		// Если состояние НЕ в списке допустимых
		Если ДопустимыеСостояния.НайтиПоЗначению(СтрокаСписка.СостояниеКачества) = Неопределено Тогда
			
			ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Для регистрации %1 анализы на показатель %2 уже выполнены (состояние: %3)."
						+ "Повторно анализы не могут быть сформированы.'"),
					СтрокаСписка.ДокументРегистрации,
					СтрокаСписка.Показатель,
					СтрокаСписка.СостояниеКачества);
			гкс_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции


#КонецОбласти