#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтраницаУсиленныйКонтроль = Параметры.Свойство("СтраницаУсиленныйКонтроль");
	СтраницаПроизвольныйКомпозит = Параметры.Свойство("СтраницаПроизвольныйКомпозит");
	
	Если СтраницаУсиленныйКонтроль Или СтраницаПроизвольныйКомпозит Тогда
		
		ДопПараметры = Параметры.ДопПараметры;
		МассивСтрокДанных = ДопПараметры.МассивСтрокДанных;
		// Таблица регистраций уже заполнена из отмеченных строк в предыдущей форме
		Если Не ДопПараметры.Свойство("МассивСтрокДанных") ИЛИ МассивСтрокДанных.Количество() = 0 Тогда
			Отказ = Истина;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, ДопПараметры);
		
		// Заполняем таблицу регистраций из отмеченных строк
		Объект.ТаблицаРегистраций.Очистить();
		Для Каждого СтруктураСтроки Из МассивСтрокДанных Цикл
			НоваяСтрока = Объект.ТаблицаРегистраций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураСтроки);
		КонецЦикла;
		
		// Заполняем реквизиты объекта из первого элемента отмеченных строк
		ПервыйЭлемент = ДопПараметры.МассивСтрокДанных[0];
		ЗаполнитьЗначенияСвойств(Объект, ПервыйЭлемент);
		
		Если СтраницаПроизвольныйКомпозит Тогда
			ЗаполнитьПоказателиИзНормативнойСертификацииНаСервере(ДопПараметры);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьЭлементовФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПоказатели

&НаКлиенте
Процедура ПоказателиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Показатели.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ НЕ ЗначениеЗаполнено(ТекущиеДанные.Показатель) Тогда
		Возврат;
	КонецЕсли;
	
	НайденыйПоказатели = Объект.Показатели.НайтиСтроки(Новый Структура("Показатель", ТекущиеДанные.Показатель));
	Если НайденыйПоказатели.Количество() > 1 Тогда
		Индекс = Объект.Показатели.Индекс(ТекущиеДанные);
		Если Индекс <> Неопределено Тогда
			Объект.Показатели.Удалить(Индекс);
		КонецЕсли;
		
	Иначе
		ВыполнитьПроверкуНормативовПоказателя(ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСформироватьПромежуточныйКомпозит(Команда)
	
	СформироватьДокумент();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоказательПоСпецификации(Команда)
	
	Если НормативныеПоказатели.Количество() = 0 Тогда
		ПоказатьПредупреждение( , "Нет доступных показателей из нормативной сертификации для данной номенклатуры.");
		Возврат;
	КонецЕсли;
	
	// Получаем список уже добавленных показателей
	УжеДобавленныеПоказатели = ПолучитьДобавленныеПоказатели();
	
	// Формируем список доступных для выбора (нормативные МИНУС уже добавленные)
	ДоступныеДляВыбора = Новый Массив;
	Для Каждого НормативныйПоказатель Из НормативныеПоказатели Цикл
		Если УжеДобавленныеПоказатели.Найти(НормативныйПоказатель.Значение) = Неопределено Тогда
			ДоступныеДляВыбора.Добавить(НормативныйПоказатель.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ДоступныеДляВыбора.Количество() = 0 Тогда
		ПоказатьПредупреждение( , "Все нормативные показатели уже добавлены в таблицу.");
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыДляНормативныхПоказателей(ДоступныеДляВыбора);
	
	// Открываем форму выбора показателей анализа
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПоказателей", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.гкс_ПоказателиАнализовНоменклатуры.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#Область ВидимостьДоступность
&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементовФормы()
	Элементы.ГруппаПоказатели.Видимость = СтраницаПроизвольныйКомпозит;	
КонецПроцедуры
#КонецОбласти

&НаСервере
Функция ПроверитьПараметры()
	Возврат Не ЗначениеЗаполнено(Объект.Номенклатура)
		Или Не ЗначениеЗаполнено(Объект.ТочкаМаршрута)
		Или Не ЗначениеЗаполнено(Объект.ВидПеревозки);
КонецФункции

&НаСервере
Процедура СформироватьДокумент()
	
	
	Если Объект.ТаблицаРегистраций.Количество() > 0 Тогда
		
		Попытка
			
			// Подготовка данных заполнения для лабораторного анализа
			ПараметрыСозданияАнализа = Новый Структура;
			ПараметрыСозданияАнализа.Вставить("гкс_НазначениеИспользованияКачества",
				Перечисления.гкс_НазначенияИспользованияКачества.ПромежуточныйКомпозит);
			
			// Подготовка данных заполнения для лабораторного анализа
			ИмяПараметраПоказатели = "ПоказателиДляЗаполнения";
			МассивПоказателей = Неопределено;

			МассивПоказателей = Новый Массив;
			Для Каждого СтрокаПоказатель Из Объект.Показатели Цикл
				Если Не ЗначениеЗаполнено(СтрокаПоказатель.Показатель) Тогда
					Продолжить;
				КонецЕсли;

				СтруктураПоказателя = Новый Структура;
				СтруктураПоказателя.Вставить("Показатель", СтрокаПоказатель.Показатель);
				СтруктураПоказателя.Вставить("ЕдиницаИзмерения", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					СтрокаПоказатель.Показатель, "ЕдиницаИзмерения"));
				МассивПоказателей.Добавить(СтруктураПоказателя);
			КонецЦикла;

			
			Если ЗначениеЗаполнено(МассивПоказателей) И МассивПоказателей.Количество() > 0 Тогда 
				ПараметрыСозданияАнализа.Вставить(ИмяПараметраПоказатели, МассивПоказателей);
			Иначе 
				ПараметрыСозданияАнализа.Вставить(ИмяПараметраПоказатели, Неопределено);	
			КонецЕсли;

			
			ПараметрыСоздания = Новый Структура;
			ПараметрыСоздания.Вставить("ТочкаМаршрута", Объект.ТочкаМаршрута);
			ПараметрыСоздания.Вставить("ПроизводственныеСуткиРегистрации", Объект.ПроизводственныеСуткиРегистрации);
			ПараметрыСоздания.Вставить("ТаблицаРегистраций", Объект.ТаблицаРегистраций.Выгрузить());

			Документы.гкс_ФормированиеНомераПробы.ДокументПромежуточногоКомпозита(ПараметрыСоздания, 
				ПараметрыСозданияАнализа);
			
		Исключение

			ТекстОшибки = "Ошибка при создании документов: " + ОписаниеОшибки();
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			
			Возврат;
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполнение показателей из документа НормативнаяСертификацияНоменклатуры
// для назначения использования "Промежуточный композит"
//
&НаСервере
Процедура ЗаполнитьПоказателиИзНормативнойСертификацииНаСервере(ДопПараметры)
	
	Если ПроверитьПараметры() Тогда
		Возврат;
	КонецЕсли;
	
	ДобавленныеПоказатели = Новый Массив;
	
	НазначениеИспользования = Перечисления.гкс_НазначенияИспользованияКачества.ПромежуточныйКомпозит;
	
	// Запрос для получения показателей из нормативной сертификации
	Запрос = Новый Запрос;
	Запрос.Текст = гкс_РаботаСПоказателямиНоменклатуры.УстановитьТекстЗапросаОбъединенногоПолученияПоказателей();
	
	
	Запрос.УстановитьПараметр("Номенклатура", Объект.Номенклатура);
	Запрос.УстановитьПараметр("НазначениеИспользованияКачества", НазначениеИспользования);
	Запрос.УстановитьПараметр("ТочкаМаршрута", Объект.ТочкаМаршрута);
	Запрос.УстановитьПараметр("ВидПеревозки", Объект.ВидПеревозки);
	Запрос.УстановитьПараметр("ДатаНачала", ДопПараметры.НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", ДопПараметры.КонецПериода);
	Запрос.УстановитьПараметр("ДокументРегистрации", Документы.гкс_СРПВПеревеска.ПустаяСсылка());
	Запрос.УстановитьПараметр("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("УсиленныйКонтроль", Ложь);
	
	ТекстУсловияПоказательАнализа = "ИСТИНА";
	ТекстУсловияПоказательУсиленногоКонтроля = "ИСТИНА";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоказательАнализа", ТекстУсловияПоказательАнализа);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоказательУсиленногоКонтроля",
			ТекстУсловияПоказательУсиленногоКонтроля);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ДобавленныеПоказатели.Найти(Выборка.Показатель) = Неопределено Тогда
				ДобавленныеПоказатели.Добавить(Выборка.Показатель);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НормативныеПоказатели.ЗагрузитьЗначения(ДобавленныеПоказатели);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПоказателей(ВыбранныеЗначения, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеЗначения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбранныеЗначения) = Тип("Массив") Тогда
		Для Каждого ВыбранныйПоказатель Из ВыбранныеЗначения Цикл
			ДобавитьПоказательВТаблицу(ВыбранныйПоказатель);
		КонецЦикла;
	Иначе
		ДобавитьПоказательВТаблицу(ВыбранныеЗначения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьДобавленныеПоказатели()
	ДобавленныеПоказатели = Новый Массив;
	
	Для Каждого СтрокаПоказателя Из Объект.Показатели Цикл
		Если ЗначениеЗаполнено(СтрокаПоказателя.Показатель) Тогда
			ДобавленныеПоказатели.Добавить(СтрокаПоказателя.Показатель);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДобавленныеПоказатели;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьПоказательВТаблицу(Показатель)
	
	Для Каждого СтрокаПоказателя Из Объект.Показатели Цикл
		Если СтрокаПоказателя.Показатель = Показатель Тогда
			Возврат; // Показатель уже есть
		КонецЕсли;
	КонецЦикла;
	
	НоваяСтрока = Объект.Показатели.Добавить();
	НоваяСтрока.Показатель = Показатель;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыФормыДляНормативныхПоказателей(ДоступныеПоказатели)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	
	Если ДоступныеПоказатели.Количество() > 0 Тогда
		
		СписокОтбора = Новый СписокЗначений;
		Для Каждого Показатель Из ДоступныеПоказатели Цикл
			СписокОтбора.Добавить(Показатель);
		КонецЦикла;
		
		ОтборСтруктура = Новый Структура;
		ОтборСтруктура.Вставить("Ссылка", СписокОтбора);
		
		ФиксированныйОтбор = Новый ФиксированнаяСтруктура(ОтборСтруктура);
		ПараметрыФормы.Вставить("ФиксированныйОтбор", ФиксированныйОтбор);
		
		ПараметрыФормы.Вставить("Отбор", ОтборСтруктура);
		
	КонецЕсли;
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПроверкуНормативовПоказателя(ТекущиеДанные)
	
	НормативныеПоказателиМассив = НормативныеПоказатели.ВыгрузитьЗначения();
	Если НормативныеПоказателиМассив.Найти(ТекущиеДанные.Показатель) = Неопределено Тогда
		
		ТекстВопроса = "Не введен норматив на композиты. Продолжить создание лабораторного анализа?";
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ТекущиеДанные", ТекущиеДанные);
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеПодтвержденияДобавленияПоказателя", ЭтотОбъект, ДополнительныеПараметры),
			ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияДобавленияПоказателя(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
		Индекс = Объект.Показатели.Индекс(ТекущиеДанные);
		Если Индекс <> Неопределено Тогда
			Объект.Показатели.Удалить(Индекс);
		КонецЕсли;
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти