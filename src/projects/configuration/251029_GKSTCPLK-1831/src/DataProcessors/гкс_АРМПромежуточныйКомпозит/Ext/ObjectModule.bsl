#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Данные для списка (универсальная функция).
//
// Параметры:
//  Параметры - Структура - Параметры запроса
//  ТипСписка - Строка - "ПроизвольныйКомпозит" или "УсиленныйКонтроль"
//
// Возвращаемое значение:
//  ТаблицаЗначений - Данные для списка
Функция ДанныеДляСписка(Параметры, ТипСписка = "ПроизвольныйКомпозит") Экспорт

	Запрос = Новый Запрос;

	// Выбор текста запроса в зависимости от типа списка
	Если ТипСписка = "ПроизвольныйКомпозит" Тогда
		Запрос.Текст = ТекстЗапросаДанныеДляДетальныхЗаписей();
	ИначеЕсли ТипСписка = "УсиленныйКонтроль" Тогда
		Запрос.Текст = ТекстЗапросаДанныеДляУсиленногоКонтроля();
	Иначе
		// По умолчанию используем запрос для произвольного композита
		Запрос.Текст = ТекстЗапросаДанныеДляДетальныхЗаписей();
	КонецЕсли;

	// Формирование условия фильтрации (паттерн динамического текста условия)
	Если ТипСписка = "ПроизвольныйКомпозит" Тогда
		// Для произвольного композита - фильтрация по состоянию регистрации
		Если Параметры.ПоказыватьВсе Тогда
			ТекстУсловияПоСостоянию = "ИСТИНА";
		Иначе
			ТекстУсловияПоСостоянию = "СостоянияРегистрации.Состояние В (&СписокСостояний)";
			Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоСостоянию", ТекстУсловияПоСостоянию);

	ИначеЕсли ТипСписка = "УсиленныйКонтроль" Тогда
		// Для усиленного контроля - фильтрация по состоянию качества
		Если Параметры.ПоказыватьВсе Тогда
			ТекстУсловияПоСостояниюКачества = "ИСТИНА";
		Иначе
			ТекстУсловияПоСостояниюКачества = "ПоказателиУсиленногоКонтроляПоРегистрации.СостояниеКачества В " +
				"(ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияКачества.ПустаяСсылка), ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияКачества.ОтборПроб))";
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоСостояниюКачества", ТекстУсловияПоСостояниюКачества);

	КонецЕсли;

	// Формирование условий фильтрации по номенклатуре и контрагенту (паттерн динамического текста)
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		ТекстУсловияПоНоменклатуре = "РегистрацияНаПЛК.Номенклатура = &ОтборНоменклатура";
		Запрос.УстановитьПараметр("ОтборНоменклатура", Номенклатура);
	Иначе
		ТекстУсловияПоНоменклатуре = "ИСТИНА";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоНоменклатуре", ТекстУсловияПоНоменклатуре);

	Если ЗначениеЗаполнено(Контрагент) Тогда
		ТекстУсловияПоКонтрагенту = "РегистрацияНаПЛК.Контрагент = &ОтборКонтрагент";
		Запрос.УстановитьПараметр("ОтборКонтрагент", Контрагент);
	Иначе
		ТекстУсловияПоКонтрагенту = "ИСТИНА";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоКонтрагенту", ТекстУсловияПоКонтрагенту);

	// Общие параметры для обоих типов списков
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("НачалоПериода", Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", Параметры.КонецПериода);
	Запрос.УстановитьПараметр("ПроизводственныеСутки", Параметры.ПроизводственныеСуткиРегистрации);

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Данные для список промежуточные пробы.
// 
// Параметры:
//  Параметры Параметры
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные для список промежуточные пробы
Функция ДанныеДляСписокПромежуточныеПробы(Параметры) Экспорт

	Запрос	= Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеДляПромежуточныхПроб();  
	
	Запрос.УстановитьПараметр("СписокСостояний", СписокСостояний);
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("НачалоПериода", Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", Параметры.КонецПериода);
	Запрос.УстановитьПараметр("ПроизводственныеСутки", Параметры.ПроизводственныеСуткиРегистрации);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции	

// Данные для список регистрации промежуточных проб.
// 
// Параметры:
//  Параметры Параметры
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные для список регистрации промежуточных проб
Функция ДанныеДляСписокРегистрацииПромежуточныхПроб(Параметры) Экспорт

	Запрос	= Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеДляРегистрацииПромежуточныхПроб();  

	Запрос.УстановитьПараметр("ДокументФормированиеНомераПробы", Параметры.ДокументФормированиеНомераПробы);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаДанныеДляДетальныхЗаписей() 
	
	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СостоянияРегистрации.ДокументРегистрации КАК ДокументРегистрации,
	|	СостоянияРегистрации.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВТ_Регистрации
	|ИЗ
	|	РегистрСведений.гкс_СостоянияРегистрации.СрезПоследних({&КонецПериода}, ВидПеревозки = &ВидПеревозки
	|	И ТипРегистрации = ЗНАЧЕНИЕ(Перечисление.гкс_ТипРегистрации.Приемка)
	|	И Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТочкаМаршрута = &ТочкаМаршрута) КАК СостоянияРегистрации
	|ГДЕ
	|	&ТекстУсловияПоСостоянию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КомпозитныеПробы.ДокументРегистрации КАК ДокументРегистрации,
	|	КомпозитныеПробы.КомпозитнаяПроба КАК КомпозитнаяПроба,
	|	ЕСТЬNULL(СправочникНомераПроб.Код, """") КАК НомерПробы,
	|	КомпозитныеПробы.ПроизводственныеСуткиРегистрации КАК ПроизводственныеСутки
	|ПОМЕСТИТЬ ВТ_КомпозитныеПробы
	|ИЗ
	|	РегистрСведений.гкс_КомпозитныеПробы КАК КомпозитныеПробы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_Регистрации.ДокументРегистрации = КомпозитныеПробы.ДокументРегистрации
	|		И КомпозитныеПробы.ВидПеревозки = &ВидПеревозки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_ФормированиеНомераПробы КАК ФормированиеНомераПробы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_НомерПробы КАК СправочникНомераПроб
	|			ПО ФормированиеНомераПробы.НомерПробы = СправочникНомераПроб.Ссылка
	|		ПО КомпозитныеПробы.КомпозитнаяПроба = ФормированиеНомераПробы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Регистрации.ДокументРегистрации КАК ДокументРегистрации,
	|	ФормированиеНомераПробы.Ссылка КАК ЕдиничнаяПроба,
	|	ЕСТЬNULL(СправочникНомераПроб.Код, """") КАК НомерПробы
	|ПОМЕСТИТЬ ВТ_ЕдиничныеПробы
	|ИЗ
	|	Документ.гкс_ФормированиеНомераПробы.СписокРегистраций КАК ФормированиеНомераПробыСписокРегистраций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_Регистрации.ДокументРегистрации = ФормированиеНомераПробыСписокРегистраций.ДокументРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_ФормированиеНомераПробы КАК ФормированиеНомераПробы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_НомерПробы КАК СправочникНомераПроб
	|			ПО ФормированиеНомераПробы.НомерПробы = СправочникНомераПроб.Ссылка
	|		ПО ФормированиеНомераПробыСписокРегистраций.Ссылка = ФормированиеНомераПробы.Ссылка
	|		И ФормированиеНомераПробы.Проведен
	|		И ФормированиеНомераПробы.ТипПробы = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыПроб.Единичная)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияНаПЛК.Ссылка КАК ДокументРегистрации,
	|	РегистрацияНаПЛК.Дата КАК ДатаРегистрации,
	|	РегистрацияНаПЛК.Организация КАК Организация,
	|	РегистрацияНаПЛК.ВидПеревозки КАК ВидПеревозки,
	|	РегистрацияНаПЛК.ТочкаМаршрута КАК ПЛК,
	|	РегистрацияНаПЛК.Контрагент КАК Контрагент,
	|	РегистрацияНаПЛК.Номенклатура КАК Номенклатура,
	|	РегистрацияНаПЛК.ТранспортноеСредство КАК ТранспортноеСредство,
	|	РегистрацияНаПЛК.ПунктПогрузки КАК ПунктПогрузки,
	|	ЕСТЬNULL(НаправлениеНаРазгрузку.Ссылка, ЗНАЧЕНИЕ(Документ.гкс_НаправлениеНаРазгрузку.ПустаяСсылка)) КАК
	|		НаправлениеНаРазгрузку,
	|	ЕСТЬNULL(ВТ_КомпозитныеПробы.КомпозитнаяПроба, ЗНАЧЕНИЕ(Документ.гкс_ФормированиеНомераПробы.ПустаяСсылка)) КАК
	|		ДокументКомпозитнаяПроба,
	|	ЕСТЬNULL(ВТ_КомпозитныеПробы.НомерПробы, NULL) КАК НомерПромежуточногоКомпозита,
	|	ЕСТЬNULL(ВТ_ЕдиничныеПробы.ЕдиничнаяПроба, ЗНАЧЕНИЕ(Документ.гкс_ФормированиеНомераПробы.ПустаяСсылка)) КАК
	|		ФормированиеПробы,
	|	ЕСТЬNULL(ВТ_ЕдиничныеПробы.НомерПробы, NULL) КАК НомерПробы,
	|	ЕСТЬNULL(ВТ_Регистрации.Состояние, ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияРегистрации.ПустаяСсылка)) КАК Состояние,
	|	ЕСТЬNULL(СвязьРегистрацииИТранспортногоДокумента.ТранспортныйДокумент.ПунктПогрузки.КодСтанцииЖД,
	|		ЗНАЧЕНИЕ(Справочник.гкс_КлассификаторСтанцийЖД.ПустаяСсылка)) КАК СтанцияОтправления,
	|	ЕСТЬNULL(СвязьРегистрацииИТранспортногоДокумента.ТранспортныйДокумент.ПунктПогрузки.КодСтанцииЖД.НаименованиеПолное,
	|		СТРОКА(10)) КАК СтанцияОтправленияПредставление
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
	|		ПО ВТ_Регистрации.ДокументРегистрации = РегистрацияНаПЛК.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_НаправлениеНаРазгрузку КАК НаправлениеНаРазгрузку
	|		ПО ВТ_Регистрации.ДокументРегистрации = НаправлениеНаРазгрузку.ДокументРегистрации
	|		И НаправлениеНаРазгрузку.Проведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеВъезд
	|		ПО ВТ_Регистрации.ДокументРегистрации = ВзвешиваниеВъезд.ДокументРегистрации
	|		И ВзвешиваниеВъезд.Проведен
	|		И ВзвешиваниеВъезд.ТипВзвешивания = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыВзвешивания.Въезд)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеВыезд
	|		ПО ВТ_Регистрации.ДокументРегистрации = ВзвешиваниеВыезд.ДокументРегистрации
	|		И ВзвешиваниеВыезд.Проведен
	|		И ВзвешиваниеВыезд.ТипВзвешивания = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыВзвешивания.Выезд)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КомпозитныеПробы КАК ВТ_КомпозитныеПробы
	|		ПО ВТ_Регистрации.ДокументРегистрации = ВТ_КомпозитныеПробы.ДокументРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕдиничныеПробы КАК ВТ_ЕдиничныеПробы
	|		ПО ВТ_Регистрации.ДокументРегистрации = ВТ_ЕдиничныеПробы.ДокументРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гкс_СвязьРегистрацииИТранспортногоДокумента КАК
	|			СвязьРегистрацииИТранспортногоДокумента
	|		ПО ВТ_Регистрации.ДокументРегистрации = СвязьРегистрацииИТранспортногоДокумента.ДокументРегистрации
	|ГДЕ
	|	&ТекстУсловияПоНоменклатуре
	|	И &ТекстУсловияПоКонтрагенту
	|	И ВЫБОР
	|		КОГДА НЕ ВТ_КомпозитныеПробы.ДокументРегистрации ЕСТЬ NULL
	|		И НЕ ВТ_КомпозитныеПробы.ПроизводственныеСутки = &ПроизводственныеСутки
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ";
	
КонецФункции

Функция ТекстЗапросаДанныеДляПромежуточныхПроб()
	
	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СостоянияРегистрации.ДокументРегистрации КАК ДокументРегистрации,
	|	СостоянияРегистрации.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВТ_Регистрации
	|ИЗ
	|	РегистрСведений.гкс_СостоянияРегистрации.СрезПоследних({&КонецПериода}, ВидПеревозки = &ВидПеревозки
	|	И ТипРегистрации = ЗНАЧЕНИЕ(Перечисление.гкс_ТипРегистрации.Приемка)
	|	И Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТочкаМаршрута = &ТочкаМаршрута) КАК СостоянияРегистрации
	|ГДЕ
	|	СостоянияРегистрации.Состояние В (&СписокСостояний)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РегистрСведенийКомпозитныеПробы.КомпозитнаяПроба КАК ДокументФормированиеНомераПробы,
	|	МАКСИМУМ(ЛабораторныйАнализ.Ссылка) КАК ЛабораторныйАнализ
	|ПОМЕСТИТЬ ВТ_ПервичныеДанныеПоРегистрации
	|ИЗ
	|	РегистрСведений.гкс_КомпозитныеПробы КАК РегистрСведенийКомпозитныеПробы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|		ПО РегистрСведенийКомпозитныеПробы.КомпозитнаяПроба = ЛабораторныйАнализ.гкс_ДокументРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО РегистрСведенийКомпозитныеПробы.ДокументРегистрации = ВТ_Регистрации.ДокументРегистрации
	|ГДЕ
	|	РегистрСведенийКомпозитныеПробы.ТочкаМаршрута = &ТочкаМаршрута
	|	И РегистрСведенийКомпозитныеПробы.ПроизводственныеСуткиРегистрации = &ПроизводственныеСутки
	|	И РегистрСведенийКомпозитныеПробы.ВидПеревозки = &ВидПеревозки
	|СГРУППИРОВАТЬ ПО
	|	РегистрСведенийКомпозитныеПробы.КомпозитнаяПроба
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РегистрСведенийКомпозитныеПробы.КомпозитнаяПроба КАК ДокументФормированиеНомераПробы,
	|	МАКСИМУМ(ЛабораторныйАнализ.Ссылка) КАК ЛабораторныйАнализ
	|ПОМЕСТИТЬ ВТ_ПервичныеДанныеПромежуточныйКомпозит
	|ИЗ
	|	РегистрСведений.гкс_КомпозитныеПробы КАК РегистрСведенийКомпозитныеПробы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|		ПО РегистрСведенийКомпозитныеПробы.КомпозитнаяПроба = ЛабораторныйАнализ.гкс_ДокументРегистрации
	|ГДЕ
	|	РегистрСведенийКомпозитныеПробы.ТочкаМаршрута = &ТочкаМаршрута
	|	И РегистрСведенийКомпозитныеПробы.ПроизводственныеСуткиРегистрации = &ПроизводственныеСутки
	|	И РегистрСведенийКомпозитныеПробы.ВидПеревозки = &ВидПеревозки
	|	И
	|		ЛабораторныйАнализ.гкс_НазначениеИспользованияКачества = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.ПромежуточныйКомпозит)
	|СГРУППИРОВАТЬ ПО
	|	РегистрСведенийКомпозитныеПробы.КомпозитнаяПроба
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПервичныеДанныеПоРегистрации.ДокументФормированиеНомераПробы КАК ДокументФормированиеНомераПробы,
	|	ВТ_ПервичныеДанныеПоРегистрации.ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|ПОМЕСТИТЬ ВТ_ПервичныеДанные
	|ИЗ
	|	ВТ_ПервичныеДанныеПоРегистрации КАК ВТ_ПервичныеДанныеПоРегистрации
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТ_ПервичныеДанныеПромежуточныйКомпозит.ДокументФормированиеНомераПробы,
	|	ВТ_ПервичныеДанныеПромежуточныйКомпозит.ЛабораторныйАнализ
	|ИЗ
	|	ВТ_ПервичныеДанныеПромежуточныйКомпозит КАК ВТ_ПервичныеДанныеПромежуточныйКомпозит
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПервичныеДанные.ДокументФормированиеНомераПробы КАК ДокументКомпозитнаяПроба,
	|	ВТ_ПервичныеДанные.ЛабораторныйАнализ КАК ПромежуточныйКомпозит,
	|	ВЫБОР
	|		КОГДА
	|			ВТ_ПервичныеДанные.ЛабораторныйАнализ.Статус = ЗНАЧЕНИЕ(Перечисление.гкс_СтатусыЛабораторногоАнализа.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.гкс_СтатусыЛабораторногоАнализа.ОтборПробы)
	|		ИНАЧЕ ВТ_ПервичныеДанные.ЛабораторныйАнализ.Статус
	|	КОНЕЦ КАК Статус,
	|	ВТ_ПервичныеДанные.ЛабораторныйАнализ.Номер КАК ЛабораторныйАнализНомер,
	|	ЕСТЬNULL(Номенклатура.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ВТ_ПервичныеДанные.ДокументФормированиеНомераПробы.НомерПробы КАК НомерПробы
	|ИЗ
	|	ВТ_ПервичныеДанные КАК ВТ_ПервичныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО ВТ_ПервичныеДанные.ЛабораторныйАнализ.Номенклатура = Номенклатура.Ссылка";
	
КонецФункции

Функция ТекстЗапросаДанныеДляРегистрацииПромежуточныхПроб()
	
	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрСведенийКомпозитныеПробы.ДокументРегистрации КАК ДокументРегистрации
	|ИЗ
	|	РегистрСведений.гкс_КомпозитныеПробы КАК РегистрСведенийКомпозитныеПробы
	|ГДЕ
	|	РегистрСведенийКомпозитныеПробы.КомпозитнаяПроба = &ДокументФормированиеНомераПробы";
	 	
КонецФункции

Функция ТекстЗапросаДанныеДляУсиленногоКонтроля()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоказателиУсиленногоКонтроляПоРегистрации.ДокументРегистрации КАК ДокументРегистрации,
	|	ПоказателиУсиленногоКонтроляПоРегистрации.Показатель КАК Показатель,
	|	ПоказателиУсиленногоКонтроляПоРегистрации.СостояниеКачества КАК СостояниеКачества
	|ПОМЕСТИТЬ ВТ_ПоказателиУсиленногоКонтроля
	|ИЗ
	|	РегистрСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации КАК ПоказателиУсиленногоКонтроляПоРегистрации
	|ГДЕ
	|	ПоказателиУсиленногоКонтроляПоРегистрации.НазначениеИспользования = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.ПромежуточныйКомпозит)
	|	И &ТекстУсловияПоСостояниюКачества
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СостоянияРегистрации.ДокументРегистрации КАК ДокументРегистрации,
	|	СостоянияРегистрации.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВТ_РегистрацииПоОтборам
	|ИЗ
	|	РегистрСведений.гкс_СостоянияРегистрации.СрезПоследних(, ВидПеревозки = &ВидПеревозки
	|	И ТипРегистрации = ЗНАЧЕНИЕ(Перечисление.гкс_ТипРегистрации.Приемка)
	|	И Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТочкаМаршрута = &ТочкаМаршрута) КАК СостоянияРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СоставГруппТранспорта.РегистрацияТранспорта КАК ДокументРегистрации,
	|	СоставГруппТранспорта.ГруппаТС КАК ГруппаТС
	|ПОМЕСТИТЬ ВТ_ГруппыТС
	|ИЗ
	|	РегистрСведений.гкс_СоставГруппТранспорта КАК СоставГруппТранспорта
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РегистрацииПоОтборам КАК ВТ_РегистрацииПоОтборам
	|		ПО СоставГруппТранспорта.РегистрацияТранспорта = ВТ_РегистрацииПоОтборам.ДокументРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ПоказателиУсиленногоКонтроля.Показатель КАК Показатель,
	|	ВТ_ПоказателиУсиленногоКонтроля.СостояниеКачества КАК СостояниеКачества,
	|	ВТ_ПоказателиУсиленногоКонтроля.ДокументРегистрации КАК ДокументРегистрации,
	|	ВТ_РегистрацииПоОтборам.Состояние КАК СостояниеРегистрации,
	|	ЕСТЬNULL(ВТ_ГруппыТС.ГруппаТС, ЗНАЧЕНИЕ(Справочник.гкс_ГруппыТС.ПустаяСсылка)) КАК ГруппаТС
	|ПОМЕСТИТЬ ВТ_Регистрации
	|ИЗ
	|	ВТ_ПоказателиУсиленногоКонтроля КАК ВТ_ПоказателиУсиленногоКонтроля
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РегистрацииПоОтборам КАК ВТ_РегистрацииПоОтборам
	|		ПО ВТ_ПоказателиУсиленногоКонтроля.ДокументРегистрации = ВТ_РегистрацииПоОтборам.ДокументРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ГруппыТС КАК ВТ_ГруппыТС
	|		ПО ВТ_ПоказателиУсиленногоКонтроля.ДокументРегистрации = ВТ_ГруппыТС.ДокументРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КомпозитныеПробы.ДокументРегистрации КАК ДокументРегистрации,
	|	КомпозитныеПробы.КомпозитнаяПроба КАК КомпозитнаяПроба,
	|	ЕСТЬNULL(СправочникНомераПроб.Код, """") КАК НомерКомпозитнойПробы,
	|	КомпозитныеПробы.ПроизводственныеСуткиРегистрации КАК ПроизводственныеСутки
	|ПОМЕСТИТЬ ВТ_КомпозитныеПробы
	|ИЗ
	|	РегистрСведений.гкс_КомпозитныеПробы КАК КомпозитныеПробы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_Регистрации.ДокументРегистрации = КомпозитныеПробы.ДокументРегистрации
	|		И КомпозитныеПробы.ВидПеревозки = &ВидПеревозки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_ФормированиеНомераПробы КАК ФормированиеНомераПробы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_НомерПробы КАК СправочникНомераПроб
	|			ПО ФормированиеНомераПробы.НомерПробы = СправочникНомераПроб.Ссылка
	|		ПО КомпозитныеПробы.КомпозитнаяПроба = ФормированиеНомераПробы.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_Регистрации.ДокументРегистрации КАК ДокументРегистрации,
	|	ФормированиеНомераПробы.Ссылка КАК ЕдиничнаяПроба,
	|	ЕСТЬNULL(СправочникНомераПроб.Код, """") КАК НомерПробы
	|ПОМЕСТИТЬ ВТ_ЕдиничныеПробы
	|ИЗ
	|	Документ.гкс_ФормированиеНомераПробы.СписокРегистраций КАК ФормированиеНомераПробыСписокРегистраций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_Регистрации.ДокументРегистрации = ФормированиеНомераПробыСписокРегистраций.ДокументРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_ФормированиеНомераПробы КАК ФормированиеНомераПробы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_НомерПробы КАК СправочникНомераПроб
	|			ПО ФормированиеНомераПробы.НомерПробы = СправочникНомераПроб.Ссылка
	|		ПО ФормированиеНомераПробыСписокРегистраций.Ссылка = ФормированиеНомераПробы.Ссылка
	|		И ФормированиеНомераПробы.Проведен
	|		И ФормированиеНомераПробы.ТипПробы = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыПроб.Единичная)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияНаПЛК.Ссылка КАК ДокументРегистрации,
	|	РегистрацияНаПЛК.Дата КАК ДатаРегистрации,
	|	РегистрацияНаПЛК.Номенклатура КАК Номенклатура,
	|	РегистрацияНаПЛК.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ЕСТЬNULL(ВТ_ЕдиничныеПробы.НомерПробы, """") КАК НомерПробы,
	|	ЕСТЬNULL(ВТ_КомпозитныеПробы.НомерКомпозитнойПробы, """") КАК НомерКомпозитнойПробы,
	|	РегистрацияНаПЛК.Контрагент КАК Контрагент,
	|	РегистрацияНаПЛК.Организация КАК Организация,
	|	РегистрацияНаПЛК.ВидПеревозки КАК ВидПеревозки,
	|	РегистрацияНаПЛК.ТочкаМаршрута КАК ПЛК,
	|	ЕСТЬNULL(ВТ_КомпозитныеПробы.КомпозитнаяПроба, ЗНАЧЕНИЕ(Документ.гкс_ФормированиеНомераПробы.ПустаяСсылка)) КАК
	|		ДокументКомпозитнаяПроба,
	|	ЕСТЬNULL(ВТ_ЕдиничныеПробы.ЕдиничнаяПроба, ЗНАЧЕНИЕ(Документ.гкс_ФормированиеНомераПробы.ПустаяСсылка)) КАК
	|		ФормированиеПробы,
	|	ВТ_Регистрации.СостояниеРегистрации КАК СостояниеРегистрации,
	|	ВТ_Регистрации.Показатель КАК Показатель,
	|	ВТ_Регистрации.СостояниеКачества КАК СостояниеКачества,
	|	ВТ_Регистрации.ГруппаТС КАК ГруппаТС,
	|	ЛОЖЬ КАК Пометка,
	|	ЕСТЬNULL(СвязьРегистрацииИТранспортногоДокумента.ТранспортныйДокумент.ПунктПогрузки.КодСтанцииЖД,
	|		ЗНАЧЕНИЕ(Справочник.гкс_КлассификаторСтанцийЖД.ПустаяСсылка)) КАК СтанцияОтправления,
	|	ЕСТЬNULL(СвязьРегистрацииИТранспортногоДокумента.ТранспортныйДокумент.ПунктПогрузки.КодСтанцииЖД.НаименованиеПолное,
	|		СТРОКА(10)) КАК СтанцияОтправленияПредставление
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
	|		ПО ВТ_Регистрации.ДокументРегистрации = РегистрацияНаПЛК.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КомпозитныеПробы КАК ВТ_КомпозитныеПробы
	|		ПО ВТ_Регистрации.ДокументРегистрации = ВТ_КомпозитныеПробы.ДокументРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕдиничныеПробы КАК ВТ_ЕдиничныеПробы
	|		ПО ВТ_Регистрации.ДокументРегистрации = ВТ_ЕдиничныеПробы.ДокументРегистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гкс_СвязьРегистрацииИТранспортногоДокумента КАК
	|			СвязьРегистрацииИТранспортногоДокумента
	|		ПО ВТ_Регистрации.ДокументРегистрации = СвязьРегистрацииИТранспортногоДокумента.ДокументРегистрации
	|ГДЕ
	|	&ТекстУсловияПоНоменклатуре
	|	И &ТекстУсловияПоКонтрагенту
	|	И ВЫБОР
	|		КОГДА НЕ ВТ_КомпозитныеПробы.ДокументРегистрации ЕСТЬ NULL
	|		И НЕ ВТ_КомпозитныеПробы.ПроизводственныеСутки = &ПроизводственныеСутки
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ";
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли