
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВыбранныеРегистрации.ЗагрузитьЗначения(Параметры.ВыбранныеРегистрации);
	ТипДопуска = Параметры.ТипДопуска;
	НовоеЗначение = Параметры.ТипДопуска;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)

	МинимальноДопустимаяДлинаКомментария = 5;
	Если СтрДлина(Комментарий) < МинимальноДопустимаяДлинаКомментария Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Комментарий не может быть меньше %1 символов'"), 
						МинимальноДопустимаяДлинаКомментария);
						
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, ,"Комментарий");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ИзменитьДопускиНаСервере();
	
    ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Вставить("ВыводитьСообщения", Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОбработкиДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Статус = "Выполнено" Тогда
		
		Закрыть();
	
	ИначеЕсли Результат.Статус = "Ошибка" Тогда	
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Произошла ошибка при изменении допуска %1 %2'"), 
			Символы.ПС, Результат.КраткоеПредставлениеОшибки);
			
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИзменитьДопускиНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	//ПараметрыВыполнения.Вставить("ОжидатьЗавершение", Неопределено);
	//ПараметрыВыполнения.Вставить("ЗапуститьНеВФоне", Истина);
	
	ПараметрыЗаполнения = Новый Структура;	
	ПараметрыЗаполнения.Вставить("ДопускКВскрытию", 	ТипДопуска);
	ПараметрыЗаполнения.Вставить("ВскрытиеЗапрещено", 	НовоеЗначение);
	ПараметрыЗаполнения.Вставить("Комментарий", 		Комментарий);
	ПараметрыЗаполнения.Вставить("Ответственный", 		Пользователи.ТекущийПользователь());
	ПараметрыЗаполнения.Вставить("Период", 				ТекущаяДатаСеанса());
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
								"Обработки.гкс_ПриемкаТранспорта.ГрупповоеИзменениеДопусков",
								ПараметрыЗаполнения, ВыбранныеРегистрации.ВыгрузитьЗначения());				
КонецФункции

#КонецОбласти



