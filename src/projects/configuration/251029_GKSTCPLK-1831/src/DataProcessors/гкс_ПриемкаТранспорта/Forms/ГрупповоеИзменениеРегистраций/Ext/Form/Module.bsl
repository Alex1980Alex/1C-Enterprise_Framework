#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = Параметры.Номенклатура;
	ВыбранныеРегистрации.ЗагрузитьЗначения(Параметры.ВыбранныеРегистрации);
	
	УстановитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспНоменклатураПриИзменении(Элемент)
	
	УстановитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытия.Вставить("Номенклатура", Номенклатура);
	
	ФормаВыбора = ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", 
		ПараметрыОткрытия, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ВидЭталоныКачества = гкс_ОбщегоНазначенияВызовСервера.ПроизвольныйПредопределенныйОбъект(
		"ВидыНоменклатурыЭталоныКачества");
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ФормаВыбора.Список, 
		"ВидНоменклатуры", ВидЭталоныКачества, ВидСравненияКомпоновкиДанных.НеРавно, , 
		Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
			
КонецПроцедуры

&НаКлиенте
Процедура ИспКомментарийПриИзменении(Элемент)
	
	УстановитьВидимостьДоступностьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбработатьДокументы(Команда)
	
	Если Не ИспНоменклатура И Не ИспКомментарий Тогда
		Возврат;	
	КонецЕсли;	
		
	ДлительнаяОперация = ОбработатьДокументыНаСервере();
	
    ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Вставить("ВыводитьСообщения", Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОбработкиДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ТекстСообщения = НСтр("ru = 'Обработка документов завершена'");		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	
	ИначеЕсли Результат.Статус = "Ошибка" Тогда	
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Произошла ошибка при изменении документов %1 %2'"), 
			Символы.ПС, Результат.КраткоеПредставлениеОшибки);
			
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыход(Команда)	
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементовФормы()
		
    Элементы.ОбработатьДокументы.Доступность = ИспНоменклатура ИЛИ ИспКомментарий;
	Элементы.Номенклатура.Доступность = ИспНоменклатура;
	Элементы.Комментарий.Доступность = ИспКомментарий;

КонецПроцедуры

&НаСервере
Функция ОбработатьДокументыНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	//ПараметрыВыполнения.Вставить("ОжидатьЗавершение", Неопределено);
	//ПараметрыВыполнения.Вставить("ЗапуститьНеВФоне", Истина);
	
	ПараметрыЗаполнения = Новый Структура;	
	Если ИспНоменклатура Тогда
		ПараметрыЗаполнения.Вставить("Номенклатура", Номенклатура);
	КонецЕсли;
	
	Если ИспКомментарий Тогда
		ПараметрыЗаполнения.Вставить("Комментарий", Комментарий);
	КонецЕсли;
	
	ПараметрыЗаполнения.Вставить("ВыбранныеРегистрации", ВыбранныеРегистрации.ВыгрузитьЗначения()); 
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
								"Обработки.гкс_ПриемкаТранспорта.ГрупповоеИзменениеРегистраций",
								ПараметрыЗаполнения);				
КонецФункции

#КонецОбласти