#Область ОписаниеПеременных

&НаКлиенте
Перем ИнтервалАвтообновления;

&НаКлиенте
Перем ТекущаяАктивнаяСтраница;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьРеквизитыФормы();
	Элементы.НастройкиУсиленногоКонтроляКачества.Обновить();
	Элементы.НастройкиОтключенияВходногоКонтроляКачества.Обновить();
	УстановитьВидимостьДоступность();
	
	ИнициализироватьНастройкиАвтообновления();
	
КонецПроцедуры 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УправлениеФормой();
	
	// Подключение автообновления настроек каждую минуту по аналогии
	ИнтервалАвтообновления = 60;
	ПодключитьОбработчикОжидания("АвтообновлениеНастроек", ИнтервалАвтообновления, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборАктивностьПриИзменении(Элемент)
	УправлениеФормой();
	ПрименитьФильтрАктивностиДляАктивнойСтраницы();
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
	УправлениеФормой(); 
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПЛКПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ГруппаНастройкиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ТекущаяАктивнаяСтраница = ТекущаяСтраница;
	
	ПрименитьФильтрАктивностиДляАктивнойСтраницы();
	
	ОтключитьОбработчикОжидания("АвтообновлениеНастроек");
	ПодключитьОбработчикОжидания("АвтообновлениеНастроек", ИнтервалАвтообновления, Ложь);
	
КонецПроцедуры

#КонецОбласти  

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтключенияВходногоКонтроля(Команда)
	ПараметрыОткрытия = 
		Новый Структура("ВидПеревозки, ТочкаМаршрута, ОткрытаИзОбработки", 
			ОтборВидПеревозки, Объект.ТочкаМаршрута, Истина);

	ОткрытьФорму("РегистрСведений.гкс_НастройкиОтключенияВходногоКонтроляКачества.ФормаЗаписи", ПараметрыОткрытия,
		ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено,
		ОписаниеОповещенияОЗакрытииОтключенияВходногоКонтроля(), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокОтключенияВходногоКонтроля(Команда)
	УправлениеФормой();
КонецПроцедуры

#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьРеквизитыФормы()
	ОтборАктивность = Истина;
	ОтборВидПеревозки = Перечисления.гкс_ТипыТранспортныхСредствДоставки.Автомобиль;
	ИнициализироватьОтборТочкаМаршрута();
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	Если ЗначениеЗаполнено(Объект.ТочкаМаршрута) 
		И ЗначениеЗаполнено(ОтборВидПеревозки) Тогда
		УстановитьПараметрыДинамическихСписков();
		Элементы.НастройкиУсиленногоКонтроляКачества.Обновить();
	КонецЕсли;
	
	УправлениеФормойОтключенияВходногоКонтроля();
КонецПроцедуры	

&НаКлиенте
Процедура УправлениеФормойОтключенияВходногоКонтроля()
	Если ЗначениеЗаполнено(Объект.ТочкаМаршрута) 
		И ЗначениеЗаполнено(ОтборВидПеревозки) Тогда
		УстановитьПараметрыДинамическихСписковОтключенияВходногоКонтроля();
		Элементы.НастройкиОтключенияВходногоКонтроляКачества.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОтборТочкаМаршрута()
	
	ИспользоватьНесколькоАЛЦ = ПолучитьФункциональнуюОпцию("гкс_ИспользоватьНесколькоАЛЦ");
	
	Элементы.ОтборТочкаМаршрута.Доступность = ИспользоватьНесколькоАЛЦ;
	Элементы.ОтборТочкаМаршрута.РежимВыбораИзСписка = ИспользоватьНесколькоАЛЦ;
	
	АЛЦПользователя = Новый Массив;
	Объект.ТочкаМаршрута = 
		гкс_ПриемкаТранспорта.ПЛКизНастроекБазыПользователя(ИспользоватьНесколькоАЛЦ, АЛЦПользователя);
	Если ИспользоватьНесколькоАЛЦ
		И ЗначениеЗаполнено(АЛЦПользователя) Тогда
		СписокВыбора = Элементы.ОтборТочкаМаршрута.СписокВыбора;
		СписокВыбора.ЗагрузитьЗначения(АЛЦПользователя);
	КонецЕсли;	
  	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыДинамическихСписков()
	
	Если ЗначениеЗаполнено(ОтборВидПеревозки) Тогда
			
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиУсиленногоКонтроляКачества,
			"ВидПеревозки", ОтборВидПеревозки);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиУсиленногоКонтроляКачества,
			"ТочкаМаршрута", Объект.ТочкаМаршрута);
										
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиУсиленногоКонтроляКачества,
			"Активен", ОтборАктивность);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиУсиленногоКонтроляКачества,
			"ПроизводственныеСутки", ПроизводственныеСуткиПоДате());
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыДинамическихСписковОтключенияВходногоКонтроля()
	
	Если ЗначениеЗаполнено(ОтборВидПеревозки) Тогда
			
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтключенияВходногоКонтроляКачества,
			"ВидПеревозки", ОтборВидПеревозки);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтключенияВходногоКонтроляКачества,
			"ТочкаМаршрута", Объект.ТочкаМаршрута);
										
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтключенияВходногоКонтроляКачества,
			"Активен", ОтборАктивность);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(НастройкиОтключенияВходногоКонтроляКачества,
			"ПроизводственныеСутки", ПроизводственныеСуткиПоДате());
		
	КонецЕсли;
		
КонецПроцедуры
 
&НаКлиенте
Функция ОписаниеОповещенияОЗакрытииОтключенияВходногоКонтроля()
	Возврат Новый ОписаниеОповещения("ПослеЗакрытияФормыЗаписиОтключенияВходногоКонтроля", ЭтотОбъект);
КонецФункции
 
&НаКлиенте
Процедура ПослеЗакрытияФормыЗаписиОтключенияВходногоКонтроля(Результат, ДопПараметры) Экспорт
	УправлениеФормойОтключенияВходногоКонтроля();
КонецПроцедуры
 

&НаСервереБезКонтекста
Функция ПроизводственныеСуткиПоДате() 
	Возврат
		гкс_ПриемкаТранспорта.ПроизводственныеСуткиПоДата(ТекущаяДатаСеанса());
КонецФункции		

&НаСервере
Процедура УстановитьВидимостьДоступность()
	ЭтоЖД = Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЭтоЖДПеревозка(ОтборВидПеревозки);
	Элементы.НастройкиУсиленногоКонтроляКачестваЖДСтанцияНаименование.Видимость = ЭтоЖД;
	Элементы.НастройкиУсиленногоКонтроляКачестваПунктПогрузки.Видимость = Не ЭтоЖД;
	
	ДоступностьРедактирования = Пользователи.РолиДоступны("гкс_НастройкаУсиленногоКонтроля");
	Элементы.НастройкиУсиленногоКонтроляКачестваСоздать.Доступность = ДоступностьРедактирования;
	Элементы.НастройкиУсиленногоКонтроляКачества.ТолькоПросмотр = Не ДоступностьРедактирования;
	
	ИспользоватьОтключениеВходногоКонтроля 
		= ПолучитьФункциональнуюОпцию("гкс_ИспользоватьОтключениеВходногоКонтроляКачества");
	Элементы.ГруппаОтключенияВходногоКонтроля.Видимость = ИспользоватьОтключениеВходногоКонтроля;
	
	Если ИспользоватьОтключениеВходногоКонтроля Тогда
		Элементы.НастройкиОтключенияВходногоКонтроляКачестваЖДСтанция.Видимость = ЭтоЖД;
		Элементы.НастройкиОтключенияВходногоКонтроляКачестваПунктПогрузки.Видимость = Не ЭтоЖД;
		
		Элементы.НастройкиОтключенияВходногоКонтроляКачестваСоздатьОтключенияВходногоКонтроля.Доступность 
			= ДоступностьРедактирования;
		Элементы.НастройкиОтключенияВходногоКонтроляКачества.ТолькоПросмотр = Не ДоступностьРедактирования;
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Процедура ИнициализироватьНастройкиАвтообновления()
	
	// Инициализация соответствий страниц и списков настроек
	// для автоматического обновления настроек активной страницы
	
	// Установка начальных параметров для фильтрации активности
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НастройкиУсиленногоКонтроляКачества, "Активен", ОтборАктивность);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		НастройкиОтключенияВходногоКонтроляКачества, "Активен", ОтборАктивность);
	
КонецПроцедуры

&НаКлиенте
Процедура Создать(Команда)                                                            
	
	ПараметрыОткрытия = Новый Структура("ВидПеревозки, ТочкаМаршрута, ОткрытаИзОбработки", 
			ОтборВидПеревозки, Объект.ТочкаМаршрута, Истина);

		
	ОткрытьФорму("РегистрСведений.гкс_НастройкиУсиленногоКонтроляКачества.ФормаЗаписи", 
					ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено,
					ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура НастройкиУсиленногоКонтроляКачестваПередУдалением(Элемент, Отказ)
	ОбщегоНазначенияКлиент.СообщитьПользователю(
		НСтр("ru = 'Удаление правила запрещено. Необходимо снять активность.'"), , , , Отказ);
КонецПроцедуры

#Область СлужебныеПроцедурыИФункцииAвтообновления

// Процедура автообновления настроек активной страницы
&НаКлиенте
Процедура АвтообновлениеНастроек()
	
	АктивнаяСтраница = Элементы.ГруппаНастройки.ТекущаяСтраница;
	
	Если АктивнаяСтраница = Элементы.ГруппаУсиленныйКонтроль Тогда
		Элементы.НастройкиУсиленногоКонтроляКачества.Обновить();
	ИначеЕсли АктивнаяСтраница = Элементы.ГруппаОтключенияВходногоКонтроля Тогда
		Элементы.НастройкиОтключенияВходногоКонтроляКачества.Обновить();
	КонецЕсли;
	
	ПрименитьФильтрАктивностиДляАктивнойСтраницы();
	
КонецПроцедуры

// Процедура применения фильтра активности для активной страницы  
&НаКлиенте  
Процедура ПрименитьФильтрАктивностиДляАктивнойСтраницы()
	
	АктивнаяСтраница = Элементы.ГруппаНастройки.ТекущаяСтраница;
	
	Если АктивнаяСтраница = Элементы.ГруппаУсиленныйКонтроль Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			НастройкиУсиленногоКонтроляКачества, "Активен", ОтборАктивность);
	ИначеЕсли АктивнаяСтраница = Элементы.ГруппаОтключенияВходногоКонтроля Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			НастройкиОтключенияВходногоКонтроляКачества, "Активен", ОтборАктивность);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
