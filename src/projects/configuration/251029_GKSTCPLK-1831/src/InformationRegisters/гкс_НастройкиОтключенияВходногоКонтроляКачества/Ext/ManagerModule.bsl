#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ТочкаМаршрута)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Проверяет, подходит ли регистрация под правила отключения входного контроля
//
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - документ регистрации транспорта
//
// Возвращаемое значение:
//  Булево - Истина, если регистрация подходит под правила отключения
//
Функция ПроверитьПрименимостьПравилаОтключения(ДокументРегистрации, МестнаяДата) Экспорт
	
	ПустыеТМ = Новый СписокЗначений;
	ПустыеТМ.Добавить(Справочники.гкс_ТочкиМаршрута.ПустаяСсылка());
	ПустыеТМ.Добавить(Справочники.гкс_КлассификаторСтанцийЖД.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПроверкиПрименимостиПравилаОтключения();
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	Запрос.УстановитьПараметр("ЖДПеревозка", Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЖДТранспорт);
	Запрос.УстановитьПараметр("ПустоеТС", Справочники.ТранспортныеСредства.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустыеТМ", ПустыеТМ);
	Запрос.УстановитьПараметр("ПроизводственныеСутки", гкс_ПриемкаТранспорта.ПроизводственныеСуткиПоДата(МестнаяДата));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

// Возвращает структуру для записи в регистр с заполненными служебными полями
// 
// Возвращаемое значение:
//  Структура - структура записи регистра
Функция СтруктураЗаписи() Экспорт
	ПустаяДата = Дата(1,1,1);
	
	Структура = Новый Структура;
	Структура.Вставить("ВидПеревозки", Перечисления.гкс_ТипыТранспортныхСредствДоставки.ПустаяСсылка());
	Структура.Вставить("ТочкаМаршрута", Справочники.гкс_ТочкиМаршрута.ПустаяСсылка());
	Структура.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	Структура.Вставить("ТранспортноеСредство", Справочники.ТранспортныеСредства.ПустаяСсылка());
	Структура.Вставить("ПунктПогрузки", Справочники.гкс_ТочкиМаршрута.ПустаяСсылка());
	Структура.Вставить("Поставщик", Справочники.Контрагенты.ПустаяСсылка());
	Структура.Вставить("ЖДСтанция", Справочники.гкс_КлассификаторСтанцийЖД.ПустаяСсылка());
	Структура.Вставить("ДатаНачала", ПустаяДата);
	Структура.Вставить("ДатаОкончания", ПустаяДата);
	Структура.Вставить("Активен", Истина);
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Реализует иерархическую систему приоритетов правил через каскадные ОБЪЕДИНИТЬ ВСЕ
// По аналогии с архитектурой запроса усиленного контроля качества
Функция ТекстЗапросаПроверкиПрименимостиПравилаОтключения()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрацияНаПЛК.Ссылка КАК Регистрация,
	|	РегистрацияНаПЛК.ТочкаМаршрута КАК ТочкаМаршрута,
	|	РегистрацияНаПЛК.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА РегистрацияНаПЛК.ВидПеревозки = &ЖДПеревозка
	|			ТОГДА ЕСТЬNULL(ТочкиМаршрута.КодСтанцииЖД, ЗНАЧЕНИЕ(Справочник.гкс_КлассификаторСтанцийЖД.ПустаяССылка))
	|		ИНАЧЕ РегистрацияНаПЛК.ПунктПогрузки
	|	КОНЕЦ КАК ПунктПогрузки,
	|	РегистрацияНаПЛК.Номенклатура КАК Номенклатура,
	|	РегистрацияНаПЛК.ТранспортноеСредство КАК ТранспортноеСредство,
	|	РегистрацияНаПЛК.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ДанныеРегистрации
	|ИЗ
	|	Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.гкс_СмещениеПроизводственныхСуток КАК СмещениеПроизводственныхСуток
	|		ПО ИСТИНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_ТочкиМаршрута КАК ТочкиМаршрута
	|		ПО РегистрацияНаПЛК.ПунктПогрузки = ТочкиМаршрута.Ссылка
	|ГДЕ
	|	РегистрацияНаПЛК.Ссылка = &ДокументРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА НастройкиОтключения.ВидПеревозки = &ЖДПеревозка
	|			ТОГДА НастройкиОтключения.ЖДСтанция
	|		ИНАЧЕ НастройкиОтключения.ПунктПогрузки
	|	КОНЕЦ КАК ПунктПогрузки,
	|	ДанныеРегистрации.ТочкаМаршрута КАК ТочкаМаршрута,
	|	НастройкиОтключения.Номенклатура КАК Номенклатура,
	|	НастройкиОтключения.Поставщик КАК Поставщик,
	|	НастройкиОтключения.ТранспортноеСредство КАК ТранспортноеСредство,
	|	НастройкиОтключения.ВидПеревозки КАК ВидПеревозки
	|ПОМЕСТИТЬ ТаблицаНастройкиОтключения
	|ИЗ
	|	РегистрСведений.гкс_НастройкиОтключенияВходногоКонтроляКачества КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.ВидПеревозки = ДанныеРегистрации.ВидПеревозки
	|		И НастройкиОтключения.ТочкаМаршрута = ДанныеРегистрации.ТочкаМаршрута
	|		И НастройкиОтключения.Номенклатура = ДанныеРегистрации.Номенклатура
	|ГДЕ
	|	НастройкиОтключения.Активен
	|	И НастройкиОтключения.ДатаНачала <= &ПроизводственныеСутки
	|	И &ПроизводственныеСутки <= ВЫБОР
	|		КОГДА НастройкиОтключения.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|		ИНАЧЕ НастройкиОтключения.ДатаОкончания
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация КАК Регистрация,
	|	НастройкиОтключения.Поставщик КАК Поставщик,
	|	НастройкиОтключения.Номенклатура КАК Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство КАК ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.ТранспортноеСредство = ДанныеРегистрации.ТранспортноеСредство
	|		И НастройкиОтключения.Поставщик = ДанныеРегистрации.Контрагент
	|		И НастройкиОтключения.ПунктПогрузки = ДанныеРегистрации.ПунктПогрузки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация,
	|	НастройкиОтключения.Поставщик,
	|	НастройкиОтключения.Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.Поставщик = ДанныеРегистрации.Контрагент
	|		И НастройкиОтключения.ПунктПогрузки = ДанныеРегистрации.ПунктПогрузки
	|ГДЕ
	|	НастройкиОтключения.ТранспортноеСредство = &ПустоеТС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация,
	|	НастройкиОтключения.Поставщик,
	|	НастройкиОтключения.Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.Поставщик = ДанныеРегистрации.Контрагент
	|		И НастройкиОтключения.ТранспортноеСредство = ДанныеРегистрации.ТранспортноеСредство
	|ГДЕ
	|	НастройкиОтключения.ПунктПогрузки В (&ПустыеТМ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация,
	|	НастройкиОтключения.Поставщик,
	|	НастройкиОтключения.Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.Поставщик = ДанныеРегистрации.Контрагент
	|ГДЕ
	|		НастройкиОтключения.ТранспортноеСредство = &ПустоеТС
	|		И НастройкиОтключения.ПунктПогрузки В (&ПустыеТМ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация,
	|	НастройкиОтключения.Поставщик,
	|	НастройкиОтключения.Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.ТранспортноеСредство = ДанныеРегистрации.ТранспортноеСредство
	|		И НастройкиОтключения.ПунктПогрузки = ДанныеРегистрации.ПунктПогрузки
	|ГДЕ
	|	НастройкиОтключения.Поставщик = &ПустойКонтрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация,
	|	НастройкиОтключения.Поставщик,
	|	НастройкиОтключения.Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.ТранспортноеСредство = ДанныеРегистрации.ТранспортноеСредство
	|ГДЕ
	|		НастройкиОтключения.Поставщик = &ПустойКонтрагент
	|		И НастройкиОтключения.ПунктПогрузки В (&ПустыеТМ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация,
	|	НастройкиОтключения.Поставщик,
	|	НастройкиОтключения.Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиОтключения.ПунктПогрузки = ДанныеРегистрации.ПунктПогрузки
	|ГДЕ
	|	НастройкиОтключения.Поставщик = &ПустойКонтрагент
	|	И НастройкиОтключения.ТранспортноеСредство = &ПустоеТС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистрации.Регистрация,
	|	НастройкиОтключения.Поставщик,
	|	НастройкиОтключения.Номенклатура,
	|	НастройкиОтключения.ТранспортноеСредство
	|ИЗ
	|	ТаблицаНастройкиОтключения КАК НастройкиОтключения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО (ИСТИНА)
	|ГДЕ
	|		НастройкиОтключения.Поставщик = &ПустойКонтрагент
	|		И НастройкиОтключения.ТранспортноеСредство = &ПустоеТС
	|		И НастройкиОтключения.ПунктПогрузки В (&ПустыеТМ)";
	
КонецФункции

#КонецОбласти

#КонецЕсли