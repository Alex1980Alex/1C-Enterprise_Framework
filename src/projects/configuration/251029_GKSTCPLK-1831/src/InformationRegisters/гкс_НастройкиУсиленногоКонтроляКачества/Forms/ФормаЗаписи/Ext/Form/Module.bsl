#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ.ИдентификаторПравила) Тогда
		Запись.ИдентификаторПравила = Новый УникальныйИдентификатор;
	КонецЕсли;	
	ЗаполнитьЗначениеЗаписи(Запись, Параметры, "ВидПеревозки");
	ЗаполнитьЗначениеЗаписи(Запись, Параметры, "ТочкаМаршрута");
	УстановитьВидимостьДоступность(); 
		
КонецПроцедуры                      

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ГруппыТС = Запись.ТипВходногоКонтроля = 
		ПредопределенноеЗначение("Перечисление.гкс_ТипыУсиленногоКонтроляКачества.ГруппыТС");
	
	Если ГруппыТС И Не ЗначениеЗаполнено(Запись.КоличествоТС) Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Необходимо заполнить Количество в группе'"), Неопределено, 
			"КоличествоТС", "Запись.КоличествоТС", Отказ);
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ТипВходногоКонтроляПриИзменении(Элемент)
	ГруппыТС = Запись.ТипВходногоКонтроля = 
		ПредопределенноеЗначение("Перечисление.гкс_ТипыУсиленногоКонтроляКачества.ГруппыТС");
	Элементы.КоличествоТС.Видимость = ГруппыТС;
	Если Не ГруппыТС Тогда
		Запись.КоличествоТС = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыиФункции

&НаСервере
Процедура ЗаполнитьЗначениеЗаписи(ЗаписьРегистра, ПараметрыЗаписи, ИмяРеквизита)
	Если Не ЗначениеЗаполнено(ЗаписьРегистра[ИмяРеквизита])
		И Параметры.Свойство(ИмяРеквизита) Тогда 
		ЗаписьРегистра[ИмяРеквизита] = ПараметрыЗаписи[ИмяРеквизита];
	КонецЕсли;	                                     
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	ЭтоЖД = Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЭтоЖДПеревозка(Запись.ВидПеревозки); 
	Элементы.ГруппаЖДСтанция.Видимость = ЭтоЖД;
	Элементы.ПунктПогрузки.Видимость = Не ЭтоЖД;
	
	Если РеквизитНеДоступен() Тогда
		Элементы.ВидПеревозки.ТолькоПросмотр = Истина;
		Элементы.ТочкаМаршрута.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Запись.ТипВходногоКонтроля = Перечисления.гкс_ТипыУсиленногоКонтроляКачества.ГруппыТС Тогда
		Элементы.КоличествоТС.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры	

// Реквизит не доступен.
// 
// Возвращаемое значение:
//  Булево - Реквизит не доступен
&НаСервере
Функция РеквизитНеДоступен()
	
	Возврат Не Пользователи.ЭтоПолноправныйПользователь()
		Или Параметры.Свойство("ОткрытаИзОбработки") 
		И Параметры["ОткрытаИзОбработки"];
КонецФункции     

#КонецОбласти


