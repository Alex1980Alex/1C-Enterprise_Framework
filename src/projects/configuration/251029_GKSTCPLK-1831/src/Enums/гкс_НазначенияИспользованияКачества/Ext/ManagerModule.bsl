#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Получает значение по указанному документу "Формирование номера пробы"
// 
// Параметры:
//  ФормированиеНомераПробы - ДокументСсылка.гкс_ФормированиеНомераПробы - ссылка на исходный документ
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.гкс_НазначенияИспользованияКачества - значение для лаб. анализа
//
Функция ЗначениеПоДокументуФормированиеНомераПробы(ФормированиеНомераПробы) Экспорт

	Результат = ПустаяСсылка();
	ТипПробы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФормированиеНомераПробы, "ТипПробы");

	Если ТипПробы = Перечисления.гкс_ТипыПроб.Композитная Тогда
		Результат = ПриемкаКомпозит;
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	гкс_РегистрацияНаПЛК.ТипРегистрации КАК ТипРегистрации
		|ИЗ
		|	Документ.гкс_ФормированиеНомераПробы.СписокРегистраций КАК гкс_ФормированиеНомераПробыСписокРегистраций
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_РегистрацияНаПЛК КАК гкс_РегистрацияНаПЛК
		|		ПО гкс_ФормированиеНомераПробыСписокРегистраций.ДокументРегистрации = гкс_РегистрацияНаПЛК.Ссылка
		|ГДЕ
		|	гкс_ФормированиеНомераПробыСписокРегистраций.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ФормированиеНомераПробы);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			
			Результат = ПолучитьПоТипуРегистрации(ВыборкаДетальныеЗаписи.ТипРегистрации);
		КонецЕсли;

	КонецЕсли;
	
	Возврат Результат;

КонецФункции


// Получить по типу регистрации.
// 
// Параметры:
//  ТипРегистрации - ПеречислениеСсылка.гкс_ТипРегистрации - 
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.гкс_НазначенияИспользованияКачества - Получить по типу регистрации
//
Функция ПолучитьПоТипуРегистрации(ТипРегистрации) Экспорт
	
	Если ТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка Тогда
		Возврат Приемка;	
	ИначеЕсли ТипРегистрации = Перечисления.гкс_ТипРегистрации.Отгрузка Тогда
		Возврат Отгрузка;
	Иначе
		Возврат ПустаяСсылка();
	КонецЕсли;
		
КонецФункции

// Определяет тип лабораторного анализа по реквизиту Названачения использования качества 
// Параметры:
// НазначениеИспользованияКачества - Перечисления.гкс_НазначенияИспользованияКачества
Функция ЭтоПриемка(НазначениеИспользованияКачества) Экспорт 
	Возврат
		НазначениеИспользованияКачества = 
			Перечисления.гкс_НазначенияИспользованияКачества.Приемка;	
КонецФункции
		
// Определяет тип лабораторного анализа по реквизиту Названачения использования качества 
// Параметры:
// НазначениеИспользованияКачества - Перечисления.гкс_НазначенияИспользованияКачества
Функция ЭтоПриемкаКомпозит(НазначениеИспользованияКачества) Экспорт 
	Возврат
		НазначениеИспользованияКачества = 
			Перечисления.гкс_НазначенияИспользованияКачества.ПриемкаКомпозит;	
КонецФункции
		
#КонецОбласти

#КонецЕсли