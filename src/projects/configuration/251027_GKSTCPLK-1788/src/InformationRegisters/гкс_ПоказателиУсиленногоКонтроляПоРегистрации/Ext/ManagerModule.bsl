#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Заполняет пустые значения НазначенияИспользования значением "Приемка"
// для всех существующих записей в регистре
//
// Параметры:
//   ПараметрыОбновления - Структура - параметры обработчика обновления
//
Процедура ЗаполнитьПустыеНазначенияИспользованияПриемкой(ПараметрыОбновления = Неопределено) Экспорт
	
	СобытиеЖурнала = ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации();
	НазначениеПриемка = Перечисления.гкс_НазначенияИспользованияКачества.Приемка;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.ДокументРегистрации КАК ДокументРегистрации,
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.Показатель КАК Показатель,
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.ЛабораторныйАнализ КАК ЛабораторныйАнализ
		|ИЗ
		|	РегистрСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации КАК гкс_ПоказателиУсиленногоКонтроляПоРегистрации
		|ГДЕ
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.НазначениеИспользования = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.ДокументРегистрации,
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.Показатель,
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.ЛабораторныйАнализ
		|ИЗ
		|	РегистрСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации КАК гкс_ПоказателиУсиленногоКонтроляПоРегистрации
		|ГДЕ
		|	гкс_ПоказателиУсиленногоКонтроляПоРегистрации.СостояниеКачества = ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияКачества.ПустаяСсылка)
		|	И НЕ гкс_ПоказателиУсиленногоКонтроляПоРегистрации.УдалитьСтатусКачества = ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияРегистрации.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	КоличествоОбновленных = 0;
	
	НаборЗаписей = РегистрыСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			НаборЗаписей.Отбор.ДокументРегистрации.Установить(Выборка.ДокументРегистрации);
			НаборЗаписей.Отбор.Показатель.Установить(Выборка.Показатель);
			НаборЗаписей.Отбор.ЛабораторныйАнализ.Установить(Выборка.ЛабораторныйАнализ);
			
			НаборЗаписей.Прочитать();

			Для Каждого Запись Из НаборЗаписей Цикл
				Если Не ЗначениеЗаполнено(Запись.НазначениеИспользования) Тогда
					Запись.НазначениеИспользования = НазначениеПриемка;
				КонецЕсли;	
				Если Не ЗначениеЗаполнено(Запись.СостояниеКачества)
					И ЗначениеЗаполнено(Запись.УдалитьСтатусКачества) Тогда
					Запись.СостояниеКачества = СтатусКачестваПоСоответсвию(Запись.УдалитьСтатусКачества);
				КонецЕсли;	
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			КоличествоОбновленных = КоличествоОбновленных + 1;
				
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			
			ИнфоОбОшибке = ИнформацияОбОшибке();
			ПодробноеПредставление = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнфоОбОшибке);
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать запись регистра гкс_ПоказателиУсиленногоКонтроляПоРегистрации 
				|по причине: %1'"), ПодробноеПредставление);
			ЗаписьЖурналаРегистрации(СобытиеЖурнала, УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации, , ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
	ТекстСообщенияУспех = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обновлено записей в РС гкс_ПоказателиУсиленногоКонтроляПоРегистрации: %1. 
				|Заполнено НазначениеИспользования = Приемка для пустых значений.'"), КоличествоОбновленных);
	ЗаписьЖурналаРегистрации(
		СобытиеЖурнала, УровеньЖурналаРегистрации.Информация,
		Метаданные.РегистрыСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации, , ТекстСообщенияУспех);
	
КонецПроцедуры


#КонецОбласти   

#Область СлужебныеПроцедурыИФункции

Функция СтатусКачестваПоСоответсвию(СтатусКачества)
	
	Если СтатусКачества = Перечисления.гкс_СостоянияРегистрации.КачествоПринято Тогда
		Результат = Перечисления.гкс_СостоянияКачества.КачествоПринято;
	ИначеЕсли СтатусКачества = Перечисления.гкс_СостоянияРегистрации.КачествоНеПринято Тогда
		Результат = Перечисления.гкс_СостоянияКачества.КачествоНеПринято;
	ИначеЕсли СтатусКачества = Перечисления.гкс_СостоянияРегистрации.ВзятыПробы Тогда
		Результат = Перечисления.гкс_СостоянияКачества.ОтборПроб;
	Иначе
		Результат = Перечисления.гкс_СостоянияКачества.ПустаяСсылка();	
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции	
#КонецОбласти   

#КонецЕсли