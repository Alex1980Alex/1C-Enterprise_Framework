#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ТочкаМаршрута)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти
// Возвращает перечень показателей качества для усиленного входного контроля
// Параметры:
// ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
// МестнаяДата - Дата
// ВозвращаемоеЗначение 
// - ТаблицаЗначений - таблица показателей со значениями мин-макс
Функция НайтиПоказателиКачестваДляУсиленногоВходногоКонтроля(ДокументРегистрации, МестнаяДата) Экспорт
	
	ПустыеТМ = Новый СписокЗначений;
	ПустыеТМ.Добавить(Справочники.гкс_ТочкиМаршрута.ПустаяСсылка());
	ПустыеТМ.Добавить(Справочники.гкс_КлассификаторСтанцийЖД.ПустаяСсылка());                           
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	Запрос.УстановитьПараметр("МестнаяДата", МестнаяДата);
	Запрос.УстановитьПараметр("ПроизводственныеСутки", гкс_ПриемкаТранспорта.ПроизводственныеСуткиПоДата(МестнаяДата));
	Запрос.УстановитьПараметр("ЖДПеревозка", Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЖДТранспорт);
	Запрос.УстановитьПараметр("ПустоеТС", Справочники.ТранспортныеСредства.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойКонтрагент", Справочники.Контрагенты.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустыеТМ", ПустыеТМ);
	Запрос.УстановитьПараметр("КаждоеТС", Перечисления.гкс_ТипыУсиленногоКонтроляКачества.КаждоеТС);
	Запрос.УстановитьПараметр("НазначениеПриемка", Перечисления.гкс_НазначенияИспользованияКачества.Приемка);
	Запрос.УстановитьПараметр("НазначениеПромежуточныйКомпозит",
		Перечисления.гкс_НазначенияИспользованияКачества.ПромежуточныйКомпозит);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаПодборПоказателейДляВходногоКонтроля(НомераТаблиц);
		
	Результаты = Запрос.ВыполнитьПакет(); 
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ПоказателиКонтроля", Результаты[НомераТаблиц.ПоказателиКонтроля].Выгрузить());
	СтруктураРезультата.Вставить("ПравилаКонтроля", Результаты[НомераТаблиц.ПравилаКонтроля].Выгрузить());
	СтруктураРезультата.Вставить("ПравилаКонтроляТС", Результаты[НомераТаблиц.ПравилаКонтроляТС].Выгрузить());
	
	Возврат 
		СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции  

Функция ТекстЗапросаПодборПоказателейДляВходногоКонтроля(НомераТаблиц)
	
	НомераТаблиц.Вставить("ДанныеРегистрации", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаВходногоКонтроля", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВсеПравилаВходногоКонтроля", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПрименяемыеПравила", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТранспортныеСредстваПоставщика", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПравилаИтог", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПоказателиКонтроля", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПравилаКонтроля", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПравилаКонтроляТС", НомераТаблиц.Количество());
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрацияНаПЛК.ТочкаМаршрута КАК ТочкаМаршрута,
	|	РегистрацияНаПЛК.ВидПеревозки КАК ВидПеревозки,
	|	ВЫБОР
	|		КОГДА РегистрацияНаПЛК.ВидПеревозки = &ЖДПеревозка
	|			ТОГДА ЕСТЬNULL(ТочкиМаршрута.КодСтанцииЖД, ЗНАЧЕНИЕ(Справочник.гкс_КлассификаторСтанцийЖД.ПустаяССылка))
	|		ИНАЧЕ РегистрацияНаПЛК.ПунктПогрузки
	|	КОНЕЦ КАК ПунктПогрузки,
	|	РегистрацияНаПЛК.Номенклатура КАК Номенклатура,
	|	РегистрацияНаПЛК.ТранспортноеСредство КАК ТранспортноеСредство,
	|	РегистрацияНаПЛК.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ДанныеРегистрации
	|ИЗ
	|	Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.гкс_СмещениеПроизводственныхСуток КАК СмещениеПроизводственныхСуток
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_ТочкиМаршрута КАК ТочкиМаршрута
	|		ПО РегистрацияНаПЛК.ПунктПогрузки = ТочкиМаршрута.Ссылка
	|ГДЕ
	|	РегистрацияНаПЛК.Ссылка = &ДокументРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиУсиленногоКонтроляКачества.Показатель КАК Показатель,
	|	ВЫБОР
	|		КОГДА НастройкиУсиленногоКонтроляКачества.ВидПеревозки = &ЖДПеревозка
	|			ТОГДА НастройкиУсиленногоКонтроляКачества.ЖДСтанция
	|		ИНАЧЕ НастройкиУсиленногоКонтроляКачества.ПунктПогрузки
	|	КОНЕЦ КАК ПунктПогрузки,
	|	НастройкиУсиленногоКонтроляКачества.ТранспортноеСредство КАК ТранспортноеСредство,
	|	НастройкиУсиленногоКонтроляКачества.Поставщик КАК Поставщик,
	|	НастройкиУсиленногоКонтроляКачества.ЗначениеПоказателяМин КАК ЗначениеПоказателяМин,
	|	НастройкиУсиленногоКонтроляКачества.ЗначениеПоказателяМакс КАК ЗначениеПоказателяМакс,
	|	ВЫБОР
	|		КОГДА НастройкиУсиленногоКонтроляКачества.ТипВходногоКонтроля = &КаждоеТС
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПроверятьКаждоеТС,
	|	НастройкиУсиленногоКонтроляКачества.Базис КАК Базис,
	|	НастройкиУсиленногоКонтроляКачества.ИдентификаторПравила КАК ИдентификаторПравила,
	|	НастройкиУсиленногоКонтроляКачества.ТипВходногоКонтроля КАК ТипВходногоКонтроля,
	|	НастройкиУсиленногоКонтроляКачества.КоличествоТС КАК КоличествоТС
	|ПОМЕСТИТЬ ТаблицаВходногоКонтроля
	|ИЗ
	|	РегистрСведений.гкс_НастройкиУсиленногоКонтроляКачества КАК НастройкиУсиленногоКонтроляКачества
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|		ПО НастройкиУсиленногоКонтроляКачества.ВидПеревозки = ДанныеРегистрации.ВидПеревозки
	|			И НастройкиУсиленногоКонтроляКачества.ТочкаМаршрута = ДанныеРегистрации.ТочкаМаршрута
	|			И НастройкиУсиленногоКонтроляКачества.Номенклатура = ДанныеРегистрации.Номенклатура
	|ГДЕ
	|	НастройкиУсиленногоКонтроляКачества.Активен
	|	И НастройкиУсиленногоКонтроляКачества.ДатаНачала <= &ПроизводственныеСутки
	|	И &ПроизводственныеСутки <= ВЫБОР
	|			КОГДА НастройкиУсиленногоКонтроляКачества.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|			ИНАЧЕ НастройкиУсиленногоКонтроляКачества.ДатаОкончания
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила КАК ИдентификаторПравила
	|ПОМЕСТИТЬ ВсеПравилаВходногоКонтроля
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО ДанныеРегистрации.Контрагент = ТаблицаВходногоКонтроля.Поставщик
	|			И ДанныеРегистрации.ТранспортноеСредство = ТаблицаВходногоКонтроля.ТранспортноеСредство
	|			И ДанныеРегистрации.ПунктПогрузки = ТаблицаВходногоКонтроля.ПунктПогрузки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО ДанныеРегистрации.Контрагент = ТаблицаВходногоКонтроля.Поставщик
	|			И ДанныеРегистрации.ПунктПогрузки = ТаблицаВходногоКонтроля.ПунктПогрузки
	|ГДЕ
	|	ТаблицаВходногоКонтроля.ТранспортноеСредство = &ПустоеТС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО ДанныеРегистрации.Контрагент = ТаблицаВходногоКонтроля.Поставщик
	|			И ДанныеРегистрации.ТранспортноеСредство = ТаблицаВходногоКонтроля.ТранспортноеСредство
	|ГДЕ
	|	ТаблицаВходногоКонтроля.ПунктПогрузки В(&ПустыеТМ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО ДанныеРегистрации.Контрагент = ТаблицаВходногоКонтроля.Поставщик
	|ГДЕ
	|	ТаблицаВходногоКонтроля.ПунктПогрузки В(&ПустыеТМ)
	|	И ТаблицаВходногоКонтроля.ТранспортноеСредство = &ПустоеТС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО ДанныеРегистрации.ТранспортноеСредство = ТаблицаВходногоКонтроля.ТранспортноеСредство
	|			И ДанныеРегистрации.ПунктПогрузки = ТаблицаВходногоКонтроля.ПунктПогрузки
	|ГДЕ
	|	ТаблицаВходногоКонтроля.Поставщик = &ПустойКонтрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО ДанныеРегистрации.ТранспортноеСредство = ТаблицаВходногоКонтроля.ТранспортноеСредство
	|ГДЕ
	|	ТаблицаВходногоКонтроля.Поставщик = &ПустойКонтрагент
	|	И ТаблицаВходногоКонтроля.ПунктПогрузки В(&ПустыеТМ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО ДанныеРегистрации.ПунктПогрузки = ТаблицаВходногоКонтроля.ПунктПогрузки
	|ГДЕ
	|	ТаблицаВходногоКонтроля.Поставщик = &ПустойКонтрагент
	|	И ТаблицаВходногоКонтроля.ТранспортноеСредство = &ПустоеТС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила
	|ИЗ
	|	ДанныеРегистрации КАК ДанныеРегистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаВходногоКонтроля.Поставщик = &ПустойКонтрагент
	|	И ТаблицаВходногоКонтроля.ТранспортноеСредство = &ПустоеТС
	|	И ТаблицаВходногоКонтроля.ПунктПогрузки В(&ПустыеТМ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВходногоКонтроля.ТипВходногоКонтроля КАК ТипВходногоКонтроля,
	|	ТаблицаВходногоКонтроля.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ТаблицаВходногоКонтроля.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ТаблицаВходногоКонтроля.Показатель КАК Показатель,
	|	ТаблицаВходногоКонтроля.ПунктПогрузки КАК ПунктПогрузки,
	|	ТаблицаВходногоКонтроля.Поставщик КАК Поставщик,
	|	ТаблицаВходногоКонтроля.ЗначениеПоказателяМин КАК ЗначениеПоказателяМин,
	|	ТаблицаВходногоКонтроля.ЗначениеПоказателяМакс КАК ЗначениеПоказателяМакс,
	|	ТаблицаВходногоКонтроля.Базис КАК Базис,
	|	ТаблицаВходногоКонтроля.КоличествоТС КАК КоличествоТС
	|ПОМЕСТИТЬ ПрименяемыеПравила
	|ИЗ
	|	ТаблицаВходногоКонтроля КАК ТаблицаВходногоКонтроля
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеПравилаВходногоКонтроля КАК ВсеПравилаВходногоКонтроля
	|		ПО ТаблицаВходногоКонтроля.ИдентификаторПравила = ВсеПравилаВходногоКонтроля.ИдентификаторПравила
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.Номенклатура КАК Номенклатура,
	|	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.Контрагент КАК Контрагент,
	|	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ЭтоПервоеТС КАК ЭтоПервоеТС,
	|	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ДокументРегистрации КАК ДокументРегистрации
	|ПОМЕСТИТЬ ТранспортныеСредстваПоставщика
	|ИЗ
	|	РегистрСведений.гкс_ТранспортныеСредстваПоставщикаУсиленногоКонтроля КАК ТранспортныеСредстваПоставщикаУсиленногоКонтроля
	|ГДЕ
	|	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ПроизводственныеСутки = &ПроизводственныеСутки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрименяемыеПравила.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ПрименяемыеПравила.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ПрименяемыеПравила.ТипВходногоКонтроля КАК ТипВходногоКонтроля,
	|	ПрименяемыеПравила.Показатель КАК Показатель,
	|	ПрименяемыеПравила.ЗначениеПоказателяМин КАК ЗначениеПоказателяМин,
	|	ПрименяемыеПравила.ЗначениеПоказателяМакс КАК ЗначениеПоказателяМакс,
	|	ПрименяемыеПравила.Базис КАК Базис,
	|	&НазначениеПриемка КАК НазначениеИспользования,
	|	0 КАК КоличествоТС
	|ПОМЕСТИТЬ ПравилаИтог
	|ИЗ
	|	ПрименяемыеПравила КАК ПрименяемыеПравила
	|ГДЕ
	|	ПрименяемыеПравила.ТипВходногоКонтроля = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыУсиленногоКонтроляКачества.КаждоеТС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрименяемыеПравила.ИдентификаторПравила,
	|	ПрименяемыеПравила.ТранспортноеСредство,
	|	ПрименяемыеПравила.ТипВходногоКонтроля,
	|	ПрименяемыеПравила.Показатель,
	|	ПрименяемыеПравила.ЗначениеПоказателяМин,
	|	ПрименяемыеПравила.ЗначениеПоказателяМакс,
	|	ПрименяемыеПравила.Базис,
	|	&НазначениеПриемка,
	|	0
	|ИЗ
	|	ПрименяемыеПравила КАК ПрименяемыеПравила
	|ГДЕ
	|	НЕ ПрименяемыеПравила.ТранспортноеСредство = &ПустоеТС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрименяемыеПравила.ИдентификаторПравила,
	|	ПрименяемыеПравила.ТранспортноеСредство,
	|	ПрименяемыеПравила.ТипВходногоКонтроля,
	|	ПрименяемыеПравила.Показатель,
	|	ПрименяемыеПравила.ЗначениеПоказателяМин,
	|	ПрименяемыеПравила.ЗначениеПоказателяМакс,
	|	ПрименяемыеПравила.Базис,
	|	&НазначениеПриемка,
	|	0
	|ИЗ
	|	ПрименяемыеПравила КАК ПрименяемыеПравила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТранспортныеСредстваПоставщика КАК ТранспортныеСредстваПоставщика
	|			ПО ДанныеРегистрации.Контрагент = ТранспортныеСредстваПоставщика.Контрагент
	|				И ДанныеРегистрации.Номенклатура = ТранспортныеСредстваПоставщика.Номенклатура
	|				И (ТранспортныеСредстваПоставщика.ЭтоПервоеТС)
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ПрименяемыеПравила.ТипВходногоКонтроля = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыУсиленногоКонтроляКачества.ПервоеТС)
	|	И ТранспортныеСредстваПоставщика.ДокументРегистрации ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрименяемыеПравила.ИдентификаторПравила,
	|	ПрименяемыеПравила.ТранспортноеСредство,
	|	ПрименяемыеПравила.ТипВходногоКонтроля,
	|	ПрименяемыеПравила.Показатель,
	|	ПрименяемыеПравила.ЗначениеПоказателяМин,
	|	ПрименяемыеПравила.ЗначениеПоказателяМакс,
	|	ПрименяемыеПравила.Базис,
	|	&НазначениеПриемка,
	|	ПрименяемыеПравила.КоличествоТС
	|ИЗ
	|	ПрименяемыеПравила КАК ПрименяемыеПравила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТранспортныеСредстваПоставщика КАК ТранспортныеСредстваПоставщика
	|			ПО ДанныеРегистрации.Контрагент = ТранспортныеСредстваПоставщика.Контрагент
	|				И ДанныеРегистрации.Номенклатура = ТранспортныеСредстваПоставщика.Номенклатура
	|				И (ТранспортныеСредстваПоставщика.ЭтоПервоеТС)
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ПрименяемыеПравила.ТипВходногоКонтроля = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыУсиленногоКонтроляКачества.ГруппыТС)
	|	И ТранспортныеСредстваПоставщика.ДокументРегистрации ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрименяемыеПравила.ИдентификаторПравила,
	|	ПрименяемыеПравила.ТранспортноеСредство,
	|	ПрименяемыеПравила.ТипВходногоКонтроля,
	|	ПрименяемыеПравила.Показатель,
	|	ПрименяемыеПравила.ЗначениеПоказателяМин,
	|	ПрименяемыеПравила.ЗначениеПоказателяМакс,
	|	ПрименяемыеПравила.Базис,
	|	&НазначениеПромежуточныйКомпозит,
	|	ПрименяемыеПравила.КоличествоТС
	|ИЗ
	|	ПрименяемыеПравила КАК ПрименяемыеПравила
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеРегистрации КАК ДанныеРегистрации
	|			ЛЕВОЕ СОЕДИНЕНИЕ ТранспортныеСредстваПоставщика КАК ТранспортныеСредстваПоставщика
	|			ПО ДанныеРегистрации.Контрагент = ТранспортныеСредстваПоставщика.Контрагент
	|				И ДанныеРегистрации.Номенклатура = ТранспортныеСредстваПоставщика.Номенклатура
	|				И (ТранспортныеСредстваПоставщика.ЭтоПервоеТС)
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ПрименяемыеПравила.ТипВходногоКонтроля = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыУсиленногоКонтроляКачества.ГруппыТС)
	|	И НЕ ТранспортныеСредстваПоставщика.ДокументРегистрации ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ДокументРегистрации КАК ДокументРегистрации,
	|	ПравилаИтог.Показатель КАК Показатель,
	|	МАКСИМУМ(ПравилаИтог.ЗначениеПоказателяМин) КАК ЗначениеПоказателяМин,
	|	МИНИМУМ(ПравилаИтог.ЗначениеПоказателяМакс) КАК ЗначениеПоказателяМакс,
	|	МИНИМУМ(ПравилаИтог.Базис) КАК Базис,
	|	&ПроизводственныеСутки КАК ПроизводственныеСутки,
	|	&МестнаяДата КАК МестнаяДата,
	|	ПравилаИтог.НазначениеИспользования КАК НазначениеИспользования
	|ИЗ
	|	ПравилаИтог КАК ПравилаИтог
	|
	|СГРУППИРОВАТЬ ПО
	|	ПравилаИтог.Показатель,
	|	ПравилаИтог.НазначениеИспользования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ДокументРегистрации КАК ДокументРегистрации,
	|	ПравилаИтог.ИдентификаторПравила КАК ИдентификаторПравила,
	|	ПравилаИтог.Показатель КАК Показатель,
	|	ПравилаИтог.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ПравилаИтог.ТипВходногоКонтроля КАК ТипВходногоКонтроля,
	|	&МестнаяДата КАК МестнаяДата,
	|	&ПроизводственныеСутки КАК ПроизводственныеСутки,
	|	ПравилаИтог.КоличествоТС КАК КоличествоТС
	|ИЗ
	|	ПравилаИтог КАК ПравилаИтог
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПравилаИтог.ИдентификаторПравила КАК ИдентификаторПравила
	|ИЗ
	|	ПравилаИтог КАК ПравилаИтог
	|ГДЕ
	|	НЕ ПравилаИтог.ТранспортноеСредство = &ПустоеТС";

КонецФункции	 
  
#КонецОбласти

#КонецЕсли