#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует документ гкс_Взвешивание в зависимости от статуса.
//
// Параметры:
//  Статус  - ПеречислениеСсылка.гкс_СРПВСтатусыВагонов - статус взвешивания по СРПВ
//	
Процедура СформироватьДокументПЛКПриНеобходимости(Статус) Экспорт
	
	Если Не (Статус = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенБрутто
		ИЛИ Статус = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенТара) Тогда
		Возврат;
	КонецЕсли;
	
	Если гкс_УчетВагоновСРПВ.НаСтатусОформленДокументОперации(ИдентификаторОтправки, Статус) Тогда
		Возврат;
	КонецЕсли;
	
	Если Статус = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенБрутто 
		И Не гкс_УчетВагоновСРПВ.НаСтатусОформленДокументОперации(ИдентификаторОтправки, 
			Перечисления.гкс_СРПВСтатусыВагонов.Прибыл) Тогда	
		Возврат;
	КонецЕсли;
	
	Если Статус = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенТара 
		И Не гкс_УчетВагоновСРПВ.НаСтатусОформленДокументОперации(ИдентификаторОтправки,
		Перечисления.гкс_СРПВСтатусыВагонов.Выгружен) Тогда				
		Возврат;
	КонецЕсли;
	
	ДанныеОформленияРегистрации = гкс_УчетВагоновСРПВ.ДанныеОформленногоДокументаОперацииПоСтатусу(
									ИдентификаторОтправки, Перечисления.гкс_СРПВСтатусыВагонов.Прибыл);
	
	Если ДанныеОформленияРегистрации.Свойство("Документ")
		И ЗначениеЗаполнено(ДанныеОформленияРегистрации.Документ) Тогда
		
		ДокументРегистрации = ДанныеОформленияРегистрации.Документ;
		
		Если Статус = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенБрутто Тогда
			Вес = ВесБрутто;
			ТипВзвешивания = Перечисления.гкс_ТипыВзвешивания.Въезд;
			
		ИначеЕсли Статус = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенТара Тогда
			Вес = ВесТары;
			ТипВзвешивания = Перечисления.гкс_ТипыВзвешивания.Выезд;
		КонецЕсли;
		
		ДанныеЗаполнения = Новый Структура("Организация, ТочкаМаршрута, ТипРегистрации");
		
		ДанныеЗаполнения.Вставить("Весы", Весы);	
		ДанныеЗаполнения.Вставить("ДокументРегистрации", ДокументРегистрации);
		ДанныеЗаполнения.Вставить("Вес", Вес);
		ДанныеЗаполнения.Вставить("ВесВведенВручную", Ложь);
		ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
		ДанныеЗаполнения.Вставить("МестнаяДата", ДанныеЗаполнения.Дата);
		ДанныеЗаполнения.Вставить("ТипВзвешивания", ТипВзвешивания);
		ДанныеЗаполнения.Вставить("ИдентификаторОтправки", ИдентификаторОтправки);
			
		ДанныеРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРегистрации, 
			"ТипРегистрации, Организация, ТочкаМаршрута");
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ДанныеРегистрации); 
		
		ДокументВзвешивание = Документы.гкс_Взвешивание
			.ПоРегистрацииИТипуВзвешивания(ДокументРегистрации, ТипВзвешивания);
		
		гкс_АвтоматическоеВзвешивание.ЗаполнитьДокументВзвешивание(ДокументВзвешивание, ДанныеЗаполнения);
		
		// создание основания движения запасов и регистрация убытия
		Если ТипВзвешивания = Перечисления.гкс_ТипыВзвешивания.Выезд Тогда
			
			ДанныеЗаполнения = гкс_УчетВагоновСРПВ.ДанныеЗаполненияОснованияДляДвиженияЗапасов(ДокументРегистрации);
						
			ОснованиеДляДвиженияЗапасов = Документы.гкс_ОснованиеДляДвиженияЗапасов
				.СформироватьДокументПоНеобходимости(ДокументРегистрации, ДанныеЗаполнения);
			
			гкс_ПриемкаТранспорта.ЗарегистрироватьНовоеСостояние(
				ДокументРегистрации, Перечисления.гкс_СостоянияРегистрации.Убыл);
		КонецЕсли;	
		
	КонецЕсли;
				
КонецПроцедуры

#КонецОбласти
	
#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ИдентификаторОтправки = Справочники.гкс_КлючиАналитикиСРПВ.ПустаяСсылка();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоЗавершеннаяПеревескаОтгрузки() Тогда
		ДополнительныеСвойства.Вставить("ПропуститьОтправкуВRMQ");
	КонецЕсли;
	
	// Новая логика для СРПВ - актуализация вместо блокировки
	Если ОформленыДокументыПЛК() Тогда	

		Если ОформленыДокументыПЛКСУчетомВременныхОграничений() Тогда
			// Блокируем изменения по временным ограничениям
			ТекстСообщения = ПолучитьТекстОшибкиБлокировки();
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Иначе
			// Разрешено изменение - актуализируем связанные документы взвешивания
			гкс_УчетВагоновСРПВ.АктуализироватьСвязанныеВзвешивания(
				ИдентификаторОтправки, ВесБрутто, ДатаБрутто, ВесТары, ДатаТары);
		КонецЕсли;

	КонецЕсли;	 
	
	гкс_ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
			
	гкс_ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Отправление Тогда
		Возврат;	
	КонецЕсли;
	
	гкс_ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ОтразитьСтатусыОформленияВагонов();
		
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	гкс_ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение
Процедура ОтразитьСтатусыОформленияВагонов()
	
	МенеджерРегистра = РегистрыСведений.гкс_СРПВСтатусыОформленияВагонов;
	
	СтатусыВагонаКОтражению = СтатусыВагонаКОтражению();
	
	Для Каждого СтатусВагона Из СтатусыВагонаКОтражению Цикл 
		
		Если гкс_УчетВагоновСРПВ.НаСтатусОформленДокументОперации(ИдентификаторОтправки, СтатусВагона) Тогда
			Продолжить;
		КонецЕсли;	
		
		ДанныеЗаписи = МенеджерРегистра.ЗначенияПоУмолчанию(Ссылка, ИдентификаторОтправки);
		
		МестнаяДата = Справочники.гкс_ТочкиМаршрута.
		ПолучитьМестнуюДатуПоТочкеМаршрута(ТочкаМаршрута);
		
		ДанныеЗаписи.Статус = СтатусВагона; 	
		ДанныеЗаписи.НомерВагона = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИдентификаторОтправки, "НомерВагона");
		ДанныеЗаписи.МестнаяДата = МестнаяДата;
		
		Если СтатусВагона = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенБрутто Тогда
			ДанныеЗаписи.ДатаОперации = ДатаБрутто;
		ИначеЕсли СтатусВагона = Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенТара Тогда
			ДанныеЗаписи.ДатаОперации = ДатаТары;
		КонецЕсли;	
		
		МенеджерРегистра.ВыполнитьЗаписьВРегистр(ДанныеЗаписи);
		
		СформироватьДокументПЛКПриНеобходимости(СтатусВагона); 
	
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

Функция ЭтоЗавершеннаяПеревескаОтгрузки()
	
	Если ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Отправление
		И ВесТарыЗаполнен() И ВесБруттоЗаполнен() Тогда
		Возврат Истина;
	Иначе	
		Возврат Ложь;
	КонецЕсли;	
	
КонецФункции

Функция ОформленыДокументыПЛК()
	
	СРПВСтатусыВагонов = Перечисления.гкс_СРПВСтатусыВагонов;
	
	ОформленБрутто = гкс_УчетВагоновСРПВ
		.НаСтатусОформленДокументОперации(ИдентификаторОтправки, СРПВСтатусыВагонов.ВзвешенБрутто);
			
	ОформленТара = гкс_УчетВагоновСРПВ
		.НаСтатусОформленДокументОперации(ИдентификаторОтправки, СРПВСтатусыВагонов.ВзвешенТара);
	
	Возврат ОформленБрутто Или ОформленТара;
	
КонецФункции	

Функция СтатусыВагонаКОтражению()
	
	Статусы = Новый Массив;
	
	ВесТарыЗаполнен = ВесТарыЗаполнен();
	ВесБруттоЗаполнен = ВесБруттоЗаполнен();
	
	Если ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Прибытие Тогда
		
		// порядок условий не менять
		Если ВесБруттоЗаполнен Тогда
			Статусы.Добавить(Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенБрутто);
		КонецЕсли;	
			
		Если ВесТарыЗаполнен И ВесБруттоЗаполнен Тогда
			Статусы.Добавить(Перечисления.гкс_СРПВСтатусыВагонов.ВзвешенТара);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Статусы;
	
КонецФункции	

Функция ВесТарыЗаполнен()

	Возврат ЗначениеЗаполнено(ВесТары) И ЗначениеЗаполнено(ДатаТары);
	
КонецФункции

Функция ВесБруттоЗаполнен()

	Возврат ЗначениеЗаполнено(ВесБрутто) И ЗначениеЗаполнено(ДатаБрутто);
	
КонецФункции


// Функция блокировки с учетом временных ограничений
//
// Возвращаемое значение:
//  Булево - Оформлены документы ПЛКСУчетом временных ограничений
Функция ОформленыДокументыПЛКСУчетомВременныхОграничений()
	
	РезультатПроверки = Ложь;
	
	// Получаем дату операции в зависимости от типа операции
	ДатаОперации = ПолучитьДатуОперации(ТипОперации, ДатаТары, ДатаБрутто, ТочкаМаршрута);
	
	// Если прошли производственные сутки, блокируем изменения
	Если гкс_ПриемкаТранспорта.ТекущиеПроизводственныеСуткиПрошли(ДатаОперации, ТочкаМаршрута) Тогда
		РезультатПроверки = Истина;
	Иначе

		Если ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Прибытие Тогда
			// Для приемки блокируем если оформлен реестр ЗПП-3
			РезультатПроверки = РеестрЗПП3Оформлен();
		ИначеЕсли ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Отправление Тогда
			// Для отгрузки блокируем если присвоен статус убыл
			РезультатПроверки = СтатусУбылПрисвоен();
		Иначе
			// По умолчанию блокируем для неизвестных типов операций
			РезультатПроверки = Истина;
		КонецЕсли;

	КонецЕсли;

	Возврат РезультатПроверки;
	
	
КонецФункции

// Обёртки для обратной совместимости
Функция РеестрЗПП3Оформлен()
	Возврат СтатусПроведенияОформлен(Перечисления.гкс_СРПВТипыОперацийСВагоном.Прибытие);
КонецФункции

Функция СтатусУбылПрисвоен()
	Возврат СтатусПроведенияОформлен(Перечисления.гкс_СРПВТипыОперацийСВагоном.Отправление);
КонецФункции

// На: единая функция проверки статуса проведения по типу операции
// Проверяет оформленность документа по типу операции (реестр ЗПП-3 для приемки, статус убыл для отгрузки).
//
// Параметры:
//  ТипПроверки - ПеречислениеСсылка.гкс_СРПВТипыОперацийСВагоном - тип операции для проверки
//
// Возвращаемое значение:
//  Булево - Истина, если документ оформлен согласно типу операции
//
Функция СтатусПроведенияОформлен(ТипПроверки)
	
	РезультатПроверки = Ложь;
	
	// Определяем статус и функцию проверки по типу операции
	Если ТипПроверки = Перечисления.гкс_СРПВТипыОперацийСВагоном.Прибытие Тогда
		СтатусДляПоиска = Перечисления.гкс_СРПВСтатусыВагонов.Прибыл;
	ИначеЕсли ТипПроверки = Перечисления.гкс_СРПВТипыОперацийСВагоном.Отправление Тогда
		СтатусДляПоиска = Перечисления.гкс_СРПВСтатусыВагонов.Выгружен;
	Иначе
		// Неподдерживаемый тип операции
		Возврат РезультатПроверки;
	КонецЕсли;

	// Получаем документ регистрации
	ДанныеОформленияРегистрации = гкс_УчетВагоновСРПВ.ДанныеОформленногоДокументаОперацииПоСтатусу(
		ИдентификаторОтправки, СтатусДляПоиска);

	Если ДанныеОформленияРегистрации.Свойство("Документ") 
		И ЗначениеЗаполнено(ДанныеОформленияРегистрации.Документ) Тогда

		ДокументРегистрации = ДанныеОформленияРегистрации.Документ;
	
		// Применяем соответствующую проверку
		Если ТипПроверки = Перечисления.гкс_СРПВТипыОперацийСВагоном.Прибытие Тогда
			РезультатПроверки =  гкс_УчетВагоновСРПВ.РеестрЗПП3ОформленДляДокумента(ДокументРегистрации);
		ИначеЕсли ТипПроверки = Перечисления.гкс_СРПВТипыОперацийСВагоном.Отправление Тогда
			РезультатПроверки = гкс_ПриемкаТранспорта.СтатусУбылПрисвоенДляДокумента(ДокументРегистрации);
		КонецЕсли;

	КонецЕсли;

	Возврат РезультатПроверки;
	
КонецФункции

// Получить текст ошибки блокировки.
// 
// Возвращаемое значение:
//  Строка - Получить текст ошибки блокировки
Функция ПолучитьТекстОшибкиБлокировки()

	ДатаОперации = ?(ЗначениеЗаполнено(ДатаБрутто), ДатаБрутто, ДатаТары);
	Если гкс_ПриемкаТранспорта.ТекущиеПроизводственныеСуткиПрошли(ДатаОперации, ТочкаМаршрута) Тогда
		ТекстОшибки = НСтр("ru = 'Документ не может быть изменен, т.к. прошли производственные сутки.'");
	ИначеЕсли ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Прибытие И РеестрЗПП3Оформлен() Тогда
		ТекстОшибки = НСтр("ru = 'Документ не может быть изменен, т.к. уже оформлен реестр ЗПП-3.'");
	ИначеЕсли ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Отправление И СтатусУбылПрисвоен() Тогда
		ТекстОшибки = НСтр("ru = 'Документ не может быть изменен, т.к. транспорт уже убыл.'");
	Иначе
		ТекстОшибки = НСтр("ru = 'Документ не может быть изменен.'");
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

// Функция блокировки с учетом временных ограничений
// 
// Параметры:
//  ТипОперации - ПеречислениеСсылка.гкс_СРПВТипыОперацийСВагоном - тип операции (Прибытие/Отправление)
//  ДатаТары - Дата - дата взвешивания тары
//  ДатаБрутто - Дата - дата взвешивания брутто
//  ТочкаМаршрута - СправочникСсылка.гкс_ТочкиМаршрута - точка маршрута для получения местной даты
// 
// Возвращаемое значение:
//  Дата - дата операции с учетом типа операции или местная дата точки маршрута
Функция ПолучитьДатуОперации(ТипОперации, ДатаТары, ДатаБрутто, ТочкаМаршрута)
	
	// Определяем основную дату в зависимости от типа операции
	Если ТипОперации = Перечисления.гкс_СРПВТипыОперацийСВагоном.Прибытие Тогда
		ОсновнаяДата = ДатаТары;
	Иначе
		ОсновнаяДата = ДатаБрутто;
	КонецЕсли;
	
	// Возвращаем основную дату или местную дату точки маршрута в качестве резервной
	Возврат ?(ЗначениеЗаполнено(ОсновнаяДата), ОсновнаяДата, 
		Справочники.гкс_ТочкиМаршрута.ПолучитьМестнуюДатуПоТочкеМаршрута(ТочкаМаршрута));
	
КонецФункции

#КонецОбласти

#КонецЕсли
	
