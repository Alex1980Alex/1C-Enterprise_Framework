
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТочкаМаршрута = Параметры.ТочкаМаршрута;
	Номенклатура = Параметры.Номенклатура;
	ВыбранныеРегистрации.ЗагрузитьЗначения(Параметры.ВыбранныеРегистрации);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПринятьКачествоПриИзменении(Элемент)
	
	Элементы.Комментарий.Доступность = ПринятьКачество;

КонецПроцедуры

&НаКлиенте
Процедура СлужебнаяНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
    ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытия.Вставить("Номенклатура", Номенклатура);
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВыбораСлужебнойНоменклатуры", 
		ПараметрыОткрытия, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры
	
&НаКлиенте
Процедура СлужебнаяНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, 
				ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СлужебнаяНоменклатура = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СлужебнаяНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
										Ожидание, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

	
#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбработатьДанные(Команда)
	
	ДлительнаяОперация = ОбработатьДокументыНаСервере();
	
    ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Вставить("ВыводитьСообщения", Истина);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОбработкиДокументов", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыход(Команда)
	
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеОбработкиДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда	
		ТекстСообщения = СтрШаблон(
						НСтр("ru = 'Произошла ошибка при создании документов направления на разгрузку %1 %2'"), 
						Символы.ПС, Результат.КраткоеПредставлениеОшибки);
			
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		Закрыть();
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ОбработатьДокументыНаСервере()
			
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	//ПараметрыВыполнения.Вставить("ОжидатьЗавершение", Неопределено);
	//ПараметрыВыполнения.Вставить("ЗапуститьНеВФоне", Истина);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ПринятьКачество", 		ПринятьКачество);
	ПараметрыЗаполнения.Вставить("Комментарий", 			Комментарий);
	ПараметрыЗаполнения.Вставить("Склад", 					Силос);
	ПараметрыЗаполнения.Вставить("ЯмаРазгрузки", 			МестоРазгрузки);
	ПараметрыЗаполнения.Вставить("СлужебнаяНоменклатура", 	СлужебнаяНоменклатура);	
	ПараметрыЗаполнения.Вставить("ТочкаМаршрута", 			ТочкаМаршрута);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
								"Обработки.гкс_ПриемкаТранспорта.ГрупповоеНаправлениеНаРазгрузку",
								ПараметрыЗаполнения, ВыбранныеРегистрации.ВыгрузитьЗначения());				
КонецФункции

#КонецОбласти

