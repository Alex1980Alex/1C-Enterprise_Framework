
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройкиФормыИзХранилища();
	
	ИнициализироватьРеквизитыФормы(); 	
	ИнициализироватьЭлементыФормы();
	УстановитьВидимостьДоступностьЭлементовФормы();
	УстановитьВидимостьДоступностьПропуститьКонтрольКачества();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(ОтборТочкаМаршрута) Тогда
		
		ШаблонСообщения = НСтр("ru = 'Не определена рабочая точка маршрута! %1'"); 
		Если ИспользоватьНесколькоАЛЦ Тогда
			ТекстУточнениея = НСтр("ru = 'Проверьте ""Настройки ролей пользователей приемки транспорта""'");
		Иначе
			ТекстУточнениея = НСтр("ru = 'Проверьте константу ""Точка маршрута базы""'");
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекстУточнениея);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ОтборТочкаМаршрута");
		ТолькоПросмотр = Истина;
		
	Иначе			
		ПодключениеОбработчикаОжиданияОбновления(Истина, Ложь);	
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидПеревозкиПриИзменении(Элемент)
		
	ВидПеревозкиПриИзмененииНаСервере();
	УстановитьПометку(Ложь);
		
КонецПроцедуры

&НаКлиенте
Процедура ТипРегистрацииПриИзменении(Элемент)

    УстановитьЗаголовкиЭлементовПоТипуРегистрации(ОтборТипРегистрации);
	ТипРегистрацииПриИзмененииНаСервере();
	УстановитьПометку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборТочкаМаршрутаПриИзменении(Элемент)
	
	ОтборТочкаМаршрутаПриИзмененииНаСервере();
	УстановитьПометку(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрПриИзменении(Элемент)
	УстановитьОтборыСпискаРегистрации(); 
	УстановитьПометку(Ложь);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокРегистрации

&НаКлиенте
Процедура СписокРегистрацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле = Элементы.СписокРегистрацииПометка Тогда	
		
		ТекущиеДанные.Пометка = Не ТекущиеДанные.Пометка;
		УстановитьПометку(ТекущиеДанные.Пометка, ТекущиеДанные.ДокументРегистрации);
		
		ОбновитьПараметрыСпискаРегистраций();
		
	ИначеЕсли Поле = Элементы.СписокРегистрацииНомер Тогда 		
		ПоказатьЗначениеВДиалоге(ТекущиеДанные.ДокументРегистрации);	
	
	ИначеЕсли Поле = Элементы.СписокРегистрацииДоговор Тогда		
		ПоказатьЗначениеВДиалоге(ТекущиеДанные.Спецификация);
						
	ИначеЕсли Поле = Элементы.СписокРегистрацииНомерДокументаПоставщика И ЭтоЖДПеревозка Тогда		
		ПоказатьЗначениеВДиалоге(ТекущиеДанные.ТранспортныйДокумент);	
			
	ИначеЕсли Поле = Элементы.СписокРегистрацииПунктПогрузки И ЭтоЖДПеревозка Тогда 		
		ПоказатьЗначениеВДиалоге(ТекущиеДанные.СтанцияЖД);
		
	КонецЕсли;
		
КонецПроцедуры 

&НаКлиенте
Процедура СписокРегистрацииПриАктивизацииСтроки(Элемент)
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрибытиеВагонов

&НаКлиенте
Процедура ПрибытиеВагоновВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если Поле.Имя = "ПрибытиеВагоновТранспортныйДокумент" Тогда 		
		ПоказатьЗначениеВДиалоге(ТекущиеДанные.ТранспортныйДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкрытьПанельДеталей(Команда)

	РежимПросмотраДетальныхЗаписей = Не РежимПросмотраДетальныхЗаписей;
	
	Элементы.ГруппаДетальнаяИнформация.Видимость = РежимПросмотраДетальныхЗаписей;
	
	Если РежимПросмотраДетальныхЗаписей Тогда
		ИнтервалОбработки = 0.5;
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДетальныеЗаписиРегистрации", ИнтервалОбработки, Истина);
	Иначе
		ОтключитьОбработчикОжидания("Подключаемый_ОбновитьДетальныеЗаписиРегистрации");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРегистрацию(Команда)
	
	ДанныеЗаполнения = ЗначенияЗаполненияРегистрацииНаПЛК();	
	ОткрытьФормуНовогоДокументаРегистрацияНаПЛК(ДанныеЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРегистрациюКПП(Команда)
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ДобавитьРегистрациюНаОснованииВъездаНаКПП", ЭтотОбъект);		
	ОткрытьФорму("Документ.гкс_ВъездНаКПП.Форма.ФормаВыбора", , ЭтотОбъект,,,, ОписаниеОповещенияОЗакрытии);

КонецПроцедуры

&НаКлиенте
Процедура РедактироватьРегистрацию(Команда)
		
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
	
	ПараметрыОткрываемойФормы = Новый Структура;
	ПараметрыОткрываемойФормы.Вставить("ИзАРМа", Истина);
	ПараметрыОткрываемойФормы.Вставить("Ключ", ТекущиеДанные.ДокументРегистрации);
		
	ОткрытьФорму("Документ.гкс_РегистрацияНаПЛК.ФормаОбъекта", 
		ПараметрыОткрываемойФормы, ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено, 
		ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				 		
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВесНаВъезде(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументРегистрацииВеса = ТекущиеДанные.ДокументРегистрацииВъезда;
	ДокументРегистрации = ТекущиеДанные.ДокументРегистрации;
		                                                                                         
	РедактироватьВесНаКлиенте(ПредопределенноеЗначение("Перечисление.гкс_ТипыВзвешивания.Въезд"),
		ДокументРегистрации, ДокументРегистрацииВеса, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВесНаВыезде(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументРегистрацииВеса = ТекущиеДанные.ДокументРегистрацииВыезда;
	ДокументРегистрации = ТекущиеДанные.ДокументРегистрации;
	СостояниеКачествоНеПринято = ТекущиеДанные.Состояние = 
		ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.КачествоНеПринято");
		
	РедактироватьВесНаКлиенте(ПредопределенноеЗначение("Перечисление.гкс_ТипыВзвешивания.Выезд"),
		ДокументРегистрации, ДокументРегистрацииВеса, СостояниеКачествоНеПринято);
		
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеПриемка(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		ИЛИ Не ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации) Тогда
		Возврат;
	КонецЕсли;
	
	Если АвтоматическоеФормированиеДокументов Тогда

	    ИдентификаторЗамера = ОценкаПроизводительностиКлиент
	    				.ЗамерВремени("ПриемкаТранспорта.ФормированиеКомплектаДокументовНаКлиенте", Истина);
	    
		ДанныеРегистрации = ДанныеРегистрацииДляФормированияКомплектаДокументов(ТекущиеДанные);
		СформироватьКомплектДокументовПриемкиИЗарегистрироватьУбытиеТранспорта(
			ТекущиеДанные.ДокументРегистрации, ДанныеРегистрации);
		
		ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(ИдентификаторЗамера, Ложь);

	Иначе				
		ЗарегистрироватьНовоеСостояние(
			ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.Убыл"), ТекущиеДанные.ДокументРегистрации);		
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьРегистрациюАвто(Команда)
	
	ВыделенныеСтроки = Элементы.СписокРегистрации.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивРегистрации = Новый Массив;
	Для Каждого НомерСтроки Из ВыделенныеСтроки Цикл
		ТекущиеДанные = Элементы.СписокРегистрации.ДанныеСтроки(НомерСтроки);
		Если ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации) Тогда
			МассивРегистрации.Добавить(Новый Структура("ДокументРегистрации, ТранспортныйДокумент",
				ТекущиеДанные.ДокументРегистрации, ТекущиеДанные.ТранспортныйДокумент));
		КонецЕсли;	
	КонецЦикла;
	
	Если МассивРегистрации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Комментарий = "";
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивРегистрации", МассивРегистрации);
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОтклонитьРегистрациюПослеВводаКомментария",
		ЭтотОбъект, ДополнительныеПараметры);
		
	ТекстЗаголовка = НСтр("ru = 'Прокомментируйте причину отклонения'");
	ПоказатьВводСтроки(ОписаниеОповещенияОЗавершении, Комментарий, ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНомерПробы(Команда)
	
	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();
	
	Если ВыбранныеРегистрации.Количество() > 0 Тогда
		
		ГрупповоеСозданиеНомерПробы(ВыбранныеРегистрации);
		
	Иначе
		
		ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ИзАРМа", Истина);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ДокументФормированиеНомераПробы) Тогда
			ПараметрыОткрытияФормы.Вставить("Ключ", ТекущиеДанные.ДокументФормированиеНомераПробы);			
		Иначе
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("ТочкаМаршрута", ОтборТочкаМаршрута);
			ЗначенияЗаполнения.Вставить("ДокументРегистрации", ТекущиеДанные.ДокументРегистрации);
			ЗначенияЗаполнения.Вставить("ТипПробы", ПредопределенноеЗначение("Перечисление.гкс_ТипыПроб.Единичная"));
			
			ПараметрыОткрытияФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		КонецЕсли;
	
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("РедактироватьНомерПробыЗавершение", ЭтотОбъект);
			
		ОткрытьФорму("Документ.гкс_ФормированиеНомераПробы.ФормаОбъекта", ПараметрыОткрытияФормы, 
						ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено, 
						ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);		
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНаправлениеНаРазгрузку(Команда)
	
	ОчиститьСообщения();
	
	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();
	Если ВыбранныеРегистрации.Количество() > 0 Тогда
		
		ГрупповоеНаправлениеНаРазгрузку(ВыбранныеРегистрации);
		
	Иначе
						
		ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
				
		НаправлениеНаРазгрузкуСсылка = ПолучитьНаправлениеНаРазгрузку(ТекущиеДанные.ДокументРегистрации);
		
		ПараметрыОткрытия = Новый Структура;
		
		Если Не ЗначениеЗаполнено(НаправлениеНаРазгрузкуСсылка) Тогда		
			ЗначенияЗаполнения = СтруктураЗаполненияДанныхНаправлениеНаРазгрузку(ТекущиеДанные);
			
			ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		Иначе		
			ПараметрыОткрытия.Вставить("Ключ", НаправлениеНаРазгрузкуСсылка);
			ПараметрыОткрытия.Вставить("ИзАРМа", Истина);	
		КонецЕсли;
		
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
		
		ОткрытьФорму("Документ.гкс_НаправлениеНаРазгрузку.ФормаОбъекта", ПараметрыОткрытия, 
			ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено, 
			ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
										
	КонецЕсли;
					         
КонецПроцедуры
	
&НаКлиенте
Процедура РедактироватьАнализ(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрацииАнализов) Тогда
		ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.ДокументРегистрацииАнализов);
		ПараметрыОткрытия.Вставить("ИзАРМа", Истина);
	Иначе
		ДокументРегистрации = ТекущиеДанные.ДокументРегистрации;
		ПодготовитьПараметрыОткрытия(ДокументРегистрации, ПараметрыОткрытия);
	КонецЕсли;
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
	
	ОткрытьФорму("Документ.гкс_ЛабораторныйАнализ.ФормаОбъекта", 
		ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор, , , 
		ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаПробыПриняты(Команда)
	
	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();
	
	Если ВыбранныеРегистрации.Количество() > 0 Тогда
		
		ГрупповоеПодтверждениеПробы(ВыбранныеРегистрации);
		
	Иначе
		
		ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;		
		Если Не (ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации)) Тогда
			Возврат;
		КонецЕсли;
							
		НомерПробы = ПолучитьНомерПробы(ТекущиеДанные.ДокументРегистрации);
		Если НомерПробы	<> Неопределено Тогда
			
			ДополнительныеПараметры = ДополнительныеПараметрыДляПробыПриняты(ТекущиеДанные.ДокументРегистрации);
			Если ДополнительныеПараметры <> Неопределено Тогда
				
				ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПодверждениеПробыПриняты",
					ЭтотОбъект, ДополнительныеПараметры); 
				
				ТекстВопроса = СтрШаблон(НСтр("ru = 'Подтвердите, что проба с номером %1 принята'"), НомерПробы);
				
				ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, 
					РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Да);
			КонецЕсли;
		КонецЕсли;					
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаПодтверждениеРазгрузки(Команда)
		
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;		
	Если Не (ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации)) Тогда
		Возврат;
	КонецЕсли;
			
	ДокументРегистрации = ТекущиеДанные.ДокументРегистрации;
		
	УИД = Строка(Новый УникальныйИдентификатор());
	ДополнительныеПараметры = Новый Структура("ДокументРегистрации, УИД", ДокументРегистрации, УИД);
	
	Если ТекущиеДанные.ТипРегистрации = ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Приемка") Тогда
		
		ДополнительныеПараметры.Вставить("Состояние", 
			ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.Выгружен"));
						
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Подтвердите, что транспорт с номером %1 разгружен'"), 
			ТекущиеДанные.ТранспортноеСредство);
	Иначе	
		ДополнительныеПараметры.Вставить("Состояние",
			ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.Погружен"));	
			
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Подтвердите, что транспорт с номером %1 погружен'"), 
			ТекущиеДанные.ТранспортноеСредство); 
	КонецЕсли;
		
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПодверждениеРазгрузки", 
									ЭтотОбъект, ДополнительныеПараметры);

	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Да);
					
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтменитьРазрешение(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;		
	Если Не (ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументНаправлениеНаРазгрузку)) Тогда
		Возврат;
	КонецЕсли;	
		
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.ДокументНаправлениеНаРазгрузку);
	ПараметрыОткрытия.Вставить("ИзАРМа", Истина);
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
	
	ОткрытьФорму("Документ.гкс_НаправлениеНаРазгрузку.ФормаОбъекта", 
				ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено, 
				ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьСпецификацию(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;		
	Если Не (ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации)) Тогда
		Возврат;
	КонецЕсли;
		
	Спецификация = ПолучитьСпецификацию(ТекущиеДанные.ДокументРегистрации);
	
	Если ЗначениеЗаполнено(Спецификация) Тогда
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ", Спецификация);
		
		ОценкаПроизводительностиКлиент.ЗамерВремени(
			"АРМПриемка.ДокументСпецификацияКДоговоруКонтрагента.ОткрытиеФормы");
		
		ОткрытьФорму("Документ.гкс_СпецификацияКДоговоруКонтрагента.ФормаОбъекта", 
			ПараметрыОткрытия, ЭтотОбъект, ЭтотОбъект, , , , 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЭтикетки(Команда)
	
	ВыполнитьКомандуПечатиЭтикетки();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечатьАктОтбораПробЭкспресс(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;

	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрацииАнализов) Тогда

		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ТекущиеДанные.ДокументРегистрацииАнализов);
                  
		ОписаниеКоманды = Новый Структура();
		ОписаниеКоманды.Вставить("ОбъектыПечати", 	МассивОбъектов);
		ОписаниеКоманды.Вставить("Форма", 			ЭтотОбъект);
		ОписаниеКоманды.Вставить("Идентификатор", 	"ПФ_MXL_АктОтбораПроб_ru");
		ОписаниеКоманды.Вставить("Представление", 	НСтр("ru = 'Акт отбора проб'"));
		ОписаниеКоманды.Вставить("МенеджерПечати", 	"Обработка.гкс_ПечатьДокументаЛабораторныйАнализ");
		ОписаниеКоманды.Вставить("ПроверкаПроведенияПередПечатью", Истина);
		ОписаниеКоманды.Вставить("Обработчик",  
		 "гкс_УправлениеПечатьюКлиент.ВыполнитьКомандуПечати_ПФ_MXL_АктОтбораПроб_ru");

		гкс_УправлениеПечатьюКлиент.ВыполнитьКомандуПечати_ПФ_MXL_АктОтбораПроб_ru(ОписаниеКоманды);

	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен лабораторный анализ'"));		
	КонецЕсли;

	
КонецПроцедуры    

&НаКлиенте
Процедура КомандаПечатьАктОтбораПроб(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрацииАнализов) Тогда
		
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ТекущиеДанные.ДокументРегистрацииАнализов);
						
		ОписаниеКоманды = Новый Структура();
		ОписаниеКоманды.Вставить("ОбъектыПечати", 	МассивОбъектов);
		ОписаниеКоманды.Вставить("Форма", 			ЭтотОбъект);
		ОписаниеКоманды.Вставить("Идентификатор", 	"ПФ_MXL_АктОтбораПроб");
		ОписаниеКоманды.Вставить("Представление", 	НСтр("ru = 'Акт отбора проб'"));
    	ОписаниеКоманды.Вставить("МенеджерПечати", 	"Обработка.гкс_ПечатьДокументаЛабораторныйАнализ");
		ОписаниеКоманды.Вставить("ПроверкаПроведенияПередПечатью", Истина);
		ОписаниеКоманды.Вставить("Обработчик",  
									"гкс_УправлениеПечатьюКлиент.ВыполнитьКомандуПечати_ПФ_MXL_АктОтбораПроб");
		
		гкс_УправлениеПечатьюКлиент.ВыполнитьКомандуПечати_ПФ_MXL_АктОтбораПроб(ОписаниеКоманды);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен лабораторный анализ'"));		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечатьАктЗабраковкиПартии(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;

	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрацииАнализов) Тогда

		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ТекущиеДанные.ДокументРегистрацииАнализов);

		ОписаниеКоманды = Новый Структура();
		ОписаниеКоманды.Вставить("ОбъектыПечати", 	МассивОбъектов);
		ОписаниеКоманды.Вставить("Форма", 			ЭтотОбъект);
		ОписаниеКоманды.Вставить("Идентификатор", 	"ПФ_MXL_АктЗабраковкиПартии_ru");
		ОписаниеКоманды.Вставить("Представление", 	НСтр("ru = 'Акт забраковки'"));
		ОписаниеКоманды.Вставить("МенеджерПечати", 	"Обработка.гкс_ПечатьДокументаЛабораторныйАнализ");
		ОписаниеКоманды.Вставить("ПроверкаПроведенияПередПечатью", Истина);
		ОписаниеКоманды.Вставить("Обработчик",  
		 "гкс_УправлениеПечатьюКлиент.ВыполнитьКомандуПечати_ПФ_MXL_АктЗабраковкиПартии_ru");

		гкс_УправлениеПечатьюКлиент.ВыполнитьКомандуПечати_ПФ_MXL_АктЗабраковкиПартии_ru(ОписаниеКоманды);  

	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен лабораторный анализ'"));		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПечатьАктРасхожденияВВесе(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
		
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации) Тогда
		МассивОбъектов = Новый Массив;
		МассивОбъектов.Добавить(ТекущиеДанные.ДокументРегистрации);
		
		ОписаниеКоманды = Новый Структура();
		ОписаниеКоманды.Вставить("ОбъектыПечати", 	МассивОбъектов);
		ОписаниеКоманды.Вставить("Форма", 			ЭтотОбъект);
		ОписаниеКоманды.Вставить("Идентификатор", 	"ПФ_MXL_АктРасхожденияВеса_by");
		ОписаниеКоманды.Вставить("Представление", 	НСтр("ru = 'Акт расхождения веса'"));
    	ОписаниеКоманды.Вставить("МенеджерПечати", 	"Обработка.гкс_ПечатьАктРасхожденияВеса");
		ОписаниеКоманды.Вставить("ПроверкаПроведенияПередПечатью", Истина);
		ОписаниеКоманды.Вставить("Обработчик",  
									"гкс_УправлениеПечатьюКлиент_by.ВыполнитьКомандуПечати_АктРасхожденияВеса");
		
		гкс_УправлениеПечатьюКлиент_by.ВыполнитьКомандуПечати_АктРасхожденияВеса(ОписаниеКоманды);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнена регистрация на ПЛК'"));		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьРегистрацию(Команда)
		
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;		
	Если Не (ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации)) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект); 
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначениеКопирования", ТекущиеДанные.ДокументРегистрации);
	ПараметрыФормы.Вставить("ИзАРМа", Истина);
	
	ОткрытьФорму("Документ.гкс_РегистрацияНаПЛК.Форма.ФормаДокумента", 
		ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено, 
		ОписаниеОповещенияОЗакрытии);
	              	              
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастройкаНазначенияРазгрузки(Команда)
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("НастройкаЯмРазгрузки_Завершение", ЭтотОбъект);
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТочкаМаршрута", ОтборТочкаМаршрута);
	ЗначенияЗаполнения.Вставить("ВидПеревозки", ОтборВидПеревозки);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", 	ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ИзАРМа", 				Истина);

	ОткрытьФорму("Документ.гкс_УстановкаНастроекНазначенияРазгрузки.ФормаОбъекта", 
					ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , ,
					ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ДопускФумигационный(Команда)
	
	ИзменитьДопуск(ПредопределенноеЗначение("Перечисление.гкс_ДопускиКВскрытиюВагонов.Фумигационный"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДопускФитосанитарный(Команда)
	
	ИзменитьДопуск(ПредопределенноеЗначение("Перечисление.гкс_ДопускиКВскрытиюВагонов.Фитосанитарный"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДопускПретензионный(Команда)
	
	ИзменитьДопуск(ПредопределенноеЗначение("Перечисление.гкс_ДопускиКВскрытиюВагонов.Претензионный"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьФормуПринятыеТС(Команда)
	
	ПараметрыФормы = Новый Структура;
		
	Если ЭтоАвтоПеревозка 
		И гкс_ПриемкаТранспортаВызовСервера.ФормироватьКомпозитныйЛабораторныйАнализ() Тогда
		
		ИмяНовойФормы = "Обработка.гкс_ПриемкаТранспорта.Форма.ФормаПринятыеТС_by";
		
		ПараметрыФормы.Вставить("ВидПеревозки", 
			ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Приемка"));
		ПараметрыФормы.Вставить("ТипРегистрации", 
			ПредопределенноеЗначение("Перечисление.гкс_ТипыТранспортныхСредствДоставки.Автомобиль"));	
	Иначе
		ИмяНовойФормы = "Обработка.гкс_ПриемкаТранспорта.Форма.ФормаПринятыеТС";
		ПараметрыФормы.Вставить("ВидПеревозки", ОтборВидПеревозки);
		ПараметрыФормы.Вставить("ТипРегистрации", ОтборТипРегистрации);
	КонецЕсли;
	
	ОткрытьФорму(ИмяНовойФормы, ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьПричинуПретензионности(Команда)
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТекущиеДанные.Претензионный Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСтроки = Новый Структура;
	ПараметрыСтроки.Вставить("Вагон", 				ТекущиеДанные.ТранспортноеСредство); 
	ПараметрыСтроки.Вставить("ДокументРегистрации", ТекущиеДанные.ДокументРегистрации);	
	
	ТекстПричины = ПолучитьПричинуПретензионности(ПараметрыСтроки);
	
	ПоказатьЗначение(, ТекстПричины);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФормы(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПодготовитьПараметрыОткрытияФормыНастроек(ПараметрыОткрытия);	
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ЗакрытиеНастройкиФормыЭлементов", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.гкс_ХранилищеНастроек.Форма.НастройкаЭлементовФормы", ПараметрыОткрытия, 
					ЭтотОбъект, УникальныйИдентификатор, , , 
					ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометки(Команда)
	
	УстановитьПометку(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометки(Команда)
	
	УстановитьПометку(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ГрупповаяОбработкаРегистраций(Команда)
	
	ОчиститьСообщения();
	
	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();
	Если ВыбранныеРегистрации.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Необходимо выбрать строки.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ГрупповаяНоменклатура = НоменклатураВВыбранныхСтроках(ВыбранныеРегистрации);	
	Если Не ЗначениеЗаполнено(ГрупповаяНоменклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Ложь);	
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", ГрупповаяНоменклатура);
	ПараметрыФормы.Вставить("ВыбранныеРегистрации", ВыбранныеРегистрации);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.гкс_ПриемкаТранспорта.Форма.ГрупповоеИзменениеРегистраций",
					ПараметрыФормы, ЭтотОбъект, , , , ОповещениеОЗакрытии,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры


						
&НаКлиенте
Процедура ПропуститьКонтрольКачества(Команда)
	
	ОчиститьСообщения();
	
	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();
	
	Если ВыбранныеРегистрации.Количество() = 0 Тогда	
		ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;	
		Если ТекущиеДанные <> Неопределено Тогда
			ВыбранныеРегистрации.Добавить(ТекущиеДанные.ДокументРегистрации);
		КонецЕсли;
	КонецЕсли;
	
	// Проверить состояние "Готов к отбору проб"
	ВалидныеСостояния = Новый Массив;
	ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.ГотовКОтборуПроб"));	
	Если Не КонтрольВыбранныхПоСостоянию(ВыбранныеРегистрации, ВалидныеСостояния, Истина) Тогда		
		ТекстСообщения = НСтр("ru='Невозможно изменить состояние регистраций'");	
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
		
	Если ВыбранныеРегистрации.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Необходимо выбрать строки.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
		
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения(
		"ПропуститьКонтрольКачестваПодтверждение", ЭтотОбъект, ВыбранныеРегистрации); 
					
	ТекстВопроса = НСтр("ru = 'Пропустить входной контроль для выбранных регистраций?'");			
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Инициалиализация

&НаСервере
Процедура ИнициализироватьЭлементыФормы()
	
	УстановитьЗаголовкиВКолонкахСпискаРегистрации();
	ОбновитьНаправленияРазгрузки();

КонецПроцедуры 

&НаСервере
Процедура ИнициализироватьРеквизитыФормы()
	
	ИнициализироватьЗаголовокФормы();
	ИнициализироватьТекущегоПользователя();
	ИнициализироватьРеквизитыОтбора();
	ИнициализироватьСтруктуруДоступов();
	ИнициализироватьРеквизитыПравПользователя();
	
	ИнициализироватьРеквизитыПризнакиТипаПеревозки();
	ИнициализироватьРеквизитыПриемкиЖД();
	ИнициализироватьРеквизитыВремяОжиданияТС();
	ИнициализироватьАвтоматическоеФормированиеДокументов(); 
	ИнициализироватьСписокРегистрации(); 
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьЗаголовокФормы()
	
	Если Параметры.Свойство("АвтоПриемка") Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru='Рабочее место ""Приемка и отгрузка на ПЛК""'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьТекущегоПользователя()
	
	Пользователь = Пользователи.ТекущийПользователь();
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь(); 
	
КонецПроцедуры
 
&НаСервере
Процедура ИнициализироватьРеквизитыОтбора()
	
	Если Параметры.Свойство("АвтоПриемка") Тогда
		ОтборТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка;
	КонецЕсли;
	
	ИнициализироватьОтборТочкаМаршрута();
	
	Если ЗначениеЗаполнено(ОтборТочкаМаршрута) Тогда
		ОтборВидПеревозки = гкс_ПриемкаНаПЛКСервер.ПолучитьВидПеревозкиПоУмолчанию(ОтборТочкаМаршрута);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОтборТочкаМаршрута()
	
	ИспользоватьНесколькоАЛЦ = ПолучитьФункциональнуюОпцию("гкс_ИспользоватьНесколькоАЛЦ");
	
	Элементы.ОтборТочкаМаршрута.Доступность = ИспользоватьНесколькоАЛЦ;
	Элементы.ОтборТочкаМаршрута.РежимВыбораИзСписка = ИспользоватьНесколькоАЛЦ;
	
	АЛЦПользователя = Новый Массив;
	ОтборТочкаМаршрута = 
		гкс_ПриемкаТранспорта.ПЛКизНастроекБазыПользователя(ИспользоватьНесколькоАЛЦ, АЛЦПользователя);
	Если ИспользоватьНесколькоАЛЦ
			И ЗначениеЗаполнено(АЛЦПользователя) Тогда
		СписокВыбора = Элементы.ОтборТочкаМаршрута.СписокВыбора;
		СписокВыбора.ЗагрузитьЗначения(АЛЦПользователя);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСтруктуруДоступов()
	
	Если ЗначениеЗаполнено(ОтборТочкаМаршрута) И ЗначениеЗаполнено(ОтборВидПеревозки) Тогда
		СтруктураДоступов = ПолучитьСтруктуруДоступовНаСервере(Истина);
	Иначе
		СтруктураДоступов = ПолучитьСтруктуруДоступовНаСервере(Ложь);
	КонецЕсли;   	
		
КонецПроцедуры 

&НаСервере
Процедура ИнициализироватьРеквизитыПравПользователя()
	
	ЕстьПравоНаРедактированиеРегистрации 	= ПравоНаРедактированияРегистрации(); 
	ЕстьПравоНаРедактированиеВзвешивания 	= ЕстьПравоНаРедактирование("гкс_Взвешивание");
	ЕстьПравоНаРедактированиеНомераПробы 	= ЕстьПравоНаРедактирование("гкс_ФормированиеНомераПробы");
	ЕстьПравоНаРедактированиеЛабАнализа 	= ЕстьПравоНаРедактирование("гкс_ЛабораторныйАнализ");
	ЕстьПравоНаРедактированиеНаправленияНаРазгрузку = ЕстьПравоНаРедактирование("гкс_НаправлениеНаРазгрузку");
	ЕстьПравоНаРедактированиеПриемкаТранспортаБезВходногоКонтроля 
		= ПравоДоступа("Изменение", Метаданные.РегистрыСведений["гкс_ПриемкаТранспортаБезВходногоКонтроля"]);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоПриемкаТранспорта(ТипРегистрации)
	
	Возврат ТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка;
	
КонецФункции 

&НаСервере
Процедура ИнициализироватьРеквизитыПризнакиТипаПеревозки()
	
	ТипыТранспортныхСредств = Перечисления.гкс_ТипыТранспортныхСредствДоставки;
	
	ЭтоЖДПеревозка 		= ТипыТранспортныхСредств.ЭтоЖДПеревозка(ОтборВидПеревозки);
	ЭтоАвтоПеревозка 	= ТипыТранспортныхСредств.ЭтоАвтоПеревозка(ОтборВидПеревозки);
		
КонецПроцедуры                       

&НаСервере
Процедура ИнициализироватьРеквизитыПриемкиЖД()
	
	РазрешитьВходнойКонтрольДоВзвешивания = гкс_ПриемкаТранспорта.
		РазрешитьВходнойКонтрольДоВзвешивания(ОтборТипРегистрации, ОтборВидПеревозки);		
			
КонецПроцедуры	

&НаСервере
Процедура ИнициализироватьРеквизитыВремяОжиданияТС()
	
	Если ЗначениеЗаполнено(ОтборТочкаМаршрута) Тогда
		ЧасовойПоясПЛК = УстановитьЧасовойПоясПЛК();
	КонецЕсли; 
	
КонецПроцедуры
  
&НаСервере
Процедура ИнициализироватьАвтоматическоеФормированиеДокументов()
	
	АвтоматическоеФормированиеДокументов = 
		гкс_РаботаСНастройками.ЕстьАвтоматическоеФормированиеДокументовДляТочкиМаршрута(ОтборТочкаМаршрута);

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСписокРегистрации()
	
	УстановитьТекстЗапросаСпискаРегистрации();
	УстановитьОтборыСпискаРегистрации();
	
КонецПроцедуры

#КонецОбласти 

#Область ВидимостьДоступность

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементовФормы()
		
	УстановитьВидимостьДоступностьКомандФормы();
	УстановитьВидимостьЭлементовСпискаРегистрации();
	УстановитьВидимостьДоступностьИныхЭлемнотовФормы();
	УстановитьВидимостьДоступностьПринятыеТС();

КонецПроцедуры                                       
 
&НаСервере
Процедура УстановитьВидимостьДоступностьКомандФормы()
		
	Элементы.СписокРегистрацииДопускФумигационный.Видимость = СтруктураДоступов.ДоступенДопускФумигационный; 
	Элементы.СписокРегистрацииДопускФитосанитарный.Видимость = СтруктураДоступов.ДоступенДопускФитосанитарный;  	
	Элементы.СписокРегистрацииДопускПретензионный.Видимость = СтруктураДоступов.ДоступенДопускПретензионный;
	
	Элементы.ПодменюДопуски.Видимость = СтруктураДоступов.ДоступенДопускаКВскрытиюВагонов И ЭтоЖДПеревозка;
	Элементы.СписокРегистрацииКонтекстноеМенюПричинаПретензионности.Видимость = ЭтоЖДПеревозка;  
	
	Элементы.СписокРегистрацииСкопироватьРегистрацию.Видимость = СтруктураДоступов.ДоступенРегистрация; 
		   		
	Элементы.СписокРегистрацииКнопкаДобавить.Видимость = 
		ЕстьПравоНаРедактированиеРегистрации Или ЭтоПолноправныйПользователь;
		
	Элементы.СписокРегистрацииКПП.Видимость = 
		(ЕстьПравоНаРедактированиеРегистрации Или ЭтоПолноправныйПользователь) И ЭтоАвтоПеревозка;
		 
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Видимость = 
		СписокРегистрацииОтклонитьРегистрациюАвтоВидимость(СтруктураДоступов);
	
	Элементы.РедактироватьРегистрацию.Видимость = 
		СтруктураДоступов.ДоступенРегистрация;
	
	Элементы.РедактироватьВесНаВъезде.Видимость = 
		СтруктураДоступов.ДоступенВзвешиваниеНаВъезде;

	ЭтоПриемкаТранспорта = ЭтоПриемкаТранспорта(ОтборТипРегистрации);	
		
	Элементы.РедактироватьНомерПробы.Видимость = 
		СтруктураДоступов.ДоступенОтборПроб И ЭтоПриемкаТранспорта;
	
	Элементы.КомандаПробыПриняты.Видимость = 
		СтруктураДоступов.ДоступенПринятиеПроб И ЭтоПриемкаТранспорта;
	
	Элементы.РедактироватьАнализ.Видимость = 
		СтруктураДоступов.ДоступенЛабАнализ И ЭтоПриемкаТранспорта;
	
	Элементы.РедактироватьНаправлениеНаРазгрузку.Видимость = 
		СтруктураДоступов.ДоступенДиспетчер И ЭтоПриемкаТранспорта;
		
	Элементы.КомандаОтменитьРазрешение.Видимость = 
		СтруктураДоступов.ДоступенПодтверждениеРазгрузки И ЭтоПриемкаТранспорта;	
	
	Элементы.КомандаПодтверждениеРазгрузки.Видимость = 
		СтруктураДоступов.ДоступенПодтверждениеРазгрузки;
	
	Элементы.РедактироватьВесНаВыезде.Видимость =
		СтруктураДоступов.ДоступенВзвешиваниеНаВыезде;
		
	Элементы.РедактированиеПриемка.Видимость = 
		СтруктураДоступов.ДоступенОформлениеПриемки;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьПропуститьКонтрольКачества()
	
   ЭтоПриемкаТранспорта = ЭтоПриемкаТранспорта(ОтборТипРегистрации);

   Элементы.СписокРегистрацииПропуститьКонтрольКачества.Видимость = 
		ЭтоПриемкаТранспорта И ЭтоЖДПеревозка;
	
КонецПроцедуры  

&НаСервереБезКонтекста
Функция СписокРегистрацииОтклонитьРегистрациюАвтоВидимость(СтруктураДоступов)

	Возврат СтруктураДоступов.ДоступенРегистрация
		Или СтруктураДоступов.ДоступенДиспетчер
		Или СтруктураДоступов.ДоступенЛабАнализ
		Или СтруктураДоступов.ДоступенВзвешиваниеНаВъезде
		Или СтруктураДоступов.ДоступенОтборПроб;
		
КонецФункции	

&НаСервере
Процедура УстановитьВидимостьЭлементовСпискаРегистрации()
	
	Элементы.СписокРегистрацииТочкаМаршрута.Видимость 	= Не ЗначениеЗаполнено(ОтборТочкаМаршрута);
	Элементы.СписокРегистрацииВидПеревозки.Видимость 	= Не ЗначениеЗаполнено(ОтборВидПеревозки);
	Элементы.СписокРегистрацииИндексКартинки.Видимость 	= Не ЗначениеЗаполнено(ОтборТипРегистрации);
		
	Если ЗначениеЗаполнено(ОтборТочкаМаршрута) И ЗначениеЗаполнено(ОтборВидПеревозки) Тогда
		
		Элементы.СписокРегистрацииФумигационный.Видимость 	= ЭтоЖДПеревозка;
		Элементы.СписокРегистрацииФитосанитарный.Видимость 	= ЭтоЖДПеревозка;
		Элементы.СписокРегистрацииПретензионный.Видимость 	= ЭтоЖДПеревозка; 
		 
		Элементы.СписокРегистрацииЖиваяОчередь.Видимость 	= ЭтоАвтоПеревозка; 
		Элементы.СписокРегистрацииВремяЗаписи.Видимость 	= ЭтоАвтоПеревозка;  
		Элементы.СписокРегистрацииВодитель.Видимость 		= ЭтоАвтоПеревозка;
	КонецЕсли;
	
	ЕстьКолонкаПротеин 
		= ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, "СписокРегистрацииПротеин");
	
	Если ЕстьКолонкаПротеин Тогда 
		ИспользоватьИнтеграциюСWarehouse = ПолучитьФункциональнуюОпцию("гкс_ИспользоватьИнтеграциюСWarehouse");
		Элементы.СписокРегистрацииПротеин.Видимость = ИспользоватьИнтеграциюСWarehouse;
	КонецЕсли;
			
КонецПроцедуры  

&НаСервере
Процедура УстановитьВидимостьДоступностьИныхЭлемнотовФормы()
	
	УстановленОтборТочкаМаршрута 	= ЗначениеЗаполнено(ОтборТочкаМаршрута);
	УстановленОтборВидПеревозки 	= ЗначениеЗаполнено(ОтборВидПеревозки);
			
	Элементы.ГруппаОсновная.Видимость 	= УстановленОтборТочкаМаршрута И УстановленОтборВидПеревозки; 
	Элементы.ГруппаГлавная.Видимость 	= УстановленОтборТочкаМаршрута И УстановленОтборВидПеревозки;
		
	Элементы.Водитель.Видимость = ЭтоАвтоПеревозка;
	
	Элементы.ГруппаПрибытиеТС.Видимость = Не ЭтоАвтоПеревозка;
	Элементы.ГруппаПрибытиеТС.Заголовок = "Прибытие вагонов";
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьПринятыеТС()
	
	ЕстьПравоНаЗПП3 = ЕстьПравоНаРедактирование("гкс_РеестрНакладныхЗПП3");
	
	Элементы.ГруппаПринятыеТС.Видимость = ЕстьПравоНаЗПП3;
	
КонецПроцедуры

#КонецОбласти 

#Область РаботаСДопускамиЖДПеревозок

&НаКлиенте
Процедура ИзменитьДопуск(ТипДопуска)
	
	Если Не (ЭтоЖДПеревозка 
		И ЭтоПриемкаТранспорта(ОтборТипРегистрации)) Тогда	
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();
	
	Если ВыбранныеРегистрации.Количество() = 0 Тогда
		Если ТекущиеДанные <> Неопределено  Тогда
			МассивИсточник = Новый Массив;
			МассивИсточник.Добавить(ТекущиеДанные.ДокументРегистрации);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВыбранныеРегистрации, МассивИсточник, Истина);			
		Иначе
			ТекстСообщения = НСтр("ru='Необходимо выбрать строки.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеВскрытиеЗапрещено = СтатусДопускаВВыбранныхСтроках(ВыбранныеРегистрации, ТипДопуска);
	Если ЗначениеВскрытиеЗапрещено <> Неопределено Тогда
		НовоеЗначениеВскрытиеЗапрещено = Не ЗначениеВскрытиеЗапрещено;
	Иначе
		Возврат;
	КонецЕсли;	
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипДопуска", ТипДопуска);
	ПараметрыФормы.Вставить("ВыбранныеРегистрации", ВыбранныеРегистрации);
	ПараметрыФормы.Вставить("НовоеЗначение", НовоеЗначениеВскрытиеЗапрещено);
	                                          
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ИзменитьДопускЗавершение", ЭтотОбъект);
											
	ОткрытьФорму("Обработка.гкс_ПриемкаТранспорта.Форма.ИзменениеДопускаТранспорта",
					ПараметрыФормы, ЭтотОбъект, , , , ОповещениеОЗакрытии,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);								
			
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДопускЗавершение(Комментарий, ДополнительныеПараметры) Экспорт
		
	ПодключениеОбработчикаОжиданияОбновления(Истина, Ложь);	

КонецПроцедуры

#Область РаботаССостояниемРегистрации

&НаСервере
Процедура ЗарегистрироватьНовоеСостояние(НовоеСостояние, ДокументРегистрации, Комментарий = "")
	
	гкс_ПриемкаТранспорта.ЗарегистрироватьНовоеСостояние(ДокументРегистрации, НовоеСостояние, Комментарий);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СлужебнаяНоменклатураДляФормированияНаправленияНаРагрузку(ЛабораторныйАнализ)
	
	Если ЗначениеЗаполнено(ЛабораторныйАнализ) Тогда
		
		РеквизитыАнализа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ЛабораторныйАнализ, "Дата, гкс_ДокументРегистрации");
				
		Возврат 
			гкс_ПриемкаТранспорта.ПодходящаяСлужебнаяНоменклатураДляФормированияНаправленияНаРагрузку(
				РеквизитыАнализа.Дата, РеквизитыАнализа.гкс_ДокументРегистрации, ЛабораторныйАнализ);
	Иначе
		Возврат Справочники.Номенклатура.ПустаяСсылка();
	КонецЕсли;	
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура УстановитьПометку(Пометка, РегистрацияНаПЛК = Неопределено)

	Если ЗначениеЗаполнено(РегистрацияНаПЛК) Тогда
		
		// если прежде был выбран "все регистрации", то при изменении режима сформируем список выбранных
		Если ЭтоУстановкаПометокДляВсехПозиций Тогда		
			РегистрацииГрупповойОбработки = РегистрацииГрупповойОбработки();
			РегистрацииВыбранные.ЗагрузитьЗначения(РегистрацииГрупповойОбработки);
			ЭтоУстановкаПометокДляВсехПозиций = Ложь;
		КонецЕсли;	
		
		ИндексПозицииДобавления = РегистрацииВыбранные.НайтиПоЗначению(РегистрацияНаПЛК);
		ИндексПозицииУдаления   = РегистрацииИсключенные.НайтиПоЗначению(РегистрацияНаПЛК);

		Если Пометка Тогда
			Если ИндексПозицииУдаления <> Неопределено Тогда
				РегистрацииИсключенные.Удалить(ИндексПозицииУдаления);
			Иначе
				РегистрацииВыбранные.Добавить(РегистрацияНаПЛК);
			КонецЕсли;
		Иначе		
			Если ИндексПозицииДобавления <> Неопределено Тогда
				РегистрацииВыбранные.Удалить(ИндексПозицииДобавления);
			Иначе
				РегистрацииИсключенные.Добавить(РегистрацияНаПЛК);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		РегистрацииВыбранные.Очистить();
		РегистрацииИсключенные.Очистить();
		ЭтоУстановкаПометокДляВсехПозиций = Пометка;
			
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ГрупповоеСозданиеНомерПробы(ВыбранныеРегистрации)
	
	ОчиститьСообщения();

	// Проверить состояния
	ВалидныеСостояния = Новый Массив;
	ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.ГотовКОтборуПроб"));
	
	Если РазрешитьВходнойКонтрольДоВзвешивания Тогда
		 ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.Прибыл"));
	КонецЕсли;
	
	Если Не КонтрольВыбранныхПоСостоянию(ВыбранныеРегистрации, ВалидныеСостояния, Истина) Тогда		
		ТекстСообщения = НСтр("ru='Невозможно сделать групповое формирование номеров проб'");	
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Ложь);
		
	ДлительнаяОперация = ВыполнитьСозданиеНомеровПробНаСервере(ВыбранныеРегистрации);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Вставить("ВыводитьСообщения", Истина);
		
	ОписаниеОповещения = Новый ОписаниеОповещения("СозданиеНомеровПробГрупповаяЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрупповоеНаправлениеНаРазгрузку(ВыбранныеРегистрации)
	
	ОчиститьСообщения();
	
	// Проверить состояния
	ВалидныеСостояния = Новый Массив;
	ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.КачествоПринято"));
	ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.ВыгрузкаРазрешена"));
	ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.РезультатыПробПолучены"));
	
	Если Не (КонтрольВыбранныхПоСостоянию(ВыбранныеРегистрации, ВалидныеСостояния, Истина) 
		И КонтрольПрохожденияНеобходимыхСтатусов(ВыбранныеРегистрации)) Тогда		
		ТекстСообщения = НСтр("ru='Невозможно сделать групповое направление на разгрузку'");	
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли; 
		
	ГрупповаяНоменклатура = НоменклатураВВыбранныхСтроках(ВыбранныеРегистрации);
	Если Не ЗначениеЗаполнено(ГрупповаяНоменклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Ложь);		
		
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ВыбранныеРегистрации", ВыбранныеРегистрации);
	ПараметрыОткрытияФормы.Вставить("Номенклатура", ГрупповаяНоменклатура);
	ПараметрыОткрытияФормы.Вставить("ТочкаМаршрута", ОтборТочкаМаршрута);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.гкс_ПриемкаТранспорта.Форма.ГрупповоеНаправлениеНаРазгрузку",
		ПараметрыОткрытияФормы, ЭтотОбъект, , , , ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);		
		
КонецПроцедуры
								
&НаКлиенте
Процедура ГрупповоеПодтверждениеПробы(ВыбранныеРегистрации)
	
	ОчиститьСообщения();
	
	// Проверить состояние "Взяты пробы"
	ВалидныеСостояния = Новый Массив;
	ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.ВзятыПробы"));	
	Если Не КонтрольВыбранныхПоСостоянию(ВыбранныеРегистрации, ВалидныеСостояния, Истина) Тогда		
		ТекстСообщения = НСтр("ru='Невозможно изменить состояние регистраций'");	
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПробыПринятыГрупповаяОбработкаПодверждение",
		ЭтотОбъект, ВыбранныеРегистрации); 
					
	ТекстВопроса = НСтр("ru = 'Подтвердите, что все выбранные пробы будут приняты'");				
	ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#Область ОбратныеВызовы

&НаКлиенте
// Оповещение после подтверждения принятия проб
//
// Параметры:
//  Результат - КодВозвратаДиалога - Да или Нет.
//  ДополнительныеПараметры - Структура - Параметры для формирования записи в регистре при подтверждении разгрузки.
//
Процедура ПодверждениеПробыПриняты(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда	
		ЗарегистрироватьНовоеСостояние(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.ПринятыПробы"), 
										ДополнительныеПараметры.ДокументРегистрации);
		
		ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Оповещение после подтверждения разгрузки
//
// Параметры:
//  Результат - КодВозвратаДиалога - Да или Нет.
//  ДополнительныеПараметры - Структура - Параметры для формирования записи в регистре при подтверждении разгрузки.
//
Процедура ПодверждениеРазгрузки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда	
		ЗарегистрироватьНовоеСостояние(ДополнительныеПараметры.Состояние, ДополнительныеПараметры.ДокументРегистрации);
		ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьКонтрольКачестваПодтверждение(Результат, ВыбранныеРегистрации) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПодключениеОбработчикаОжиданияОбновления(Ложь);
		
		ДлительнаяОперация = ВыполнитьПропускКонтроляКачестваНаСервере(ВыбранныеРегистрации);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.Вставить("ВыводитьСообщения", Истина);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПропуститьКонтрольКачестваЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПропуститьКонтрольКачестваЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ТекстСообщения = НСтр("ru = 'Обработка документов завершена'");		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
	
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Произошла ошибка пропуска контроля качества %1 %2'"), 
			Символы.ПС, Результат.КраткоеПредставлениеОшибки);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПропускКонтроляКачестваНаСервере(ВыбранныеРегистрации)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.Вставить("ОжидатьЗавершение", Неопределено);
	ПараметрыВыполнения.Вставить("ЗапуститьНеВФоне", Истина);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
								"Обработки.гкс_ПриемкаТранспорта.ЗарегистрироватьПропуcкКонтроляКачества",
								ВыбранныеРегистрации);
			
КонецФункции
							
&НаКлиенте
// Оповещение после подтверждения принятия проб
//
Процедура ПробыПринятыГрупповаяОбработкаПодверждение(Результат, ВыбранныеРегистрации) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПодключениеОбработчикаОжиданияОбновления(Ложь);
		
		ДлительнаяОперация = ВыполнитьПробыПринятыНаСервере(ВыбранныеРегистрации);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.Вставить("ВыводитьСообщения", Истина);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПробыПринятыГрупповаяЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
		
	КонецЕсли;	 
			
КонецПроцедуры

&НаКлиенте
Процедура ПробыПринятыГрупповаяЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ТекстСообщения = НСтр("ru = 'Обработка документов завершена'");		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Произошла ошибка установки отметки принятия проб %1 %2'"), 
			Символы.ПС, Результат.КраткоеПредставлениеОшибки);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Ложь);
	
КонецПроцедуры
							
&НаСервере
Функция ВыполнитьПробыПринятыНаСервере(ВыбранныеРегистрации)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.Вставить("ОжидатьЗавершение", Неопределено);
	ПараметрыВыполнения.Вставить("ЗапуститьНеВФоне", Истина);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
								"Обработки.гкс_ПриемкаТранспорта.ЗарегистрироватьНовоеСостояниеПоСписку",
								ВыбранныеРегистрации, Перечисления.гкс_СостоянияРегистрации.ПринятыПробы);
			
КонецФункции
							
&НаСервере
Функция ВыполнитьСозданиеНомеровПробНаСервере(ВыбранныеРегистрации)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Дата", 				ТекущаяДатаСеанса());
	ЗначенияЗаполнения.Вставить("ТочкаМаршрута", 		ОтборТочкаМаршрута);
	ЗначенияЗаполнения.Вставить("ТипПробы", 			Перечисления.гкс_ТипыПроб.Единичная);
			
	ЧасовойПоясТочки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборТочкаМаршрута, "гкс_ЧасовойПоясТочкиМаршрута");
	Если ЗначениеЗаполнено(ЧасовойПоясТочки) Тогда
		МестнаяДата = МестноеВремя(ТекущаяУниверсальнаяДата(), ЧасовойПоясТочки);			
	Иначе
		МестнаяДата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ЗначенияЗаполнения.Вставить("МестнаяДата", МестнаяДата);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.Вставить("ОжидатьЗавершение", Неопределено);
	ПараметрыВыполнения.Вставить("ЗапуститьНеВФоне", Истина);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
								"Обработки.гкс_ПриемкаТранспорта.ГрупповоеСозданиеНомеровПроб",
								ЗначенияЗаполнения, ВыбранныеРегистрации);
			
КонецФункции
							
&НаКлиенте
Процедура СозданиеНомеровПробГрупповаяЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ТекстСообщения = НСтр("ru = 'Обработка документов завершена'");		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		АвтоматическаяПечатьЭтикеток = гкс_ОбщегоНазначенияВызовСервера.
			ЗначениеКонстанты("гкс_АвтоматическаяПечатьЭтикеток");
		
		Если АвтоматическаяПечатьЭтикеток Тогда 
			ВыполнитьКомандуПечатиЭтикетки(); 
		КонецЕсли;
	
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Произошла ошибка формирования номеров проб %1 %2'"), 
			Символы.ПС, Результат.КраткоеПредставлениеОшибки);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЯмРазгрузки_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбновитьНаправленияРазгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьНомерПробыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	//@skip-warning
	Если ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("ЗаписатьДанныеДляСАРА") И Результат.ЗаписатьДанныеДляСАРА Тогда
			
		ЗаписатьДанныеДляСАРАНаКлиенте(Результат);
	КонецЕсли;
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияОбновитьФорму(Результат, ДополнительныеПараметры) Экспорт
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьВесНаВыездеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;	
	Если ТекущиеДанные = Неопределено Тогда	
		Возврат;
	КонецЕсли;
		
	Если ВозможноАвтоЗавершениеПроцесса(ОтборТипРегистрации, ТекущиеДанные.Состояние) Тогда
		РедактированиеПриемка(Команды.РедактированиеПриемка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРегистрациюНаОснованииВъездаНаКПП(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) И ТипЗнч(Результат) = Тип("ДокументСсылка.гкс_ВъездНаКПП") Тогда
						
		ЗначенияЗаполнения = ЗначенияЗаполненияРегистрацииНаПЛК();		
		ЗначенияЗаполнения.Вставить("ДокументОснование", Результат);
		
		ОткрытьФормуНовогоДокументаРегистрацияНаПЛК(ЗначенияЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеНастройкиФормыЭлементов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено И Результат Тогда
		ЗагрузитьНастройкиФормыИзХранилища();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Подключает/отключает обработчик ожидания для обновления списка.
//
// Параметры:
//	Подключать 			- Булево
//
Процедура ПодключениеОбработчикаОжиданияОбновления(Подключать = Истина, Однократно = Истина) Экспорт
	
	Если Не Элементы.ГруппаОсновная.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Если Подключать Тогда		
		Интервал = ?(Однократно, 0.5, 7);
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеНаФорме", Интервал, Однократно);
	Иначе
		ОтключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеНаФорме");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКомандУправелнияРегистрацейПоТекущемуСостоянию

#Область ОбщиеПроцедурыУправленияДоступностьюКомандамиРегистрации

&НаКлиенте
Процедура УстановитьДоступностьКнопокУправленияРегистрациейТранспортаПоУмолчанию()
	
	Элементы.РедактироватьРегистрацию.Доступность 				= Ложь;
	Элементы.КомандаПробыПриняты.Доступность 					= Ложь;
	Элементы.РедактироватьНаправлениеНаРазгрузку.Доступность 	= Ложь;
	Элементы.КомандаПодтверждениеРазгрузки.Доступность 			= Ложь;
	Элементы.РедактироватьАнализ.Доступность 					= Ложь;
	Элементы.РедактироватьВесНаВъезде.Доступность 				= Ложь;
	Элементы.РедактироватьНомерПробы.Доступность 				= Ложь;
	Элементы.РедактироватьВесНаВыезде.Доступность 				= Ложь;
	Элементы.РедактированиеПриемка.Доступность 					= Ложь;
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = Ложь;
	Элементы.КомандаОтменитьРазрешение.Доступность 				= Ложь; 
	Элементы.СписокРегистрацииДопускФумигационный.Доступность 	= Ложь;
	Элементы.СписокРегистрацииДопускФитосанитарный.Доступность 	= Ложь;
	Элементы.СписокРегистрацииДопускПретензионный.Доступность 	= Ложь;
	Элементы.СписокРегистрацииПечатьЭтикетки.Доступность 		= Истина;
	Элементы.СписокРегистрацииКонтекстноеМенюПричинаПретензионности.Доступность = Ложь;
	Элементы.СписокРегистрацииПропуститьКонтрольКачества.Доступность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандУправленияДопусками()

	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ЭтоПриемкаЖДТранспорта = ЭтоЖДПеревозка И ЭтоПриемкаТранспорта(ОтборТипРегистрации);

	Элементы.СписокРегистрацииДопускФумигационный.Доступность 	= ЭтоПриемкаЖДТранспорта;
	Элементы.СписокРегистрацииДопускФитосанитарный.Доступность 	= ЭтоПриемкаЖДТранспорта;
	Элементы.СписокРегистрацииДопускПретензионный.Доступность 	= ЭтоПриемкаЖДТранспорта;
	
	Элементы.СписокРегистрацииКонтекстноеМенюПричинаПретензионности.Доступность = 
		ЭтоПриемкаЖДТранспорта И ТекущиеДанные.Претензионный;

КонецПроцедуры 
	
&НаКлиенте
Функция ДоступностьКнопкиВесНаВъезде(СтруктураДоступов)
	Возврат 
		РазрешитьВходнойКонтрольДоВзвешивания
		И СтруктураДоступов.ДоступенВзвешиваниеНаВъезде
		И ЕстьПравоНаРедактированиеВзвешивания;
КонецФункции  

&НаКлиенте
Функция ДоступностьКнопкиНомерПробы(СтруктураДоступов)
	Возврат
		РазрешитьВходнойКонтрольДоВзвешивания
		И СтруктураДоступов.ДоступенОтборПроб 
		И ЕстьПравоНаРедактированиеНомераПробы;
КонецФункции
	
&НаКлиенте
Функция ДоступностьКнопкиНаправлениеНаРазгрузку(Регистрация, СтруктураДоступов)
	
	Возврат ЕстьПраваНаправлениеНаРазгрузку(СтруктураДоступов) И НаправлятьНаРазгрузкуРазрешение(Регистрация);
	
КонецФункции   

&НаСервереБезКонтекста
Функция НаправлятьНаРазгрузкуРазрешение(Регистрация)
	
	Возврат гкс_ПриемкаТранспорта.НаправлятьНаРазгрузкуРазрешение(Регистрация);
	
КонецФункции

&НаКлиенте
Функция ЕстьПраваНаправлениеНаРазгрузку(СтруктураДоступов)
	Возврат
		СтруктураДоступов.ДоступенДиспетчер 
		И ЕстьПравоНаРедактированиеНаправленияНаРазгрузку;
КонецФункции
#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюПрибыл

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюПрибыл(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.Прибыл); 
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ПрибылАвторизован); 
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.НаВесахБрутто); 
	
	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияПрибыл()
		
	Элементы.СписокРегистрацииПечатьЭтикетки.Доступность = Ложь;
	
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = 
	                        СтруктураДоступов.ДоступенРегистрация 
		                    Или СтруктураДоступов.ДоступенВзвешиваниеНаВъезде 
		                    Или СтруктураДоступов.ДоступенДиспетчер;
	
	Элементы.РедактироватьРегистрацию.Доступность = СтруктураДоступов.ДоступенРегистрация 
		                                            И ЕстьПравоНаРедактированиеРегистрации;
	
	Элементы.РедактироватьВесНаВъезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВъезде 
	                                                И ЕстьПравоНаРедактированиеВзвешивания;
	Элементы.РедактироватьНомерПробы.Доступность = ДоступностьКнопкиНомерПробы(СтруктураДоступов); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюГотовКОтборуПроб

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюГотовКОтборуПроб(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ГотовКОтборуПроб); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияГотовКОтборуПроб()

	Элементы.РедактироватьВесНаВъезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВъезде	
		                                            И ЕстьПравоНаРедактированиеВзвешивания;
	
	Элементы.РедактироватьНомерПробы.Доступность = СтруктураДоступов.ДоступенОтборПроб 
	                                               И ЕстьПравоНаРедактированиеНомераПробы;
	
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВъезде 
		                                                             Или СтруктураДоступов.ДоступенОтборПроб 
		                                                             Или СтруктураДоступов.ДоступенДиспетчер;

	Элементы.РедактироватьРегистрацию.Доступность = СтруктураДоступов.ДоступенРегистрация 
		                                            И ЕстьПравоНаРедактированиеРегистрации;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюВзвешенБрутто

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюВзвешенБрутто(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ВзвешенБрутто); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияВзвешенБрутто()

	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВъезде
		                                                             Или СтруктураДоступов.ДоступенОтборПроб 
		                                                             Или СтруктураДоступов.ДоступенДиспетчер;
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		                                                             
	Если ТекущиеДанные.ТипРегистрации = ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Приемка") Тогда
		
			Элементы.РедактироватьВесНаВъезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВъезде
			                                            И ЕстьПравоНаРедактированиеВзвешивания;
			                                            
			Элементы.РедактироватьРегистрацию.Доступность = СтруктураДоступов.ДоступенРегистрация
			                                            И ЕстьПравоНаРедактированиеРегистрации;
	Иначе
		Элементы.РедактированиеПриемка.Доступность = СтруктураДоступов.ДоступенОформлениеПриемки;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюВзвешенТара

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюВзвешенТара(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ВзвешенТара);
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.НеПринятоПоПревышениюМаксимальногоВеса);

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияВзвешенТара()

	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
			                                                             
	Если ТекущиеДанные.ТипРегистрации = ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Приемка") Тогда
		
		Элементы.РедактироватьВесНаВыезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВыезде 
		                                                И ЕстьПравоНаРедактированиеВзвешивания;
		                                                
		Элементы.РедактированиеПриемка.Доступность = СтруктураДоступов.ДоступенОформлениеПриемки;
		Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенДиспетчер;
	
	
	ИначеЕсли ТекущиеДанные.ТипРегистрации = ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Отгрузка") Тогда
		
		Элементы.РедактироватьВесНаВъезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВъезде	
		                                                И ЕстьПравоНаРедактированиеВзвешивания;
		                                                
		Элементы.КомандаПодтверждениеРазгрузки.Доступность = СтруктураДоступов.ДоступенДиспетчер 
		                                                И ЕстьПравоНаРедактированиеНаправленияНаРазгрузку;
		                                                
	Иначе
			                                                
		Элементы.РедактироватьВесНаВыезде.Доступность 					= Ложь;
		Элементы.РедактированиеПриемка.Доступность 						= Ложь;
		Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность 	= Ложь;
		Элементы.РедактироватьВесНаВъезде.Доступность 					= Ложь;
		Элементы.КомандаПодтверждениеРазгрузки.Доступность 				= Ложь; 	                                                
		                                                
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюНеПринято

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюНеПринято(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.НеПринятоПоПревышениюМаксимальногоВеса);

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияНеПринято()

	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
			                                                             
	Если ТекущиеДанные.ТипРегистрации = ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Приемка") Тогда
		
		Элементы.РедактироватьВесНаВыезде.Доступность = Ложь;                                                
		Элементы.РедактированиеПриемка.Доступность = СтруктураДоступов.ДоступенОформлениеПриемки;
		Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенДиспетчер;
		                                                
	Иначе
			                                                
		Элементы.РедактироватьВесНаВыезде.Доступность 					= Ложь;
		Элементы.РедактированиеПриемка.Доступность 						= Ложь;
		Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность 	= Ложь;
		Элементы.РедактироватьВесНаВъезде.Доступность 					= Ложь;
		Элементы.КомандаПодтверждениеРазгрузки.Доступность 				= Ложь; 	                                                
		                                                
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюВзятыПробы

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюВзятыПробы(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ВзятыПробы); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияВзятыПробы()
	
	Элементы.РедактироватьНомерПробы.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
	                                               И ЕстьПравоНаРедактированиеНомераПробы;
	                                               
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенОтборПроб 
	                                                                 Или СтруктураДоступов.ДоступенЛабАнализ 
	                                                                 Или СтруктураДоступов.ДоступенДиспетчер;
	                                                                 
	Элементы.КомандаПробыПриняты.Доступность = СтруктураДоступов.ДоступенПринятиеПроб;
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущийВидПеревозки = ТекущиеДанные.ВидПеревозки;
	Если ТекущийВидПеревозки = ПредопределенноеЗначение("Перечисление.гкс_ТипыТранспортныхСредствДоставки.Автомобиль") Тогда
		Элементы.РедактироватьАнализ.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
		                                           И ЕстьПравоНаРедактированиеЛабАнализа;
	КонецЕсли;	    
											   
	Элементы.РедактироватьВесНаВъезде.Доступность = ДоступностьКнопкиВесНаВъезде(СтруктураДоступов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюПринятыПробы

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюПринятыПробы(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ПринятыПробы); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияПринятыПробы()

	Элементы.РедактироватьАнализ.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
	                                           И ЕстьПравоНаРедактированиеЛабАнализа;
	                                           
    Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
                                                                     ИЛИ СтруктураДоступов.ДоступенДиспетчер;
	Элементы.РедактироватьВесНаВъезде.Доступность = ДоступностьКнопкиВесНаВъезде(СтруктураДоступов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюПринятыПробы

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюРезультатыПробПолучены(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.РезультатыПробПолучены); 

	Возврат Не СостоянияПроверки.Найти(Состояние) = Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияРезультатыПробПолучены()

	Элементы.РедактироватьАнализ.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
	                                            И ЕстьПравоНаРедактированиеЛабАнализа;
	                                            
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
	                                                                 Или СтруктураДоступов.ДоступенДиспетчер;
	                                                                 
	Элементы.РедактироватьНаправлениеНаРазгрузку.Доступность = СтруктураДоступов.ДоступенДиспетчер 
	                                                           И ЕстьПравоНаРедактированиеНаправленияНаРазгрузку;
															   
	Элементы.РедактироватьВесНаВъезде.Доступность = ДоступностьКнопкиВесНаВъезде(СтруктураДоступов);
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюКачествоНеПринято

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюКачествоНеПринято(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.КачествоНеПринято); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияКачествоНеПринято()
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Элементы.РедактироватьАнализ.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
	                                           И ЕстьПравоНаРедактированиеЛабАнализа;
	
	Элементы.РедактироватьНаправлениеНаРазгрузку.Доступность = 
		ДоступностьКнопкиНаправлениеНаРазгрузку(ТекущиеДанные.ДокументРегистрации, СтруктураДоступов);  
	
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенЛабАнализ
		                                                             Или СтруктураДоступов.ДоступенДиспетчер;
	
	Элементы.РедактироватьВесНаВыезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВыезде
		                                            И ЕстьПравоНаРедактированиеВзвешивания;
	Элементы.РедактироватьВесНаВъезде.Доступность = ДоступностьКнопкиВесНаВъезде(СтруктураДоступов);		
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюКачествоПринято

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюКачествоПринято(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.КачествоПринято); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияКачествоПринято()

	Элементы.РедактироватьАнализ.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
	                                           И ЕстьПравоНаРедактированиеЛабАнализа;
											   
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.РедактироватьНаправлениеНаРазгрузку.Доступность = 
		ДоступностьКнопкиНаправлениеНаРазгрузку(ТекущиеДанные.ДокументРегистрации, СтруктураДоступов);  
		
	Элементы.РедактироватьВесНаВъезде.Доступность = ДоступностьКнопкиВесНаВъезде(СтруктураДоступов);
	                                                           
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенЛабАнализ 
	                                                                 Или СтруктураДоступов.ДоступенДиспетчер;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюКачествоПринято

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюПогрузкаРазрешена(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ПогрузкаРазрешена); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияПогрузкаРазрешена()

	Элементы.РедактироватьНаправлениеНаРазгрузку.Доступность = СтруктураДоступов.ДоступенДиспетчер 
	                                                           И ЕстьПравоНаРедактированиеНаправленияНаРазгрузку;
	                                                           
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенДиспетчер;
	Элементы.КомандаПодтверждениеРазгрузки.Доступность = СтруктураДоступов.ДоступенПодтверждениеРазгрузки;
	Элементы.КомандаОтменитьРазрешение.Доступность = СтруктураДоступов.ДоступенПодтверждениеРазгрузки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюВыгрузкаРазрешена

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюВыгрузкаРазрешена(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.ВыгрузкаРазрешена); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияВыгрузкаРазрешена()

	Элементы.РедактироватьНаправлениеНаРазгрузку.Доступность = СтруктураДоступов.ДоступенДиспетчер 
	                                                           И ЕстьПравоНаРедактированиеНаправленияНаРазгрузку;
			
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенДиспетчер;
	Элементы.КомандаПодтверждениеРазгрузки.Доступность = СтруктураДоступов.ДоступенПодтверждениеРазгрузки;
	Элементы.КомандаОтменитьРазрешение.Доступность = СтруктураДоступов.ДоступенПодтверждениеРазгрузки;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюПогружен

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюПогружен(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.Погружен); 

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияПогружен()

	Элементы.РедактироватьВесНаВыезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВыезде 
	                                                И ЕстьПравоНаРедактированиеВзвешивания;
	                                                
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенДиспетчер;

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеДоступностиКнопокПоСостояниюВыгружен

&НаСервереБезКонтекста
Функция СоотвествуютСостояниюВыгружен(Состояние)
	
	СостоянияПроверки = Новый Массив;
	СостоянияПроверки.Добавить(Перечисления.гкс_СостоянияРегистрации.Выгружен);

	Возврат СостоянияПроверки.Найти(Состояние) <> Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКомандУправленияДляСостоянияВыгружен()

	Элементы.РедактироватьВесНаВыезде.Доступность = СтруктураДоступов.ДоступенВзвешиваниеНаВыезде 
	                                                И ЕстьПравоНаРедактированиеВзвешивания;
	                                                
	Элементы.СписокРегистрацииОтклонитьРегистрациюАвто.Доступность = СтруктураДоступов.ДоступенДиспетчер;
		
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОбновитьКнопки() 
	
	УстановитьДоступностьКнопокУправленияРегистрациейТранспортаПоУмолчанию();
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если Не (ТекущиеДанные <> Неопределено И ТекущиеДанные.Свойство("Состояние")) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьДоступностьКомандУправленияДопусками();
	
	ТекущееСостояние = ТекущиеДанные.Состояние;
	
	Если СоотвествуютСостояниюПрибыл(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияПрибыл();
		
	ИначеЕсли СоотвествуютСостояниюГотовКОтборуПроб(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияГотовКОтборуПроб();
		
	ИначеЕсли СоотвествуютСостояниюВзвешенБрутто(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияВзвешенБрутто();
		
	ИначеЕсли СоотвествуютСостояниюВзятыПробы(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияВзятыПробы();
		
	ИначеЕсли СоотвествуютСостояниюПринятыПробы(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияПринятыПробы();
		
	ИначеЕсли СоотвествуютСостояниюРезультатыПробПолучены(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияРезультатыПробПолучены();
		
	ИначеЕсли СоотвествуютСостояниюКачествоНеПринято(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияКачествоНеПринято();
		
	ИначеЕсли СоотвествуютСостояниюКачествоПринято(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияКачествоПринято();
		
	ИначеЕсли СоотвествуютСостояниюПогрузкаРазрешена(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияПогрузкаРазрешена();
		
	ИначеЕсли СоотвествуютСостояниюВыгрузкаРазрешена(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияВыгрузкаРазрешена();
		
	ИначеЕсли СоотвествуютСостояниюПогружен(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияПогружен();
		
	ИначеЕсли СоотвествуютСостояниюВыгружен(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияВыгружен();
		
	ИначеЕсли СоотвествуютСостояниюНеПринято(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияНеПринято();	
		
	ИначеЕсли СоотвествуютСостояниюВзвешенТара(ТекущееСостояние) Тогда
		ОбновитьДоступностьКомандУправленияДляСостоянияВзвешенТара();
			
	КонецЕсли;
	
	Элементы.СписокРегистрацииПропуститьКонтрольКачества.Доступность 
		= ЕстьПравоНаРедактированиеПриемкаТранспортаБезВходногоКонтроля;
		
	//ОбновитьКнопкиГрупповыхОпреаций();
	
КонецПроцедуры

// !закомментировано до проерки быстродействия работы с формой
//
//&НаКлиенте
//Процедура ОбновитьКнопкиГрупповыхОпреаций()
//	
//	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();
//	Если ВыбранныеРегистрации.Количество() > 0  Тогда
//		// Проверить состояние "Готов к отбору проб"
//		ВалидныеСостояния = Новый Массив;
//		ВалидныеСостояния.Добавить(ПредопределенноеЗначение("Перечисление.гкс_СостоянияРегистрации.ГотовКОтборуПроб"));
//		КонтрольВыбранныхПоСостоянию = КонтрольВыбранныхПоСостоянию(ВыбранныеРегистрации, ВалидныеСостояния);
//		
//		Элементы.СписокРегистрацииПропуститьКонтрольКачества.Доступность = КонтрольВыбранныхПоСостоянию;
//		Элементы.СписокРегистрацииГрупповаяОбработкаРегистраций.Доступность = Истина;
//	Иначе
//		Элементы.СписокРегистрацииГрупповаяОбработкаРегистраций.Доступность = Ложь;
//		Элементы.СписокРегистрацииПропуститьКонтрольКачества.Доступность = Ложь;	
//	КонецЕсли;
//
//КонецПроцедуры
		
#КонецОбласти

#Область ПодключаемыеКоманды 
// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	Если Элементы.СписокРегистрации.ТекущиеДанные <> Неопределено Тогда
		
		ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, 
			Элементы.СписокРегистрации.ТекущиеДанные.ДокументРегистрации);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.СписокРегистрации);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
#КонецОбласти

#Область Заметки

&НаСервере
Процедура ОбновитьНаправленияРазгрузки()
	
	Пока Элементы.ГруппаНаправленияРазгрузкиЭлементы.ПодчиненныеЭлементы.Количество() > 0 Цикл
		Элементы.Удалить(Элементы.ГруппаНаправленияРазгрузкиЭлементы.ПодчиненныеЭлементы.Получить(0));
	КонецЦикла;
	
	НаправленияРазгрузки = НаправленияРазгрузкиПоВидуПеревозки(ОтборТочкаМаршрута, ОтборВидПеревозки);
	Для Каждого КлючЗначение Из НаправленияРазгрузки Цикл
		
		СформироватьЭлементИнформацияНаправленияРазгрузки(
			ПредставлениеНаправленияРазгрузкиПоНоменклатуре(НаправленияРазгрузки, КлючЗначение.Ключ));
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЭлементИнформацияНаправленияРазгрузки(Знач ТекстЭлемента)
	
	Ключ = "Ключ" + СтрЗаменить(Строка(Новый УникальныйИдентификатор),"-", "");
			
	НовыйТекст = Элементы.Добавить(Ключ + "Надпись", Тип("ДекорацияФормы"), Элементы.ГруппаНаправленияРазгрузкиЭлементы);
	НовыйТекст.Вид       = ВидДекорацииФормы.Надпись;
	НовыйТекст.Заголовок = ТекстЭлемента;
	//НовыйТекст.АвтоМаксимальнаяШирина = Ложь;
	НовыйТекст.Рамка		= Новый Рамка(ТипРамкиЭлементаУправления.Одинарная, 1);
	НовыйТекст.Ширина		= 28;
	НовыйТекст.Высота		= 4;
	НовыйТекст.ГоризонтальноеПоложение	= ГоризонтальноеПоложениеЭлемента.Центр;
	НовыйТекст.ВертикальноеПоложение	= ВертикальноеПоложениеЭлемента.Центр;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеНаправленияРазгрузкиПоНоменклатуре(НаправленияРазгрузки, Номенклатура)	
	МассивСтрок	= Новый Массив;
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(Номенклатура + Символы.ПС, Новый Шрифт(, 8, Истина)));
	
	ДанныеМассива = НаправленияРазгрузки.Получить(Номенклатура); 
	МассивПодстрок = Новый Массив;
	Для Каждого ЭлементМассива Из ДанныеМассива Цикл	
		МассивПодстрок.Добавить(СтрШаблон("%1 - %2", ЭлементМассива.Силос, ЭлементМассива.СлужебнаяНоменклатура));	
	КонецЦикла;
	
	МассивСтрок.Добавить(СтрСоединить(МассивПодстрок, Символы.ПС));
	
	Возврат	Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецФункции	

&НаСервереБезКонтекста
Функция НаправленияРазгрузкиПоВидуПеревозки(ТочкаМаршрута, ВидПеревозки)
	
	НаправленияРазгрузки = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПРЕДСТАВЛЕНИЕ(НастройкиНазначенияРазгрузки.Номенклатура) КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(НастройкиНазначенияРазгрузки.СлужебнаяНоменклатура) КАК СлужебнаяНоменклатура,
	|	ПРЕДСТАВЛЕНИЕ(НастройкиНазначенияРазгрузки.Силос) КАК Силос
	|ИЗ
	|	РегистрСведений.гкс_НастройкиНазначенияРазгрузки.СрезПоследних(
	|			,
	|			ТочкаМаршрута = &ТочкаМаршрута
	|				И ВидПеревозки = &ВидПеревозки) КАК НастройкиНазначенияРазгрузки
	|ГДЕ
	|	НастройкиНазначенияРазгрузки.Действует
	|ИТОГИ ПО
	|	Номенклатура";
	
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка	= РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока Выборка.Следующий() Цикл
		
		МассивСтрок = Новый Массив;
		
		ВыборкаДетальная = Выборка.Выбрать();
		Пока ВыборкаДетальная.Следующий() Цикл		
			ДанныеОтображения = Новый Структура("Силос, СлужебнаяНоменклатура");
			ЗаполнитьЗначенияСвойств(ДанныеОтображения, ВыборкаДетальная); 
			МассивСтрок.Добавить(ДанныеОтображения);			
		КонецЦикла;
		
		НаправленияРазгрузки.Вставить(Выборка.Номенклатура, МассивСтрок);	
	КонецЦикла;
	
	Возврат НаправленияРазгрузки;
	
КонецФункции

#КонецОбласти 

#Область Прочее

&НаКлиенте
Процедура ЗаписатьДанныеДляСАРАНаКлиенте(ПараметрыЗаписиДанныхСАРА)
	
	Перем СсылкаНаПробу;
	
	Если Не ПараметрыЗаписиДанныхСАРА.Свойство("СсылкаНаСформированнуюПробу", СсылкаНаПробу) Тогда
		Возврат;
	КонецЕсли;
	
	гкс_ИнтеграцияСАРАКлиент.СформироватьФайлДляСАРА(СсылкаНаПробу);

КонецПроцедуры

&НаСервере
Функция ПолучитьПричинуПретензионности(ПараметрыСтроки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СтатусыДопускаВагоновСрезПоследних.Комментарий КАК СТРОКА(500)) КАК Комментарий
	|ИЗ
	|	РегистрСведений.гкс_СтатусыДопускаВагоновКВскрытию.СрезПоследних(
	|			&Период,
	|			ДокументРегистрации = &ДокументРегистрации
	|				И Вагон = &Вагон
	|				И ДопускКВскрытию = &ДопускКВскрытию) КАК СтатусыДопускаВагоновСрезПоследних";
	
	Запрос.УстановитьПараметр("Вагон", ПараметрыСтроки.Вагон);
	Запрос.УстановитьПараметр("ДокументРегистрации", ПараметрыСтроки.ДокументРегистрации);
	Запрос.УстановитьПараметр("ДопускКВскрытию", Перечисления.гкс_ДопускиКВскрытиюВагонов.Претензионный);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();

	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ТекстПричины = Выборка.Комментарий;
	Иначе		
		ТекстПричины = НСтр("ru = 'Комментарий пустой'");
	КонецЕсли;
	
	Возврат ТекстПричины;
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура ПодготовитьПараметрыОткрытия(ДокументРегистрации, ПараметрыОткрытия)
		
	ЛабАнализРегистрации = гкс_ПриемкаТранспорта.ОпределитьЛабораторныйАнализ(ДокументРегистрации);
	
	Если ЗначениеЗаполнено(ЛабАнализРегистрации) Тогда
		ПараметрыОткрытия.Вставить("Ключ", ЛабАнализРегистрации);
		ПараметрыОткрытия.Вставить("ИзАРМа", Истина);
	КонецЕсли;
	
	ДополнительныеПараметры = ДополнительныеПараметрыДляПробыПриняты(ДокументРегистрации);
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЗначениеВДиалоге(Значение)
	
	Если ЗначениеЗаполнено(Значение) Тогда
		ПоказатьЗначение(Неопределено, Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДополнительныеПараметрыДляПробыПриняты(ДокументРегистрации)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДокументРегистрации", ДокументРегистрации); 
	ДополнительныеПараметры.Вставить("УИД", Строка(Новый УникальныйИдентификатор()));
	ДополнительныеПараметры.Вставить("Состояние", Перечисления.гкс_СостоянияРегистрации.ПринятыПробы);
			
	Возврат ДополнительныеПараметры;
	
КонецФункции  

&НаСервере
Процедура УстановитьТекстЗапросаСпискаРегистрации()
	
	Если ЗначениеЗаполнено(ОтборВидПеревозки) Тогда 
		
		Если ЭтоЖДПеревозка Тогда
			ТекстЗапроса = РегистрыСведений.
				гкс_АРМПриемкиНаПЛКСписокРегистраций.ПолучитьТекстЗапросаДинамическогоСпискаДопуски();
		Иначе
			ТекстЗапроса = "";
		КонецЕсли;
		
		СписокРегистрации.ТекстЗапроса = ТекстЗапроса + РегистрыСведений.
			гкс_АРМПриемкиНаПЛКСписокРегистраций.ПолучитьТекстЗапросаДинамическогоСписка(ЭтоЖДПеревозка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УстановитьЧасовойПоясПЛК()
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтборТочкаМаршрута, "гкс_ЧасовойПоясТочкиМаршрута");
КонецФункции

&НаСервере
Функция ПолучитьСтруктуруДоступовНаСервере(НужноЗаполнять)
	
	Если НужноЗаполнять Тогда
		СтруктураДоступов = гкс_ПриемкаНаПЛКСервер.ПолучитьРолиНаПЛК(
			ОтборТочкаМаршрута, ОтборВидПеревозки, Пользователь, ЭтоПолноправныйПользователь);
	Иначе
		СтруктураДоступов = гкс_ПриемкаНаПЛКСервер.ИнициализироватьСтруктуруДоступовПриемкаПЛК();
	КонецЕсли;

	Возврат СтруктураДоступов;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОбновитьДанныеНаФорме()
	
	ОбновитьПараметрыСпискаРегистраций();
	Элементы.СписокРегистрации.Обновить();
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
		
	Если ТекущиеДанные.Свойство("ТипРегистрации")
		И Не ОтборТипРегистрации = ТекущиеДанные.ТипРегистрации Тогда
			
		УстановитьЗаголовкиЭлементовПоТипуРегистрации(ТекущиеДанные.ТипРегистрации);
	КонецЕсли;
	
	ОбновитьКнопки();
	
	Подключаемый_ОбновитьДетальныеЗаписиРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыСпискаРегистраций()
 
	СписокРегистрации.Параметры.УстановитьЗначениеПараметра("ЭтоУстановкаПометокДляВсехПозиций", ЭтоУстановкаПометокДляВсехПозиций);
	СписокРегистрации.Параметры.УстановитьЗначениеПараметра("РегистрацииВыбранные", РегистрацииВыбранные.ВыгрузитьЗначения());
	СписокРегистрации.Параметры.УстановитьЗначениеПараметра("РегистрацииИсключенные", РегистрацииИсключенные.ВыгрузитьЗначения());

КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовкиЭлементовПоТипуРегистрации(ТипРегистрацииТранспорта)
	
	ЭтоОтгрузка = (ТипРегистрацииТранспорта = ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Отгрузка"));
	 
	Элементы.ОтправительПолучатель.Заголовок = ?(ЭтоОтгрузка, НСтр("ru='Грузополучатель'"), НСтр("ru='Грузоотправитель'"));
	Элементы.Организация.Заголовок = ?(ЭтоОтгрузка, НСтр("ru='Грузоотправитель'"), НСтр("ru='Грузополучатель'"));	
	Элементы.Контрагент.Заголовок = ?(ЭтоОтгрузка, НСтр("ru='Покупатель'"), НСтр("ru='Поставщик'"));
	Элементы.РедактироватьНаправлениеНаРазгрузку.Заголовок = ?(ЭтоОтгрузка, НСтр("ru='На погрузку'"), НСтр("ru='На разгрузку'"));
	Элементы.КомандаПодтверждениеРазгрузки.Заголовок = ?(ЭтоОтгрузка, НСтр("ru='Погрузка'"), НСтр("ru='Разгрузка'"));
	Элементы.РедактированиеПриемка.Заголовок = ?(ЭтоОтгрузка, НСтр("ru='Отгрузка'"), НСтр("ru='Приемка'"));
		
	Элементы.ГруппаАнализы.Видимость = ?(ЭтоОтгрузка, Ложь, Истина);
	
КонецПроцедуры

#Область РаботаСОкномДетальныеЗаписиРегистраций

&НаКлиенте
Процедура Подключаемый_ОбновитьДетальныеЗаписиРегистрации()
	
	Если Не РежимПросмотраДетальныхЗаписей Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ Не ЗначениеЗаполнено(ТекущиеДанные.ДокументРегистрации) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДетальныеЗаписиРегистрацииНаСервере(ТекущиеДанные);
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДетальныеЗаписиРегистрацииНаСервере(ТекущиеДанные)
	
	ИнициализироватьЗначенияДетальныхЗаписей();
	
	ДокументРегистрации 		= ТекущиеДанные.ДокументРегистрации;
	ДокументРегистрацииАнализов = ТекущиеДанные.ДокументРегистрацииАнализов;
	НомерПробы 				= ТекущиеДанные.НомерПробы;
	СкладПлан 				= ТекущиеДанные.СкладПлан;
	КомментарийРегистрации 	= ТекущиеДанные.Комментарий;
	
	Если ОтборТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка Тогда		
		ВесНаВъезде	= ТекущиеДанные.ВесБрутто;
		ВесНаВыезде	= ТекущиеДанные.ВесТары;		
	Иначе	
		ВесНаВыезде = ТекущиеДанные.ВесБрутто;
		ВесНаВъезде	= ТекущиеДанные.ВесТары;
	КонецЕсли;
			
	Обработка = РеквизитФормыВЗначение("Объект");
	ДанныеДляДетальныхЗаписей = Обработка.ДанныеДляДетальныхЗаписей(ДокументРегистрации, ДокументРегистрацииАнализов);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеДляДетальныхЗаписей.ДанныеРегистрации);
	
	КачественныеПоказатели.Очистить();
	КачественныеПоказатели.Загрузить(ДанныеДляДетальныхЗаписей.КачественныеПоказатели);
	
	Если ЗначениеЗаполнено(ДокументРегистрацииАнализов) Тогда
		ДатаРегистрацииАнализов	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРегистрацииАнализов, "Дата");
	КонецЕсли;
	
	ВесБруттоПоДокументам = ДанныеДляДетальныхЗаписей.ДанныеРегистрации.ВесБруттоПоДокументам;
	ВесТарыПоДокументам = ДанныеДляДетальныхЗаписей.ДанныеРегистрации.ВесТарыПоДокументам;
	
	ТаблицаСписокПоказанийВесаЗаполнить(); 
	
КонецПроцедуры

&НаСервере
Процедура ТаблицаСписокПоказанийВесаЗаполнить()
	
	Если ОтборТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка Тогда
		ВесНетто	= ВесНаВъезде - ВесНаВыезде;
		ВесРазница	= ВесНетто - ВесПоДокументам;
		ВесРазницаБрутто = ВесБруттоПоДокументам - ВесНаВъезде;
		ВесРазницаТара = ВесТарыПоДокументам - ВесНаВыезде; 		 
	Иначе
		ВесНетто	= ВесНаВыезде - ВесНаВъезде;
		ВесРазница	= 0;
		ВесРазницаБрутто = 0;
		ВесРазницаТара = 0;		
	КонецЕсли;
	
	ТаблицаСписокПоказанийВеса.Очистить();
	
	НоваяСтрока = ТаблицаСписокПоказанийВеса.Добавить();
	НоваяСтрока.ВидВеса       = "Брутто";
	НоваяСтрока.ПоДокументам  = ВесБруттоПоДокументам;
	НоваяСтрока.ПоВзвешиванию = ?(ОтборТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка, 
								  ВесНаВъезде, ВесНаВыезде);
	НоваяСтрока.РазницаВеса   = ВесРазницаБрутто;
	
	НоваяСтрока = ТаблицаСписокПоказанийВеса.Добавить();
	НоваяСтрока.ВидВеса       = "Тара";
	НоваяСтрока.ПоДокументам  = ВесТарыПоДокументам;
	НоваяСтрока.ПоВзвешиванию = ?(ОтборТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка,
								  ВесНаВыезде, ВесНаВъезде);
	НоваяСтрока.РазницаВеса   = ВесРазницаТара;
	
	НоваяСтрока = ТаблицаСписокПоказанийВеса.Добавить();
	НоваяСтрока.ВидВеса       = "Нетто";
	НоваяСтрока.ПоДокументам  = ВесПоДокументам;
	НоваяСтрока.ПоВзвешиванию = ВесНетто;
	НоваяСтрока.РазницаВеса   = ВесРазница; 

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьЗначенияДетальныхЗаписей()
	
	ВесНетто 		= 0;
	ВесРазница 		= 0;
	ВесНаВъезде		= 0;
	ВесНаВыезде 	= 0;
	ВесПоДокументам	= 0;
	
	НомерТТН 				= Неопределено;
	Водитель 				= Неопределено;
	СкладПлан 				= Неопределено;
	НомерПробы 				= Неопределено;
	Контрагент 				= Неопределено;
	Организация 			= Неопределено;
	Собственник				= Неопределено;
	Номенклатура 			= Неопределено;
	ДатаРегистрации			= Неопределено;
	ТранспортноеСредство 	= Неопределено;
	ОтправительПолучатель 	= Неопределено;
	КомментарийРегистрации 	= Неопределено;
	ДатаРегистрацииАнализов	= Неопределено;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьОтборыСпискаРегистрации()
	
	Если ЗначениеЗаполнено(ОтборТочкаМаршрута) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"ТочкаМаршрута", ОтборТочкаМаршрута, ВидСравненияКомпоновкиДанных.Равно);
	Иначе	
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "ТочкаМаршрута");	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборВидПеревозки) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"ВидПеревозки", ОтборВидПеревозки, ВидСравненияКомпоновкиДанных.Равно);
	Иначе	
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "ВидПеревозки");	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборТипРегистрации) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"ТипРегистрации", ОтборТипРегистрации, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "ТипРегистрации");	
	КонецЕсли;
	
	// дополнительные фильтры
	Если ЗначениеЗаполнено(ОтборНоменклатура) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"Номенклатура", ОтборНоменклатура, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "Номенклатура");	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборСостояние) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"Состояние", ОтборСостояние, ВидСравненияКомпоновкиДанных.Равно);
	Иначе			
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "Состояние");	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборКонтрагент) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"Контрагент", ОтборКонтрагент, ВидСравненияКомпоновкиДанных.Равно);
	Иначе			
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "Контрагент");	
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ОтборТранспорт) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"ТранспортноеСредство", ОтборТранспорт, ВидСравненияКомпоновкиДанных.Равно);
	Иначе			
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "ТранспортноеСредство");	
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ОтборНомерПробы) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"НомерПробы", ОтборНомерПробы, ВидСравненияКомпоновкиДанных.Равно);
	Иначе			
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "НомерПробы");	
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(ОтборПунктПогрузки) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"ПунктПогрузки", ОтборПунктПогрузки, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "ПунктПогрузки");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборСтанцияЖД) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"СтанцияЖД", ОтборСтанцияЖД, ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "СтанцияЖД");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборНомерДокументаПоставщика) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРегистрации,
			"НомерДокументаПоставщика", ОтборНомерДокументаПоставщика, ВидСравненияКомпоновкиДанных.Содержит);
	Иначе
		ОбщегоНазначенияКлиентСервер.
			УдалитьЭлементыГруппыОтбораДинамическогоСписка(СписокРегистрации, "НомерДокументаПоставщика");
	КонецЕсли;
	
КонецПроцедуры                                  

&НаСервере 
Процедура УстановитьНастройкиСортировки()
	
	Если ОтборВидПеревозки = Перечисления.гкс_ТипыТранспортныхСредствДоставки.Автомобиль Тогда
		ЭлементыПользовательскихНастроек = СписокРегистрации.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
 
    	Для Каждого НастройкаСписка Из ЭлементыПользовательскихНастроек Цикл
			
			Если ТипЗнч(НастройкаСписка) = ТипЗнч(СписокРегистрации.КомпоновщикНастроек.Настройки.Порядок)
            	И НастройкаСписка.Элементы.Количество() > 0 Тогда
            	Если НастройкаСписка.Элементы[0].Поле = Новый ПолеКомпоновкиДанных("Протеин") Тогда
            		НастройкаСписка.Элементы.Очистить();
            		Прервать;
            	КонецЕсли;
			КонецЕсли;
			
    	КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСпецификацию(ДокументРегистрации)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРегистрации, "Спецификация");
	
КонецФункции

&НаКлиенте
Процедура РедактироватьВесНаКлиенте(ТипВзвешивания, ДокументРегистрации,
	ДокументРегистрацииВеса, КачествоНеПринято = Ложь)
	
	ПараметрыФормы = Новый Структура;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ДокументРегистрации", ДокументРегистрации);
	ЗначенияЗаполнения.Вставить("ТипВзвешивания", ТипВзвешивания);        
	ЗначенияЗаполнения.Вставить("ТочкаМаршрута", ОтборТочкаМаршрута);
	
	Если ЗначениеЗаполнено(ДокументРегистрацииВеса) Тогда
		ПараметрыФормы = Новый Структура("Ключ", ДокументРегистрацииВеса);
	Иначе
		ЗначенияЗаполнения.Вставить("КачествоНеПринято", КачествоНеПринято);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ИзАРМа", Истина);
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
	
	Если ЭтоЖДПеревозка И ТипВзвешивания = ПредопределенноеЗначение("Перечисление.гкс_ТипыВзвешивания.Выезд") Тогда
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("РедактироватьВесНаВыездеЗавершение", ЭтотОбъект);
	КонецЕсли;
			
	ОткрытьФорму("Документ.гкс_Взвешивание.ФормаОбъекта", ПараметрыФормы, 
		ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено, 
		ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
						 	
КонецПроцедуры

#Область ФормированиеКомплектаДокументовПриемки

&НаКлиенте
Функция ДанныеРегистрацииДляФормированияКомплектаДокументов(ВходящиеДанныеРегистрации)
	
	ДанныеРегистрации = Новый Структура;

	ДанныеРегистрации.Вставить("ЛабораторныйАнализ", ВходящиеДанныеРегистрации.ДокументРегистрацииАнализов);
	
	ДанныеРегистрации.Вставить("ВесНеттоФакт",	ВходящиеДанныеРегистрации.ВесНетто);
	ДанныеРегистрации.Вставить("ВесНетто", 		ВходящиеДанныеРегистрации.ВесНеттоДокумент);
	
	ДанныеРегистрации.Вставить("ВесБруттоФакт", ВходящиеДанныеРегистрации.ВесБрутто);
	ДанныеРегистрации.Вставить("ВесБрутто", 	ВходящиеДанныеРегистрации.ВесБруттоДокумент);
	
	ДанныеРегистрации.Вставить("ВесТарыФакт", 	ВходящиеДанныеРегистрации.ВесТары);
	ДанныеРегистрации.Вставить("ВесТары", 		ВходящиеДанныеРегистрации.ВесТарыДокумент);
	
	ДанныеРегистрации.Вставить("ПунктРазгрузки", ВходящиеДанныеРегистрации.СкладПлан);

	Возврат ДанныеРегистрации;
	
КонецФункции

&НаСервере
Процедура СформироватьКомплектДокументовПриемкиИЗарегистрироватьУбытиеТранспорта(ДокументРегистрации, ДанныеРегистрации)
	
	РеквизитФормыВЗначение("Объект")
	      .СформироватьКомплектДокументовПриемкиИЗарегистрироватьУбытиеТранспорта(ДокументРегистрации, ДанныеРегистрации);

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ОтклонитьРегистрациюНаСервере(Комментарий, ДополнительныеПараметры)
	
	МодульОбъекта = РеквизитФормыВЗначение("Объект");
	
	Для Каждого ДанныеРегистрации Из ДополнительныеПараметры.МассивРегистрации Цикл
		
		ДополнитьКомментарийОтклоненияРегистрации(Комментарий, ДанныеРегистрации.ТранспортныйДокумент);
		МодульОбъекта.ВыполнитьУдалениеСвязиРегистрацииИТранспортногоДокумента(ДанныеРегистрации.ДокументРегистрации);
		
		гкс_ПриемкаТранспорта.ЗарегистрироватьНовоеСостояние(ДанныеРегистрации.ДокументРегистрации, 
				Перечисления.гкс_СостоянияРегистрации.НеПринято, Комментарий);	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьКомментарийОтклоненияРегистрации(Комментарий, ТранспортныйДокумент)
	
	ТекстПользователя = Комментарий;
	ТекДата = ТекущаяДатаСеанса();
	
	Если ЗначениеЗаполнено(ТранспортныйДокумент) Тогда
		ШаблонКомментария = НСтр("ru='%1 : %2. Дата отклонения: %3'");
		Комментарий = СтрШаблон(ШаблонКомментария, ТранспортныйДокумент, 
			ТекстПользователя, ТекДата);
	Иначе
		ШаблонКомментария = НСтр("ru='%1. Дата отклонения: %2'");
		Комментарий = СтрШаблон(ШаблонКомментария, ТекстПользователя, ТекДата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Оповещение после отклонения регистрации
//
// Параметры:
//  Комментарий - Строка - комментарий с причинами отклонения.
//  ДополнительныеПараметры - Структура - Параметры для формирования записи в регистре при подтверждении отклонения.
//
Процедура ОтклонитьРегистрациюПослеВводаКомментария(Комментарий, ДополнительныеПараметры) Экспорт
		
	Если ЗначениеЗаполнено(Комментарий) Тогда	
		ОтклонитьРегистрациюНаСервере(Комментарий, ДополнительныеПараметры);
		ПодключениеОбработчикаОжиданияОбновления(Истина, Истина);
		Оповестить("ИзменениеОтклонитьРегистрацию");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНомерПробы(ДокументРегистрации)
	
	РезультатВозврата = Неопределено;
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
               |	НомерПробы.Код КАК Код
               |ИЗ
               |	Справочник.гкс_НомерПробы.СписокРегистраций КАК НомерПробыТаблица
               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гкс_НомерПробы КАК НомерПробы
               |		ПО НомерПробыТаблица.Ссылка = НомерПробы.Ссылка
               |ГДЕ
               |	НомерПробыТаблица.ДокументРегистрации = &ДокументРегистрации
               |
               |УПОРЯДОЧИТЬ ПО
               |	Код УБЫВ";
	
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		РезультатВозврата = Выборка.Код;
	КонецЕсли;
	
	Возврат РезультатВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНаправлениеНаРазгрузку(Регистрация)
	
	Возврат гкс_ПриемкаТранспорта.НаправлениеНаРазгрузкуПоРегистрации(Регистрация);
	
КонецФункции

&НаСервереБезКонтекста
Функция СостоянияПриемкиДляРазгрузки()
	
	Состояния = Новый Массив;
	Состояния.Добавить(Перечисления.гкс_СостоянияРегистрации.ВзвешенТара);
	Состояния.Добавить(Перечисления.гкс_СостоянияРегистрации.КачествоПринято);	
	Возврат Состояния;
			
КонецФункции

&НаСервере
Функция ЕстьПравоНаРедактирование(ИмяОбъекта)
	
	Возврат ПравоДоступа("Добавление", Метаданные.Документы[ИмяОбъекта]);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьКомандуПечатиЭтикетки()

	ТекущиеДанные = Элементы.СписокРегистрации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныеРегистрации = РегистрацииГрупповойОбработки();	
	Если ВыбранныеРегистрации.Количество() = 0 Тогда   
		ВыбранныеРегистрации.Добавить(ТекущиеДанные.ДокументРегистрации);
	КонецЕсли;	
	
	МассивОбъектов = ФормированияПроб(ВыбранныеРегистрации);	
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ДокументРегистрацияНаПЛК", ВыбранныеРегистрации);
	
	АвтоматическаяПечатьЭтикеток = гкс_ОбщегоНазначенияВызовСервера.ЗначениеКонстанты(
		"гкс_АвтоматическаяПечатьЭтикеток");
	
	Если АвтоматическаяПечатьЭтикеток Тогда
		ПараметрыПечати.Вставить("АвтоматическаяПечатьЭтикеток", АвтоматическаяПечатьЭтикеток);
		
		гкс_УправлениеПечатьюКлиент.ПечатьЭтикеткаНаПринтер("Документ.гкс_ФормированиеНомераПробы",
			"ПФ_MXL_Этикетка", МассивОбъектов, ПараметрыПечати);
	Иначе
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.гкс_ФормированиеНомераПробы",
			"ПФ_MXL_Этикетка", МассивОбъектов, ЭтотОбъект, ПараметрыПечати);
	КонецЕсли;
			
КонецПроцедуры

&НаСервереБезКонтекста
Функция ФормированияПроб(ВыбранныеРегистрации)
	
	МенеджерДокумента = Документы.гкс_ФормированиеНомераПробы;
    МассивОбъектов = Новый Массив;
	
	Для Каждого ДокуменРегистрации Из ВыбранныеРегистрации Цикл 
		
		ДокументФормированиеНомераПробы = МенеджерДокумента.ДокументПоРегистрацииТипуПробы(
											ДокуменРегистрации, Перечисления.гкс_ТипыПроб.Единичная);
		
		Если ЗначениеЗаполнено(ДокументФормированиеНомераПробы) Тогда
			МассивОбъектов.Добавить(ДокументФормированиеНомераПробы);
		КонецЕсли;	
	КонецЦикла;
	
	Возврат МассивОбъектов;
	
КонецФункции	
                    
&НаСервере
Процедура ВидПеревозкиПриИзмененииНаСервере()
	
	ИнициализироватьРеквизитыПризнакиТипаПеревозки(); 
	ИнициализироватьРеквизитыПриемкиЖД();
	УстановитьЗаголовкиВКолонкахСпискаРегистрации();
	УстановитьТекстЗапросаСпискаРегистрации();	   
	УстановитьОтборыСпискаРегистрации();
	УстановитьНастройкиСортировки();

	ПриИзмененииОтборовТочкаМаршрутаВидПеревозкиСервер();
	УстановитьВидимостьДоступностьПропуститьКонтрольКачества();
	
КонецПроцедуры    

&НаСервере
Процедура ТипРегистрацииПриИзмененииНаСервере()
	
	ИнициализироватьРеквизитыПриемкиЖД();
	УстановитьВидимостьДоступностьКомандФормы();
	УстановитьОтборыСпискаРегистрации();
	УстановитьВидимостьЭлементовСпискаРегистрации();
	
	УстановитьВидимостьДоступностьПропуститьКонтрольКачества();
	
КонецПроцедуры

&НаСервере
Процедура ОтборТочкаМаршрутаПриИзмененииНаСервере()
	
	УстановитьОтборыСпискаРегистрации();
	УстановитьВидимостьЭлементовСпискаРегистрации();
	УстановитьВидимостьДоступностьИныхЭлемнотовФормы();
	УстановитьВидимостьДоступностьПринятыеТС();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОтборовТочкаМаршрутаВидПеревозкиСервер()
	
	ИнициализироватьСтруктуруДоступов();
	
	ЕстьПравоНаРедактированиеРегистрации = ПравоНаРедактированияРегистрации();
	
	ИнициализироватьРеквизитыВремяОжиданияТС();
	ИнициализироватьАвтоматическоеФормированиеДокументов();     	
	
	УстановитьВидимостьДоступностьКомандФормы();
	УстановитьВидимостьДоступностьИныхЭлемнотовФормы();
	УстановитьВидимостьЭлементовСпискаРегистрации();
	
	ОбновитьНаправленияРазгрузки();
	
КонецПроцедуры                                           

&НаСервере
Процедура УстановитьЗаголовкиВКолонкахСпискаРегистрации()
	
	Элементы.СписокРегистрацииТранспортноеСредство.Заголовок = 
		СписокРегистрацииТранспортноеСредствоЗаголовок(ОтборВидПеревозки);
	
	Элементы.СписокРегистрацииНомерДокументаПоставщика.Заголовок = 
		СписокРегистрацииНомерДокументаПоставщикаЗаголовок(ОтборВидПеревозки);
	
	Элементы.СписокРегистрацииПунктПогрузки.Заголовок = 
		СписокРегистрацииПунктПогрузкиЗаголовок (ОтборВидПеревозки); 
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокРегистрацииТранспортноеСредствоЗаголовок(ВидПеревозки)
	
	ТипыТранспортныхСредств = Перечисления.гкс_ТипыТранспортныхСредствДоставки;
	
	Если ТипыТранспортныхСредств.ЭтоАвтоПеревозка(ВидПеревозки) Тогда
	    Результат = "Авто";	
	ИначеЕсли ТипыТранспортныхСредств.ЭтоЖДПеревозка(ВидПеревозки) Тогда
		Результат = "Вагон";	
	Иначе
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

&НаСервереБезКонтекста
Функция СписокРегистрацииНомерДокументаПоставщикаЗаголовок(ВидПеревозки)
	
	ТипыТранспортныхСредств = Перечисления.гкс_ТипыТранспортныхСредствДоставки;
	
	Если ТипыТранспортныхСредств.ЭтоАвтоПеревозка(ВидПеревозки) Тогда
	    Результат = "Номер ТТН";	
	ИначеЕсли ТипыТранспортныхСредств.ЭтоЖДПеревозка(ВидПеревозки) Тогда
		Результат = "Номер ЖД";	
	Иначе
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокРегистрацииПунктПогрузкиЗаголовок(ВидПеревозки)
	
	ТипыТранспортныхСредств = Перечисления.гкс_ТипыТранспортныхСредствДоставки;
	
	Если ТипыТранспортныхСредств.ЭтоАвтоПеревозка(ВидПеревозки) Тогда
	    Результат = "Пункт погрузки";	
	ИначеЕсли ТипыТранспортныхСредств.ЭтоЖДПеревозка(ВидПеревозки) Тогда
		Результат = "ЖД станция";	
	Иначе
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
    
&НаСервере
Функция ПравоНаРедактированияРегистрации()
	
	Возврат СтруктураДоступов.Свойство("ДоступенРегистрация")
			И СтруктураДоступов.ДоступенРегистрация 
			И ЕстьПравоНаРедактирование("гкс_РегистрацияНаПЛК");	
	
КонецФункции

#Область НастройкаФормы

&НаСервере
Процедура ПодготовитьПараметрыОткрытияФормыНастроек(ПараметрыОткрытия) 
	
	ТекПользователь = Пользователи.ТекущийПользователь();	
	РеквизитыТекПользователь = РеквизитыТекПользователь(ТекПользователь);
	
	КлючОбъекта 	= ИмяФормы;
	КлючНастроек 	= СтрШаблон("%1_%2", РеквизитыТекПользователь.Наименование, 
											РеквизитыТекПользователь.ИдентификаторПользователяИБ);
	
	ДанныеЭлементов = ИнициализироватьЭлементыДляФормыНастроек(КлючОбъекта, КлючНастроек);
	ПараметрыОткрытия.Вставить("ДанныеЭлементов", 	ДанныеЭлементов);
	ПараметрыОткрытия.Вставить("КлючОбъекта", 		КлючОбъекта);
	ПараметрыОткрытия.Вставить("КлючНастроек", 		КлючНастроек);
	
КонецПроцедуры

&НаСервере
Функция ИнициализироватьЭлементыДляФормыНастроек(КлючОбъекта, КлючНастроек) 

	ДанныеЭлементов = РегистрыСведений.гкс_ХранилищеНастроек.ПолучитьСохраненныеНастройки(КлючОбъекта, КлючНастроек);	
	
	Если Не ЗначениеЗаполнено(ДанныеЭлементов) Тогда		
		ДанныеЭлементов = ПолучитьНастройкиПоУмолчанию(Элементы.СписокРегистрации);
	Иначе
		// Сбросить настройки формы на стандартные
		КлючНастройкиФормы = ИмяФормы + "/НастройкиОкна"; 
		ХранилищеСистемныхНастроек.Удалить(КлючНастройкиФормы, "", ИмяПользователя());
    	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат ДанныеЭлементов;
	
КонецФункции

&НаСервере
Функция ПолучитьНастройкиПоУмолчанию(ЭлементСписок, ЭтоВГруппеКолонок = Ложь)
	
	ДанныеЭлементов 			= Новый Соответствие;		
	ДанныеПодчиненныхЭлементов 	= Новый Соответствие;
	СписокЭлементов 			= ЭлементСписок.ПодчиненныеЭлементы;
	
	ПеребратьСписокЭлементов(СписокЭлементов, ДанныеПодчиненныхЭлементов, ДанныеЭлементов, ЭтоВГруппеКолонок);
	
	Если Не ЭтоВГруппеКолонок Тогда
		ДанныеЭлементов.Вставить(ЭлементСписок.Имя, ДанныеПодчиненныхЭлементов);
	КонецЕсли;
	
	Возврат ДанныеЭлементов;
	
КонецФункции

&НаСервере
Процедура ПеребратьСписокЭлементов(СписокЭлементов, ДанныеПодчиненныхЭлементов, ДанныеЭлементов, ЭтоВГруппеКолонок) 
	
	НомерПП = 1;
	
	Для Каждого ЭлСписка Из СписокЭлементов Цикл
		
		НастройкиЭлементов = Новый Структура;
			
		Если ЭлСписка.Имя = "СписокРегистрацииИндексКартинки" Тогда
			Продолжить;
		ИначеЕсли ЭлСписка.Вид = ВидГруппыФормы.ГруппаКолонок Тогда
			Ширина 					= ЭлСписка.Ширина;
			ЭтоФлажок 				= Ложь;
			ЭтоГруппаКолонок 		= Истина;
			МаксимальнаяШирина 		= Неопределено;
			АвтоМаксимальнаяШирина 	= Неопределено;
			ИндексКартинки			= 1;
			
			ЭлементыГруппы 			= ПолучитьНастройкиПоУмолчанию(ЭлСписка, Истина);
			НастройкиЭлементов.Вставить("ЭлементыГруппы", ЭлементыГруппы);
			
		ИначеЕсли ЭлСписка.Вид = ВидПоляФормы.ПолеФлажка Тогда
			Ширина 					= ЭлСписка.ШиринаЭлемента;
			ЭтоФлажок 				= Истина;
			ЭтоГруппаКолонок 		= Ложь;
			МаксимальнаяШирина 		= Неопределено;
			АвтоМаксимальнаяШирина 	= Неопределено;
			ИндексКартинки			= 2;
		Иначе
			Ширина 					= ЭлСписка.Ширина;
			ЭтоФлажок 				= Ложь;
			ЭтоГруппаКолонок 		= Ложь;
			МаксимальнаяШирина 		= ЭлСписка.МаксимальнаяШирина;
			АвтоМаксимальнаяШирина 	= ЭлСписка.АвтоМаксимальнаяШирина;
			ИндексКартинки			= 2;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЭлСписка.Заголовок) Тогда
			ЗаголовокПоля = ЭлСписка.Заголовок;
		Иначе
			ЗаголовокПоля = СтрЗаменить(ЭлСписка.Имя, "СписокРегистрации", "");
		КонецЕсли;
		
		ИндексЭлемента = СписокЭлементов.Индекс(ЭлСписка);
		
		НастройкиЭлементов.Вставить("Видимость", 				ЭлСписка.Видимость);
		НастройкиЭлементов.Вставить("НомерПП", 					НомерПП);
		НастройкиЭлементов.Вставить("НовыйИндекс", 				ИндексЭлемента);
		НастройкиЭлементов.Вставить("СтарыйИндекс", 			ИндексЭлемента);
		НастройкиЭлементов.Вставить("ИзмененПорядок", 			Ложь);
		НастройкиЭлементов.Вставить("Ширина", 					Ширина);
		НастройкиЭлементов.Вставить("МаксимальнаяШирина", 		МаксимальнаяШирина);
		НастройкиЭлементов.Вставить("АвтоМаксимальнаяШирина", 	АвтоМаксимальнаяШирина);
		НастройкиЭлементов.Вставить("ЭтоГруппаКолонок", 		ЭтоГруппаКолонок);
		НастройкиЭлементов.Вставить("ЭтоФлажок", 				ЭтоФлажок);
		НастройкиЭлементов.Вставить("Заголовок", 				ЗаголовокПоля);
		НастройкиЭлементов.Вставить("ИндексКартинки", 			ИндексКартинки);
		
		Если Не ЭтоВГруппеКолонок Тогда
			ДанныеПодчиненныхЭлементов.Вставить(ЭлСписка.Имя, НастройкиЭлементов);
		Иначе
			ДанныеЭлементов.Вставить(ЭлСписка.Имя, НастройкиЭлементов);
		КонецЕсли;
		
		НомерПП = НомерПП + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиФормыИзХранилища() 
	
	ТекПользователь = Пользователи.ТекущийПользователь();
	РеквизитыТекПользователь = РеквизитыТекПользователь(ТекПользователь);
	
	КлючОбъекта 	= ИмяФормы;
	КлючНастроек 	= СтрШаблон("%1_%2", РеквизитыТекПользователь.Наименование, 
											РеквизитыТекПользователь.ИдентификаторПользователяИБ);
	НастройкиПорядок.Очистить();
	
	ДанныеЭлементов = ИнициализироватьЭлементыДляФормыНастроек(КлючОбъекта, КлючНастроек);
	ПрочитатьНастройкиФормы(ДанныеЭлементов);
	ИзменитьПоложенияЭлементаИзНастройки();
	ИзменитьПорядокВСохраненныеНастройкиФормы(ДанныеЭлементов, КлючОбъекта, КлючНастроек);	
		
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиФормы(ДанныеЭлементов) 
	
	Для Каждого СтрокаЭлементов Из ДанныеЭлементов Цикл
		
		ТекЭлемент = Элементы.Найти(СтрокаЭлементов.Ключ);		
		Если ТекЭлемент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеСтроки = СтрокаЭлементов.Значение;		
		
		Если ТипЗнч(ЗначениеСтроки) = Тип("Соответствие") И ТипЗнч(ТекЭлемент) = Тип("ТаблицаФормы") Тогда
			ПрочитатьНастройкиФормы(ЗначениеСтроки);
		ИначеЕсли ЗначениеСтроки.ЭтоГруппаКолонок Тогда
			ДобавитьНастройкуПорядка(ТекЭлемент, СтрокаЭлементов.Ключ, ЗначениеСтроки);
			ПрочитатьНастройкиФормы(ЗначениеСтроки.ЭлементыГруппы);
		Иначе
			ДобавитьНастройкуПорядка(ТекЭлемент, СтрокаЭлементов.Ключ, ЗначениеСтроки);
			ЗаполнитьНастройкиФормы(ЗначениеСтроки, ТекЭлемент);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры 

&НаСервере
Процедура ИзменитьПорядокВСохраненныеНастройкиФормы(ДанныеЭлементов, КлючОбъекта, КлючНастроек) 
	
	Если Не ЗначениеЗаполнено(НастройкиПорядок) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеКолонок = ДанныеЭлементов.Получить("СписокРегистрации");
	Если ДанныеКолонок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройки = Ложь;
	
	Для Каждого Настройка Из НастройкиПорядок Цикл
				
		НастройкиКолонки = ДанныеКолонок.Получить(Настройка.Ключ);
		Если ЗначениеЗаполнено(НастройкиКолонки) Тогда
			НастройкиКолонки.ИзмененПорядок = Ложь;
			СохранитьНастройки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СохранитьНастройки Тогда
		РегистрыСведений.гкс_ХранилищеНастроек.СохраненитьНастройку(КлючОбъекта, КлючНастроек, ДанныеЭлементов);
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиФормы(Настройки, ТекЭлемент) 

	Если Настройки.ЭтоФлажок Тогда
		СписокСвойств = "Видимость,ШиринаЭлемента";
		Настройки.Вставить("ШиринаЭлемента", Настройки.Ширина);
	ИначеЕсли Настройки.ЭтоГруппаКолонок Тогда
		СписокСвойств = "Видимость,Ширина";	
	Иначе
		СписокСвойств = "АвтоМаксимальнаяШирина,Видимость,МаксимальнаяШирина,Ширина";
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекЭлемент, Настройки, СписокСвойств);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНастройкуПорядка(ТекЭлемент, Ключ, ЗначениеСтроки) 

	Родитель = ТекЭлемент.Родитель;
	Индекс = Родитель.ПодчиненныеЭлементы.Индекс(ТекЭлемент);
	
	Если ЗначениеСтроки.ИзмененПорядок Тогда
		НоваяСтрока = НастройкиПорядок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначениеСтроки); 
		НоваяСтрока.Ключ 			= Ключ;
		НоваяСтрока.ТекущийИндекс 	= Индекс;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПоложенияЭлементаИзНастройки() 

	Если Не ЗначениеЗаполнено(НастройкиПорядок) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблица = НастройкиПорядок.Выгрузить();
	ВременнаяТаблица.Очистить();
	
	Для Каждого Настройка Из НастройкиПорядок Цикл
		
		НайденныйЭлемент = Элементы.Найти(Настройка.Ключ);
		
		Если НайденныйЭлемент = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РодительЭлемента = НайденныйЭлемент.Родитель;
		Если Настройка.ТекущийИндекс > Настройка.НовыйИндекс Тогда
			ИндексПередКоторымРазместить = Настройка.НовыйИндекс;
		Иначе
			ИндексПередКоторымРазместить = Настройка.НовыйИндекс + 1;
		КонецЕсли;
		Месторасположение = РодительЭлемента.ПодчиненныеЭлементы.Получить(ИндексПередКоторымРазместить);
		
		Элементы.Переместить(НайденныйЭлемент, РодительЭлемента, Месторасположение);
		
		Если Настройка.СтарыйИндекс = Настройка.НовыйИндекс Тогда
			ЗаполнитьЗначенияСвойств(ВременнаяТаблица.Добавить(), Настройка);
		КонецЕсли;
		
	КонецЦикла;
	
	// Если нет настроек с одинаковым индексом, то очистится НастройкиПорядок. 
	// Иначе заполним настроками с одинаковым индексом для изменения сохраненных настроек в регистре. 
	НастройкиПорядок.Загрузить(ВременнаяТаблица);
	
КонецПроцедуры

#КонецОбласти

//  Возвращает список регистраций помеченных для групповой обработки
//
//	Возвращаемое значение:
//		Массив из ДокументСсылка.гкс_РегистрацияНаПЛК
//
&НаСервере
Функция РегистрацииГрупповойОбработки()   
	
	Результат = Новый Массив;
	
	СхемаКомпоновкиДанных = Элементы.СписокРегистрации.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.СписокРегистрации.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	Если Не (ЭтоУстановкаПометокДляВсехПозиций 
		И РегистрацииИсключенные.Количество() = 0
		И РегистрацииВыбранные.Количество() = 0) Тогда
		
		НовыйЭлементОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ПолеОтбора = Новый ПолеКомпоновкиДанных("Пометка");
		НовыйЭлементОтбора.ЛевоеЗначение 	= ПолеОтбора;
		НовыйЭлементОтбора.Использование 	= Истина;
		НовыйЭлементОтбора.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
		НовыйЭлементОтбора.ПравоеЗначение 	= Истина;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	Попытка
		МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
			Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	Исключение
		Возврат Результат;
	КонецПопытки;
		
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТаблицаРезультат = ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);

	Возврат ТаблицаРезультат.ВыгрузитьКолонку("ДокументРегистрации");
	
КонецФункции

&НаСервереБезКонтекста
Функция НоменклатураВВыбранныхСтроках(ВыбранныеРегистрации)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрацияНаПЛК.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
	|ГДЕ
	|	РегистрацияНаПЛК.Ссылка В (&СписокРегистраций)
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияНаПЛК.Номенклатура";
	
	Запрос.УстановитьПараметр("СписокРегистраций", ВыбранныеРегистрации);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Результат = Выборка.Номенклатура;
	Иначе
		ТекстСообщения = НСтр("ru='В выбранных строках разная номенклатура! Не возможно применить групповую операцию.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);

		Результат = Неопределено;
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтатусДопускаВВыбранныхСтроках(ВыбранныеРегистрации, ТипДопуска)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СтатусыДопускаВагоновКВскрытию.ВскрытиеЗапрещено КАК ВскрытиеЗапрещено
	|ИЗ
	|	РегистрСведений.гкс_СтатусыДопускаВагоновКВскрытию.СрезПоследних(
	|			,
	|			ДокументРегистрации В (&СписокРегистраций)
	|				И ДопускКВскрытию = &ДопускКВскрытию) КАК СтатусыДопускаВагоновКВскрытию";
	
	Запрос.УстановитьПараметр("СписокРегистраций", ВыбранныеРегистрации);
	Запрос.УстановитьПараметр("ДопускКВскрытию", ТипДопуска);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
 		Выборка.Следующий();
		Результат = Выборка.ВскрытиеЗапрещено;
	Иначе
		ТекстСообщения = НСтр("ru='В выбранных строках у допуска ""%1"" установлены разные значения. 
								|Не возможно применить групповую операцию.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ТипДопуска); 
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);

		Результат = Неопределено;
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция КонтрольВыбранныхПоСостоянию(ВыбранныеРегистрации, ВалидныеСостояния, ВыводитьСообщения = Ложь)    
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СостоянияРегистрацииСрезПоследних.ДокументРегистрации КАК ДокументРегистрации,
		|	СостоянияРегистрацииСрезПоследних.Состояние КАК Состояние,
		|	СостоянияРегистрацииСрезПоследних.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ВЫБОР
		|		КОГДА СостоянияРегистрацииСрезПоследних.Состояние В (&Состояния)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоКорректноеСостояние
		|ИЗ
		|	РегистрСведений.гкс_СостоянияРегистрации.СрезПоследних(, ДокументРегистрации В (&МассивРегистраций)) КАК СостоянияРегистрацииСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоКорректноеСостояние УБЫВ
		|ИТОГИ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Состояние)
		|ПО
		|	ЭтоКорректноеСостояние";
	
	Запрос.УстановитьПараметр("МассивРегистраций", ВыбранныеРегистрации);
	Запрос.УстановитьПараметр("Состояния", ВалидныеСостояния); 
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	
	Пока Выборка.Следующий() Цикл               
		
		Если Выборка.ЭтоКорректноеСостояние Тогда
			// проверка на один вид состояния в выборке
		    Результат = ?(Выборка.Состояние = 1, Истина, Ложь);
			Если Не Результат И ВыводитьСообщения Тогда
				ТекстСообщения = НСтр("ru = 'Выбраны регистрации в разных состояниях!
                                       | Необходимо выбрать регистрации с одним состоянием'");	
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);	
			КонецЕсли;	
		Иначе	  	
			Результат = Ложь;
			Если ВыводитьСообщения Тогда	
				ВыборкаДетальныеЗаписи = Выборка.Выбрать();	
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  		
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Несоответствующее состояние документа для проведения операции:
                    |	%1, %2, %3'"), 
						ВыборкаДетальныеЗаписи.ДокументРегистрации, 
						ВыборкаДетальныеЗаписи.Состояние, 
						ВыборкаДетальныеЗаписи.ТранспортноеСредство);
					
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				КонецЦикла;             
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция КонтрольПрохожденияНеобходимыхСтатусов(ВыбранныеРегистрации)
	
	Результат = Истина;	
	
	Для Каждого ДокументРегистрации Из ВыбранныеРегистрации Цикл
		
		Если Не гкс_ПриемкаТранспорта.НаправлятьНаРазгрузкуРазрешение(ДокументРегистрации) Тогда		
			
			Результат = Ложь;
			ТранспортноеСредствоРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ДокументРегистрации, "ТранспортноеСредство");
			
			ТекстСообщения = СтрШаблон(НСтр("ru='Транспорт %1 не готов для направления на разгрузку:
				|	%2'"), ТранспортноеСредствоРегистрации, ДокументРегистрации);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;	
	КонецЦикла;              
	
	Возврат Результат;
	
КонецФункции	

&НаКлиенте
Функция СтруктураЗаполненияДанныхНаправлениеНаРазгрузку(ТекущиеДанные)

	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Дата", 				ОбщегоНазначенияКлиент.ДатаСеанса());	
	ЗначенияЗаполнения.Вставить("ДокументРегистрации", 	ТекущиеДанные.ДокументРегистрации);
	ЗначенияЗаполнения.Вставить("ТочкаМаршрута", 		ОтборТочкаМаршрута);
	ЗначенияЗаполнения.Вставить("ПервичныйСтатус", 		ТекущиеДанные.Состояние);
	
	// определим силос и яму разгрузки 
	Если ОтборТипРегистрации = ПредопределенноеЗначение("Перечисление.гкс_ТипРегистрации.Приемка") Тогда
		
		СостоянияПриемки = СостоянияПриемкиДляРазгрузки();
		Если СостоянияПриемки.Найти(ТекущиеДанные.Состояние) <> Неопределено Тогда 		
			ЗначенияЗаполнения.Вставить("Склад", 		ТекущиеДанные.СкладПлан);
			ЗначенияЗаполнения.Вставить("ЯмаРазгрузки", ТекущиеДанные.ЯмаРазгрузки);
		КонецЕсли;
			
		СлужебнаяНоменклатура = СлужебнаяНоменклатураДляФормированияНаправленияНаРагрузку(
			ТекущиеДанные.ДокументРегистрацииАнализов);
			
			Если ЗначениеЗаполнено(СлужебнаяНоменклатура) Тогда
				
			ЯмаСилосРазгрузки = гкс_ПриемкаТранспортаВызовСервера.ЯмаСилосРазгрузки(
				ОтборТочкаМаршрута, ОтборВидПеревозки, ТекущиеДанные.Номенклатура, СлужебнаяНоменклатура);
				
			Если ЗначениеЗаполнено(ЯмаСилосРазгрузки) Тогда	
				ЗначенияЗаполнения.Вставить("Склад", 					ЯмаСилосРазгрузки.Силос);
				ЗначенияЗаполнения.Вставить("ЯмаРазгрузки", 			ЯмаСилосРазгрузки.ЯмаРазгрузки);
				ЗначенияЗаполнения.Вставить("СлужебнаяНоменклатура", 	СлужебнаяНоменклатура);
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗначенияЗаполнения;
	
КонецФункции
	
&НаСервереБезКонтекста
Функция РеквизитыТекПользователь(ТекПользователь)
	
	Возврат	 ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекПользователь, "Наименование, ИдентификаторПользователяИБ");
		
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуНовогоДокументаРегистрацияНаПЛК(ЗначенияЗаполнения)  
	
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработчикОжиданияОбновитьФорму", ЭтотОбъект);
		
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ИзАРМа", 				Истина);
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", 	ЗначенияЗаполнения);
	
	Если ЗначенияЗаполнения.Свойство("ТранспортныйДокумент") Тогда
		ПараметрыОткрытия.Вставить("ТранспортныйДокумент", ЗначенияЗаполнения.ТранспортныйДокумент);	
	КонецЕсли;	
		
	ОткрытьФорму("Документ.гкс_РегистрацияНаПЛК.ФормаОбъекта", 
					ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор, Неопределено, Неопределено,
					ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

&НаКлиенте
Функция ЗначенияЗаполненияРегистрацииНаПЛК()
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТочкаМаршрута", 	ОтборТочкаМаршрута);
	ЗначенияЗаполнения.Вставить("ТипРегистрации", 	ОтборТипРегистрации);
	ЗначенияЗаполнения.Вставить("ВидПеревозки", 	ОтборВидПеревозки);	
	ЗначенияЗаполнения.Вставить("Внутригрупповой", 	Истина);
	
	Возврат ЗначенияЗаполнения;
		
КонецФункции		

&НаСервереБезКонтекста
Функция ВозможноАвтоЗавершениеПроцесса(ТипРегистрации, ТекущееСостояние)
			
	Возврат ТекущееСостояние = Перечисления.гкс_СостоянияРегистрации.ВзвешенБрутто
		И ТипРегистрации = Перечисления.гкс_ТипРегистрации.Отгрузка
		Или ТекущееСостояние = Перечисления.гкс_СостоянияРегистрации.ВзвешенТара 
		И ТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка;
			
КонецФункции
	
#КонецОбласти
