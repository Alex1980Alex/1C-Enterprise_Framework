#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// Определяет список команд отчетов.
//
// Параметры:
//  КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//  Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВариантыОтчетов

// СтандартныеПодсистемы.Печать

// Переопределяет настройки печати для объекта.
//
// Параметры:
//  Настройки - см. УправлениеПечатью.НастройкиПечатиОбъекта.
//
Процедура ПриОпределенииНастроекПечати(Настройки) Экспорт
	
	Настройки.ПриДобавленииКомандПечати = Истина;
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
// 	КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПФ_MXL_СкладскаяРасписка";
	КомандаПечати.Представление = НСтр("ru = 'Складская расписка'");
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать
//  ПараметрыПечати - Структура - дополнительные настройки печати
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение ссылка на объект
//                                            представление имя области, в которой был выведен объект (выходной параметр)
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр)
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_СкладскаяРасписка") Тогда
		ТабличныйДокумент = ПечатьСкладскойРасписки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПФ_MXL_СкладскаяРасписка", 
			НСтр("ru = 'Макет складской расписки'"), ТабличныйДокумент);
	КонецЕсли;

КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

// Групповое изменение реквизитов Регистрации из АРМ.
//
// Параметры:
//   ПараметрыЗаполнения - Структура
//
Процедура ГрупповоеИзменениеРегистраций(Знач ПараметрыЗаполнения) Экспорт
	
	ЗначенияЗаполнения = Новый Структура;		
	Если ПараметрыЗаполнения.Свойство("Номенклатура") Тогда
		ЗначенияЗаполнения.Вставить("Номенклатура", ПараметрыЗаполнения.Номенклатура);
	КонецЕсли;
		
	Если ПараметрыЗаполнения.Свойство("Комментарий") Тогда
		ЗначенияЗаполнения.Вставить("Комментарий", ПараметрыЗаполнения.Комментарий);
	КонецЕсли;
	
	Для Каждого ДокументРегистрация Из ПараметрыЗаполнения.ВыбранныеРегистрации Цикл	
		Документы.гкс_РегистрацияНаПЛК.ИзменитьРеквизитыДокумента(ДокументРегистрация, ЗначенияЗаполнения);	
	КонецЦикла;
	
КонецПроцедуры

// Групповое создание номеров проб.
// 
// Параметры:
//  ЗначенияЗаполнения - Структура - Значения заполнения
//  ВыбранныеРегистрации - Массив из ДокументСсылка.гкс_РегистрацияНаПЛК - массив документов к обработке
//
Процедура ГрупповоеСозданиеНомеровПроб(Знач ЗначенияЗаполнения, ВыбранныеРегистрации) Экспорт

	МенеджерОбъекта = Документы.гкс_ФормированиеНомераПробы;
	
	Для Каждого ДокументРегистрации Из ВыбранныеРегистрации Цикл
		
		ДокументФормированиеНомераПробы = МенеджерОбъекта.ДокументПоРегистрацииТипуПробы(
			ДокументРегистрации, Перечисления.гкс_ТипыПроб.Единичная); 
			
		Если Не ЗначениеЗаполнено(ДокументФормированиеНомераПробы) Тогда
			
			ЗначенияЗаполнения.Вставить("ДокументРегистрации", 	ДокументРегистрации);
		    // Попытка
			Документы.гкс_ФормированиеНомераПробы.СоздатьНовыйДокумент(ЗначенияЗаполнения);

		КонецЕсли;	
	КонецЦикла;
	
КонецПроцедуры

// Групповое направление на разгрузку.
// 
// Параметры:
//  ЗначенияЗаполнения - Структура - Значения заполнения
//  ВыбранныеРегистрации - Массив из ДокументСсылка.гкс_РегистрацияНаПЛК - массив документов к обработке
//
Процедура ГрупповоеНаправлениеНаРазгрузку(Знач ЗначенияЗаполнения, ВыбранныеРегистрации) Экспорт
		
	Для Каждого ДокументРегистрации Из ВыбранныеРегистрации Цикл
				
		Состояние = РегистрыСведений.гкс_СостоянияРегистрации.ТекущееСостояниеПоРегистрации(ДокументРегистрации);		
		ЭтоРезультатыПробПолучены = Перечисления.гкс_СостоянияРегистрации.ЭтоРезультатыПробПолучены(Состояние);		
			
		Если ЭтоРезультатыПробПолучены И Не ЗначенияЗаполнения.ПринятьКачество Тогда	
			Продолжить;
		КонецЕсли;	
				
		ЗначенияЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
		
		МестнаяДата = ПолучитьМестнуюДату(ЗначенияЗаполнения.ТочкаМаршрута);	
		ЗначенияЗаполнения.Вставить("МестнаяДата", МестнаяДата);
		
		ЗначенияЗаполнения.Вставить("ДокументРегистрации", ДокументРегистрации);
		ЗначенияЗаполнения.Вставить("ТочкаМаршрута", ЗначенияЗаполнения.ТочкаМаршрута);
		ЗначенияЗаполнения.Вставить("ПервичныйСтатус", Состояние); 
		
		НаправлениеНаРазгрузку = Документы.гкс_НаправлениеНаРазгрузку.ПолучитьПоРегистрации(ДокументРегистрации);
			
		Документы.гкс_НаправлениеНаРазгрузку.
				СоздатьИзменитьИзАРМПриемки(ЗначенияЗаполнения, 
				ЗначениеЗаполнено(НаправлениеНаРазгрузку), НаправлениеНаРазгрузку);
		
	КонецЦикла;
	
КонецПроцедуры

// Зарегистрировать пропуcк контроля качества для документов из массива.
// 
// Параметры:
//  ВыбранныеРегистрации - Массив из ДокументСсылка.гкс_РегистрацияНаПЛК - массив документов к обработке
//
Процедура ЗарегистрироватьПропуcкКонтроляКачества(Знач ВыбранныеРегистрации) Экспорт

	Для Каждого ДокументРегистрации Из ВыбранныеРегистрации Цикл
				
		НачатьТранзакцию();
		Попытка
			
			// 1. Записать новое состояние в РС "гкс_СостоянияРегистрации" 
			// 2. Записать новое состояние в РС "АРМПриемкиНаПЛКСписокРегистраций"
			гкс_ПриемкаТранспорта.ЗарегистрироватьНовоеСостояние(ДокументРегистрации, 
				Перечисления.гкс_СостоянияРегистрации.КачествоПринято);

			// 3. Записать в новый РС "Приемка без входного контроля"
			гкс_ПриемкаТранспорта.ЗаписатьРегистрПриемкиБезВходногоКонтроля(ДокументРегистрации);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = НСтр("ru='Невозможно изменить состояние регистрации %1 Повторите попытку'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументРегистрации);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Зарегистрировать новое состояние для документов из массива.
// 
// Параметры:
//  ВыбранныеРегистрации - Массив из ДокументСсылка.гкс_РегистрацияНаПЛК - массив документов к обработке
//  Состояние - ПеречислениеСсылка.гкс_СостоянияРегистрации - новое состояние регистрации
//
Процедура ЗарегистрироватьНовоеСостояниеПоСписку(Знач ВыбранныеРегистрации, Состояние) Экспорт

	Для Каждого ДокументРегистрации Из ВыбранныеРегистрации Цикл
				
		НачатьТранзакцию();
		Попытка
			гкс_ПриемкаТранспорта.ЗарегистрироватьНовоеСостояние(ДокументРегистрации, Состояние);
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = НСтр("ru='Невозможно изменить состояние регистрации %1 Повторите попытку'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, ДокументРегистрации);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Групповое изменение допусков вагонов.
//
// Параметры:
//   ЗначенияЗаполнения - Структура - данные заполнения
//   
//   ВыбранныеРегистрации - Массив из ДокументСсылка.гкс_РегистрацияНаПЛК - ссылки обрабатываемых документов
//
Процедура ГрупповоеИзменениеДопусков(ЗначенияЗаполнения, ВыбранныеРегистрации) Экспорт
		
	Для Каждого ДокументРегистрации Из ВыбранныеРегистрации Цикл	
			
		// Дополним параметры доступа значениями для регистрации
		Вагон = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРегистрации, "ТранспортноеСредство");
	  	ЗначенияЗаполнения.Вставить("Вагон", Вагон);
		ЗначенияЗаполнения.Вставить("ДокументРегистрации", ДокументРегистрации);
		
		Попытка
		
			РегистрыСведений.гкс_СтатусыДопускаВагоновКВскрытию.ЗарегистрироватьИзменениеДопуска(ЗначенияЗаполнения);
			
			ЕстьДопускПоКонтролямСтатусов = гкс_ПриемкаТранспорта.ЕстьДопускПоКонтролямСтатусов(ДокументРегистрации);
			ТекущееСостояние = гкс_ПриемкаТранспорта.ТекущееСостояниеРегистрации(ДокументРегистрации);

			Если ТекущееСостояние = Перечисления.гкс_СостоянияРегистрации.ВзвешенБрутто 
				И ЕстьДопускПоКонтролямСтатусов Тогда
				
				гкс_ПриемкаТранспорта.ЗарегистрироватьНовоеСостояние(ДокументРегистрации, 
						Перечисления.гкс_СостоянияРегистрации.ГотовКОтборуПроб, ЗначенияЗаполнения.Комментарий);							   
			КонецЕсли;
				
		Исключение

			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не изменить допуск %1. %2'"), 
				ДокументРегистрации, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументРегистрации); 
			
			ЗаписьЖурналаРегистрации("СтатусыДопускаВагоновКВскрытию.Запись", 
								УровеньЖурналаРегистрации.Ошибка,
								Метаданные.РегистрыСведений.гкс_СтатусыДопускаВагоновКВскрытию, , 
								ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));	
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

Функция ПечатьСкладскойРасписки(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураПодписантов = Неопределено;
	
	Если ПараметрыПечати <> Неопределено Тогда
		Если ПараметрыПечати.Свойство("СтруктураПодписантов") Тогда
			СтруктураПодписантов = ПараметрыПечати.СтруктураПодписантов;
		КонецЕсли;
	КонецЕсли; 
	
	ДанныеДляПечатиШапка = ВыбратьДанныеДляПечатиСкладскойРаспискиШапка(МассивОбъектов[0]);
	ДанныеДляПечатиТаблица = ВыбратьДанныеДляПечатиСкладскойРаспискиТаблица(МассивОбъектов);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_гкс_АРМПриемкаНаПЛК_ПФ_MXL_СкладскаяРасписка";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.гкс_ПриемкаТранспорта.ПФ_MXL_СкладскаяРасписка");
	
	СекцияШапка = Макет.ПолучитьОбласть("Шапка");
	Если ДанныеДляПечатиШапка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СекцияШапка.Параметры, ДанныеДляПечатиШапка);
		СекцияШапка.Параметры.ДатаРасписки = Формат(ДанныеДляПечатиШапка.ДатаРасписки, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(СекцияШапка);
	СекцияШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабличныйДокумент.Вывести(СекцияШапкаТаблицы);
	
	ИтогВесНетто = 0;
	НомерПП = 0;
	Пока ДанныеДляПечатиТаблица.Следующий() Цикл
		СекцияСтрока = Макет.ПолучитьОбласть("Строка");
		НомерПП = НомерПП + 1;
		ЗаполнитьЗначенияСвойств(СекцияСтрока.Параметры, ДанныеДляПечатиТаблица);
		СекцияСтрока.Параметры.НомерПП = НомерПП;
		ИтогВесНетто = ИтогВесНетто + ДанныеДляПечатиТаблица.ВесНетто;
		ТабличныйДокумент.Вывести(СекцияСтрока);
	КонецЦикла;
	
	СекцияПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	СекцияПодвалТаблицы.Параметры.ВесНетто = ИтогВесНетто;
	ТабличныйДокумент.Вывести(СекцияПодвалТаблицы);
	СекцияПодвал = Макет.ПолучитьОбласть("Подвал");
	ЗаполнитьЗначенияСвойств(СекцияПодвал.Параметры, СтруктураПодписантов);
	ТабличныйДокумент.Вывести(СекцияПодвал);
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ВыбратьДанныеДляПечатиСкладскойРаспискиШапка(СсылкаНаПервыйДокумент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБТ_ДвижениеЗапасов.Организация КАК Организация,
		|	ЦБТ_ДвижениеЗапасов.ПунктРазгрузки КАК Склад,
		|	НАЧАЛОПЕРИОДА(ЦБТ_ДвижениеЗапасов.Дата, ДЕНЬ) КАК ДатаРасписки,
		|	РегистрацияНаПЛК.Собственник КАК ВладелецГруза
		|ИЗ
		|	Документ.ЦБТ_ДвижениеЗапасов КАК ЦБТ_ДвижениеЗапасов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
		|		ПО ЦБТ_ДвижениеЗапасов.гкс_ДокументРегистрации = РегистрацияНаПЛК.Ссылка
		|ГДЕ
		|	ЦБТ_ДвижениеЗапасов.Ссылка = &СсылкаНаПервыйДокумент";
	
	Запрос.УстановитьПараметр("СсылкаНаПервыйДокумент", СсылкаНаПервыйДокумент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаДетальныеЗаписи;
КонецФункции

Функция ВыбратьДанныеДляПечатиСкладскойРаспискиТаблица(МассивОбъектов)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЦБТ_ДвижениеЗапасов.Ссылка КАК ДвижениеЗапасов,
		|	ЦБТ_ДвижениеЗапасов.гкс_ДокументРегистрации КАК ДокументРегистрации
		|ПОМЕСТИТЬ ВТ_ДвиженияЗапасов
		|ИЗ
		|	Документ.ЦБТ_ДвижениеЗапасов КАК ЦБТ_ДвижениеЗапасов
		|ГДЕ
		|	ЦБТ_ДвижениеЗапасов.Ссылка В(&МассивОбъектов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДвиженияЗапасов.ДвижениеЗапасов КАК ДвижениеЗапасов,
		|	ВТ_ДвиженияЗапасов.ДокументРегистрации КАК ДокументРегистрации,
		|	МАКСИМУМ(ЕСТЬNULL(ВзвешиваниеБрутто.Ссылка, ЗНАЧЕНИЕ(Документ.гкс_Взвешивание.ПустаяСсылка))) КАК ДокументВзвешиваниеБрутто,
		|	МАКСИМУМ(ЕСТЬNULL(ВзвешиваниеТара.Ссылка, ЗНАЧЕНИЕ(Документ.гкс_Взвешивание.ПустаяСсылка))) КАК ДокументВзвешиваниеТары
		|ПОМЕСТИТЬ ВТ_Взвешивание
		|ИЗ
		|	ВТ_ДвиженияЗапасов КАК ВТ_ДвиженияЗапасов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеБрутто
		|		ПО ВТ_ДвиженияЗапасов.ДокументРегистрации = ВзвешиваниеБрутто.ДокументРегистрации
		|			И (ВзвешиваниеБрутто.ТипВзвешивания = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыВзвешивания.Въезд))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеТара
		|		ПО ВТ_ДвиженияЗапасов.ДокументРегистрации = ВзвешиваниеТара.ДокументРегистрации
		|			И (ВзвешиваниеТара.ТипВзвешивания = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыВзвешивания.Выезд))
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДвиженияЗапасов.ДвижениеЗапасов,
		|	ВТ_ДвиженияЗапасов.ДокументРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВзвешиваниеБрутто.Вес, 0) / 1000 КАК ВесБрутто,
		|	ЕСТЬNULL(ВзвешиваниеТара.Вес, 0) / 1000 КАК ВесТары,
		|	ЦБТ_ДвижениеЗапасовТовары.КоличествоРазгрузка КАК ВесНетто,
		|	ЦБТ_ДвижениеЗапасовТовары.ТранспортныйДокумент КАК ТранспортныйДокумент,
		|	ЦБТ_ДвижениеЗапасовТовары.Номенклатура КАК Номенклатура,
		|	ЦБТ_ДвижениеЗапасовТовары.НомерТранспортногоСредства КАК ТранспортноеСредство,
		|	СпрНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ЦБТ_ТранспортныйДокумент.ПунктПогрузки, ЗНАЧЕНИЕ(Справочник.гкс_ТочкиМаршрута.ПустаяСсылка)) КАК СтанцияОтправления
		|ПОМЕСТИТЬ ВТ_Итог
		|ИЗ
		|	ВТ_Взвешивание КАК ВТ_Взвешивание
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЦБТ_ДвижениеЗапасов.Товары КАК ЦБТ_ДвижениеЗапасовТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|			ПО ЦБТ_ДвижениеЗапасовТовары.Номенклатура = СпрНоменклатура.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_ТранспортныйДокумент КАК ЦБТ_ТранспортныйДокумент
		|			ПО ЦБТ_ДвижениеЗапасовТовары.ТранспортныйДокумент = ЦБТ_ТранспортныйДокумент.Ссылка
		|		ПО ВТ_Взвешивание.ДвижениеЗапасов = ЦБТ_ДвижениеЗапасовТовары.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеБрутто
		|		ПО ВТ_Взвешивание.ДокументВзвешиваниеБрутто = ВзвешиваниеБрутто.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеТара
		|		ПО ВТ_Взвешивание.ДокументВзвешиваниеТары = ВзвешиваниеТара.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Итог.ВесБрутто КАК ВесБрутто,
		|	ВТ_Итог.ВесТары КАК ВесТары,
		|	ВТ_Итог.ВесНетто КАК ВесНетто,
		|	ВТ_Итог.ТранспортныйДокумент КАК ТранспортныйДокумент,
		|	ВТ_Итог.Номенклатура КАК Номенклатура,
		|	ВТ_Итог.ТранспортноеСредство КАК ТранспортноеСредство,
		|	ВТ_Итог.ЕдиницаИзмерения КАК ЕдИзм,
		|	гкс_ТочкиМаршрута.КодСтанцииЖД КАК СтанцияОтправления
		|ИЗ
		|	ВТ_Итог КАК ВТ_Итог
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_ТочкиМаршрута КАК гкс_ТочкиМаршрута
		|		ПО ВТ_Итог.СтанцияОтправления = гкс_ТочкиМаршрута.Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаДетальныеЗаписи;
	
КонецФункции

#КонецОбласти

Функция ПолучитьМестнуюДату(ТочкаМаршрута)
	
	ЧасовойПоясПЛК 	= гкс_ПриемкаТранспорта.ЧасовойПоясПЛК(ТочкаМаршрута);
	МестнаяДата 	= ТекущаяДатаСеанса();
	Если ЗначениеЗаполнено(ЧасовойПоясПЛК) Тогда
		МестнаяДата = МестноеВремя(ТекущаяУниверсальнаяДата(), ЧасовойПоясПЛК);
	КонецЕсли;
	
	Возврат МестнаяДата;
	
КонецФункции

#КонецОбласти 

#КонецЕсли
