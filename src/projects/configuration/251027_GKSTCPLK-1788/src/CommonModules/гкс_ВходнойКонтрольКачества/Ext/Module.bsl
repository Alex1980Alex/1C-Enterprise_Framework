#Область ПрограммныйИнтерфейс

// Регистрирует данные для усиленного входного контроля
// Параметры:
// ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
Процедура ЗарегистрироватьТранспортноеСредствоДляУсиленногоКонтроля(ДокументРегистрации, МестнаяДата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеПоказателей = Неопределено;
	Если Не ТранспортноеСредствоЗарегистрировано(ДокументРегистрации) Тогда                                        
		ПервоеПоступлениеПостащика = Неопределено;
		ЭтоПервоеТС = ЭтоПервоеТСВСуткахОтПоставщика(ДокументРегистрации, МестнаяДата, ПервоеПоступлениеПостащика);
		Если ТребуетсяУсиленныйКонтроль(ДокументРегистрации, МестнаяДата, ДанныеПоказателей) Тогда
		
			Если ДанныеПоказателей.ПоказателиКонтроля.Количество() > 0 Тогда
				ЗаписатьДанныеПоУсиленномуКонтролюДляРегистрации(ДокументРегистрации, ДанныеПоказателей.ПоказателиКонтроля);
				ЗаписатьПравилаПоУсиленномуКонтролюДляРегистрации(ДокументРегистрации, ДанныеПоказателей.ПравилаКонтроля);
				УстановитьНовыеЗначенияНастройкеУсиленногоКонтроля(
					ДанныеПоказателей.ПравилаКонтроляТС, "Активен", Истина, Ложь);
			КонецЕсли;		
		КонецЕсли;
		ЗаписатьДанныеПоМашинеПоставщика(ДокументРегистрации, МестнаяДата, ЭтоПервоеТС);	
	КонецЕсли;                     
	
КонецПроцедуры  

// Процедура заполняет лаб. анализ и статус прохождения анализа
// Параметры:
// ЛабораторныйАнализ - ДокументСсылка.гкс_ЛабораторныйАнализ 
// ДокументДегистрации - ДокументССылка.гкс_РегистрацияНаПЛК
Процедура ЗаполнитьРегистрПоказателейУсиленногоКонтроляЛабАнализом(ЛабораторныйАнализ, ДокументРегистрации, НазначениеИспользования) Экспорт
	
	НаборЗаписей = РегистрыСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументРегистрации.Установить(ДокументРегистрации);
	НаборЗаписей.Отбор.НазначениеИспользования.Установить(НазначениеИспользования);
	НаборЗаписей.Прочитать();
	
	КачественныеПоказатели = ЛабораторныйАнализ.КачественныеПоказатели; 
	Если НаборЗаписей.Количество() = 0
		Или КачественныеПоказатели.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Статус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЛабораторныйАнализ, "Статус");
	КачествоНеПройдено = Новый Массив;
	Для Каждого Запись Из НаборЗаписей Цикл                   
		Показатель = Запись.Показатель;
		СтрокаПоказатель = КачественныеПоказатели.Найти(Показатель);
		Если Не СтрокаПоказатель = Неопределено Тогда
			Запись.СостояниеКачества = 
				СтатусКачестваПоЛабАнализу(СтрокаПоказатель.СоответствуетНормативу, Статус);
			Если Запись.СостояниеКачества = Перечисления.гкс_СостоянияКачества.КачествоНеПринято Тогда
				КачествоНеПройдено.Добавить(Показатель);
			КонецЕсли;	
			Если НЕ ЗначениеЗаполнено(Запись.ЛабораторныйАнализ) Тогда
				Запись.ЛабораторныйАнализ = ЛабораторныйАнализ;       
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НаборЗаписей.Модифицированность() Тогда
		НаборЗаписей.Записать();
	КонецЕсли; 
	
	Если КачествоНеПройдено.Количество() > 0 Тогда
		ИзменитьТипНастройкиУсиленногоКонтроля(ДокументРегистрации, КачествоНеПройдено);
	КонецЕсли;	
	
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ИзменитьТипНастройкиУсиленногоКонтроля(ДокументРегистрации, ПоказателиКачества)
	
	НайстройкиКонтроля = 
		НастройкиУсиленногоКонтроляСОтборомПоПоказателям(ДокументРегистрации, ПоказателиКачества);
		
	Если НайстройкиКонтроля.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	                              
	
	УстановитьНовыеЗначенияНастройкеУсиленногоКонтроля(
		НайстройкиКонтроля, "ТипВходногоКонтроля", 
		Перечисления.гкс_ТипыУсиленногоКонтроляКачества.ПервоеТС,
		Перечисления.гкс_ТипыУсиленногоКонтроляКачества.КаждоеТС);
	
	
КонецПроцедуры                                                                                    
	
Функция НастройкиУсиленногоКонтроляСОтборомПоПоказателям(ДокументРегистрации, ПоказателиКачества)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПравилаУсиленногоКонтроляДляТранспортногоСредства.ИдентификаторПравила КАК ИдентификаторПравила
	               |ИЗ
	               |	РегистрСведений.гкс_ПравилаУсиленногоКонтроляПоРегистрации КАК ПравилаУсиленногоКонтроляДляТранспортногоСредства
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.гкс_НастройкиУсиленногоКонтроляКачества КАК НастройкиУсиленногоКонтроляКачества
	               |		ПО ПравилаУсиленногоКонтроляДляТранспортногоСредства.ИдентификаторПравила = НастройкиУсиленногоКонтроляКачества.ИдентификаторПравила
	               |			И (НастройкиУсиленногоКонтроляКачества.Активен)
	               |			И (НастройкиУсиленногоКонтроляКачества.ТипВходногоКонтроля = &ПервоеТС)
	               |ГДЕ
	               |	ПравилаУсиленногоКонтроляДляТранспортногоСредства.ДокументРегистрации = &ДокументРегистрации
	               |	И ПравилаУсиленногоКонтроляДляТранспортногоСредства.Показатель В(&Показатели)";
	
	Запрос.УстановитьПараметр("Показатели", ПоказателиКачества);
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	Запрос.УстановитьПараметр("ПервоеТС", Перечисления.гкс_ТипыУсиленногоКонтроляКачества.ПервоеТС);
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();	
	
КонецФункции	

Функция СтатусКачестваПоЛабАнализу(СоответствуетНормативу, СтатусЛабАнализа)
	
	Если СтатусЛабАнализа = Перечисления.гкс_СтатусыЛабораторногоАнализа.ОтборПробы Тогда
		Результат = Перечисления.гкс_СостоянияКачества.ОтборПроб;
	ИначеЕсли СтатусЛабАнализа = Перечисления.гкс_СтатусыЛабораторногоАнализа.Выполнен Тогда          
		Результат = ?(СоответствуетНормативу,
			Перечисления.гкс_СостоянияКачества.КачествоПринято,
			Перечисления.гкс_СостоянияКачества.КачествоНеПринято);
	Иначе	     
		Результат = Перечисления.гкс_СостоянияКачества.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции	

Функция ЭтоПервоеТСВСуткахОтПоставщика(ДокументРегистрации, МестнаяДата, ПервоеПоступлениеПостащика) 
	Запрос = Новый Запрос;  
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	Запрос.УстановитьПараметр("МестнаяДата", МестнаяДата);
	Запрос.Текст = ТекстЗапросаПервоеТСВСуткахОтПоставщика();
	Результат = Запрос.Выполнить(); 
	ПустойРезультат = Результат.Пустой();
	Если Не ПустойРезультат Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПервоеПоступлениеПостащика = Выборка.ДокументРегистрации;
	КонецЕсли;
	
	Возврат 
		Результат.Пустой();
КонецФункции 

Функция ТребуетсяУсиленныйКонтроль(ДокументРегистрации, МестнаяДата, ДанныеПоказателей = Неопределено)
	
	ДанныеПоказателей = 
		РегистрыСведений.гкс_НастройкиУсиленногоКонтроляКачества.НайтиПоказателиКачестваДляУсиленногоВходногоКонтроля(
			ДокументРегистрации, МестнаяДата);
	Показатели = ДанныеПоказателей.ПоказателиКонтроля;	
			
	Возврат Показатели.Количество();
	
КонецФункции  

Функция ТранспортноеСредствоЗарегистрировано(ДокументРегистрации)
	
	Запрос = Новый Запрос;                                                                          
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	Запрос.Текст =  
	"ВЫБРАТЬ
    |	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ДокументРегистрации КАК ДокументРегистрации
    |ИЗ
    |	РегистрСведений.гкс_ТранспортныеСредстваПоставщикаУсиленногоКонтроля КАК ТранспортныеСредстваПоставщикаУсиленногоКонтроля
    |ГДЕ
    |	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ДокументРегистрации = &ДокументРегистрации";
	
	Результат = Запрос.Выполнить();
	Возврат
		Не Результат.Пустой();
	
КонецФункции

Функция ТекстЗапросаПервоеТСВСуткахОтПоставщика()
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&МестнаяДата, ЧАС, -СмещениеПроизводственныхСуток.Значение), ДЕНЬ) КАК ПроизводственныеСутки,
		|	РегистрацияНаПЛК.Контрагент КАК Контрагент,
		|	РегистрацияНаПЛК.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_Регистрация
		|ИЗ
		|	Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.гкс_СмещениеПроизводственныхСуток КАК СмещениеПроизводственныхСуток
		|		ПО (ИСТИНА)
		|ГДЕ
		|	РегистрацияНаПЛК.Ссылка = &ДокументРегистрации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ДокументРегистрации КАК ДокументРегистрации
		|ИЗ
		|	РегистрСведений.гкс_ТранспортныеСредстваПоставщикаУсиленногоКонтроля КАК ТранспортныеСредстваПоставщикаУсиленногоКонтроля
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрация КАК ВТ_Регистрация
		|		ПО ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ПроизводственныеСутки = ВТ_Регистрация.ПроизводственныеСутки
		|			И ТранспортныеСредстваПоставщикаУсиленногоКонтроля.Контрагент = ВТ_Регистрация.Контрагент
		|			И ТранспортныеСредстваПоставщикаУсиленногоКонтроля.Номенклатура = ВТ_Регистрация.Номенклатура
		|ГДЕ
		|	ТранспортныеСредстваПоставщикаУсиленногоКонтроля.ЭтоПервоеТС";
		
КонецФункции	

Функция ИнициироватьТаблицуРегистрацииПоказателей()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ДокументРегистрации");
	Таблица.Колонки.Добавить("Показатель");
	Таблица.Колонки.Добавить("ЗначениеПоказателяМин");
	Таблица.Колонки.Добавить("ЗначениеПоказателяМакс");  
	Таблица.Колонки.Добавить("Базис");  
	
	Возврат Таблица;
	
КонецФункции

Процедура ЗаписатьДанныеПоУсиленномуКонтролюДляРегистрации(ДокументРегистрации, ТаблицаПоказателей)
	
	НаборЗаписей = РегистрыСведений.гкс_ПоказателиУсиленногоКонтроляПоРегистрации.СоздатьНаборЗаписей();	
	НаборЗаписей.Отбор.ДокументРегистрации.Установить(ДокументРегистрации);  
	НаборЗаписей.Загрузить(ТаблицаПоказателей);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ЗаписатьДанныеПоМашинеПоставщика(ДокументРегистрации, МестнаяДата, ЭтоПервоеТС)
	
	Реквизиты = "ТранспортноеСредство, Контрагент, Номенклатура";
	
	НаборЗаписей = РегистрыСведений.гкс_ТранспортныеСредстваПоставщикаУсиленногоКонтроля.СоздатьНаборЗаписей();	
	НаборЗаписей.Отбор.ДокументРегистрации.Установить(ДокументРегистрации);   
	Запись = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(Запись, ДокументРегистрации, Реквизиты);
	Запись.МестнаяДата = МестнаяДата;
	Запись.ПроизводственныеСутки = 
		гкс_ПриемкаТранспорта.ПроизводственныеСуткиПоДата(МестнаяДата);
	Запись.ЭтоПервоеТС = ЭтоПервоеТС;	
	Запись.ДокументРегистрации = ДокументРегистрации;
	НаборЗаписей.Записать();
	
КонецПроцедуры   

Процедура ЗаписатьПравилаПоУсиленномуКонтролюДляРегистрации(ДокументРегистрации, ПравилаКонтроля)
	
	НаборЗаписей = РегистрыСведений.гкс_ПравилаУсиленногоКонтроляПоРегистрации.СоздатьНаборЗаписей();	
	НаборЗаписей.Отбор.ДокументРегистрации.Установить(ДокументРегистрации);                                         
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() <> 0 Тогда
		НаборЗаписей.Очистить();
	КонецЕсли;
	НаборЗаписей.Загрузить(ПравилаКонтроля);
	НаборЗаписей.Записать();
	
КонецПроцедуры	

Процедура УстановитьНовыеЗначенияНастройкеУсиленногоКонтроля(ПравилаКонтроляТС, ИмяРеквизита, СтароеЗначение, НовоеЗначение)
	
	Для каждого ПравилоТС из ПравилаКонтроляТС цикл
		НаборЗаписей = РегистрыСведений.гкс_НастройкиУсиленногоКонтроляКачества.СоздатьНаборЗаписей();	
		НаборЗаписей.Отбор.ИдентификаторПравила.Установить(ПравилоТС.ИдентификаторПравила);                                         
		НаборЗаписей.Прочитать();
		Для каждого Запись из НаборЗаписей цикл
			Если Запись[ИмяРеквизита] = СтароеЗначение Тогда
				Запись[ИмяРеквизита] = НовоеЗначение;
			КонецЕсли;	
		КонецЦикла;   
		Если НаборЗаписей.Модифицированность() Тогда
			НаборЗаписей.Записать();
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти 
