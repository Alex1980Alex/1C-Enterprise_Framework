// @strict-types

#Область ПрограммныйИнтерфейс

// Возвращает текущее состояние для документа регистрации.
//
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - 
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.гкс_СостоянияРегистрации - текущее состояние регистрации на ПЛК.
//
Функция ТекущееСостояниеРегистрации(ДокументРегистрации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущееСостояние =  РегистрыСведений.гкс_СостоянияРегистрации.ТекущееСостояниеПоРегистрации(ДокументРегистрации);
	Если ТекущееСостояние = Неопределено Тогда
		ТекущееСостояние = Перечисления.гкс_СостоянияРегистрации.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ТекущееСостояние;
	
КонецФункции

// Делает запись в регистр сведений о новом состоянии регистрации
//
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - документ для изменения статуса
//  Состояние - ПеречислениеСсылка.гкс_СостоянияРегистрации - новое состояние регистрации
//  Комментарий - Строка - дополнительный комментарий
//                
Процедура ЗарегистрироватьНовоеСостояние(ДокументРегистрации, Состояние, Комментарий = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущееСостояниеПоРегистрации = РегистрыСведений.гкс_СостоянияРегистрации
		.ТекущееСостояниеПоРегистрации(ДокументРегистрации);
		
	Если ТекущееСостояниеПоРегистрации = Состояние Тогда
		Возврат;	
	КонецЕсли;	
	
	ОписаниеСостояния = РегистрыСведений.гкс_СостоянияРегистрации.СтруктураЗаписи();
		
	РеквизитыРегистрации = "ТочкаМаршрута, ТипРегистрации, ВидПеревозки, ТранспортноеСредство";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРегистрации, РеквизитыРегистрации);
	
	ЗаполнитьЗначенияСвойств(ОписаниеСостояния, ЗначенияРеквизитов);
	
    ОписаниеСостояния.ДокументРегистрации = ДокументРегистрации;
	ОписаниеСостояния.Состояние 	= Состояние;	
	ОписаниеСостояния.Пользователь	= Пользователи.ТекущийПользователь();
	ОписаниеСостояния.Комментарий	= Комментарий;
	
	ЧасовойПоясТочкиМаршрута = ЧасовойПоясПЛК(ОписаниеСостояния.ТочкаМаршрута);
		
	ОписаниеСостояния.Период = ТекущаяДатаСеанса();
	ОписаниеСостояния.МестнаяДата = МестноеВремя(ТекущаяУниверсальнаяДата(), ЧасовойПоясТочкиМаршрута);

	РегистрыСведений.гкс_СостоянияРегистрации.УстановитьСостояниеРегистрации(ОписаниеСостояния);
	
КонецПроцедуры

// Возвращает строковый идентифкатор часовой пояс точки маршрута, представляющей ПЛК 
//
// Параметры:
//  ТочкаМаршрута - СправочникСсылка.гкс_ТочкиМаршрута - Точка маршрута
// 
// Возвращаемое значение:
//  Строка - идентификатор часового пояса.
//
Функция ЧасовойПоясПЛК(ТочкаМаршрута) Экспорт
	
	Если Не ЗначениеЗаполнено(ТочкаМаршрута) Тогда
		Возврат "";	
	КонецЕсли;
	
	ЧасовойПояс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТочкаМаршрута, "гкс_ЧасовойПоясТочкиМаршрута");
	
	Возврат ЧасовойПояс;
	
КонецФункции

// Типы документов регистрации.
// 
// Возвращаемое значение:
//  Массив - Типы документов регистрации
Функция ТипыДокументовПриемкиПЛК() Экспорт

	ТипДокументовПриемки = Новый Массив;
	ТипДокументовПриемки.Добавить(Тип("ДокументСсылка.гкс_Взвешивание"));
	ТипДокументовПриемки.Добавить(Тип("ДокументСсылка.гкс_НаправлениеНаРазгрузку"));
	ТипДокументовПриемки.Добавить(Тип("ДокументСсылка.гкс_ЛабораторныйАнализ"));
	ТипДокументовПриемки.Добавить(Тип("ДокументСсылка.гкс_ФормированиеНомераПробы"));
	ТипДокументовПриемки.Добавить(Тип("ДокументСсылка.гкс_РегистрацияНаПЛК"));
	
	Возврат ТипДокументовПриемки;
	
КонецФункции

// Проверяет возможность измения документа из цепочки Приемки
//
// Параметры:
//   Документ - ДокументСсылка - ссылка на документ.
//
// Возвращаемое значение:
//  Булево - Истина, если на документ регистрации оформлен реестр накладных
//
Функция ЗапретИзмененияДокументаПоРеестру(Документ) Экспорт
	
	Результат = Ложь;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.гкс_РеестрНакладныхЗПП3") Тогда	
		Возврат Документ.Проведен;
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.гкс_ОснованиеДляДвиженияЗапасов") Тогда	
		
		Если гкс_ДокументальноеОформление.ОформленРеестрНаДвижениеЗапасов(Документ) Тогда		
			Результат = Истина;
		КонецЕсли;
		
	Иначе
		
		УстановитьПривилегированныйРежим(Истина);
		ДокументРегистрации = РегистрыСведений.гкс_СостоянияРегистрации.ДокументРегистрации(Документ);
		
		Если Не ЗначениеЗаполнено(ДокументРегистрации) Тогда
			Возврат Результат;
		КонецЕсли;
		
		ТипДокумента = ТипЗнч(ДокументРегистрации);
		Если ТипДокумента = Тип("ДокументСсылка.гкс_ФормированиеНомераПробы") Тогда
			Возврат ЗапретИзмененияДокументаПоРеестру(ДокументРегистрации);
			
		ИначеЕсли ТипДокумента <> Тип("ДокументСсылка.гкс_РегистрацияНаПЛК") Тогда
			Возврат Результат;	
		КонецЕсли;	
		
		Если гкс_ДокументальноеОформление.ОформленРеестрНаРегистрацию(ДокументРегистрации) Тогда
			
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;	
		
	Возврат Результат;
	
КонецФункции

// Проверяет функциональную опцию автоматического формирования лаб. анализа
// типа Композит.
// 
// Возвращаемое значение:
//  Булево - Истина, если опция включена
//
Функция ФормироватьКомпозитныйЛабораторныйАнализ() Экспорт
	
	Если ПолучитьФункциональнуюОпцию("гкс_ФормироватьКомпозитныйЛабораторныйАнализАвто") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;	
	КонецЕсли;
	
КонецФункции	

// Возвращает текущую яму и сислос разгрузки номенклатуры по качеству
//
// Параметры:
//  ТочкаМаршрута  - СправочникСсылка.гкс_ТочкиМаршрута - ПЛК
//  ВидПеревозки  - ПеречислениеСсылка.гкс_ТипыТранспортныхСредствДоставки - Авто,ЖД и пр
//  Номенклатура  - СправочникСсылка.Номенклатура - номенклатура для разгрузки
//  СлужебнаяНоменклатура  - СправочникСсылка.Номенклатура - служебная номенклатура (сегмент качества)
//
// Возвращаемое значение:
//   Структура   - 
//		* ЯмаРазгрузки - СправочникСсылка.гкс_ТочкиМаршрута
//		* Силос - СправочникСсылка.гкс_ТочкиМаршрута
//
Функция ЯмаСилосРазгрузки(ТочкаМаршрута, ВидПеревозки, Номенклатура, СлужебнаяНоменклатура) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат РегистрыСведений.гкс_НастройкиНазначенияРазгрузки
			.ЯмаСилосРазгрузки(ТочкаМаршрута, ВидПеревозки, Номенклатура, СлужебнаяНоменклатура);
	
КонецФункции

// Возвращает ПЛК из константы или настроек Пользователя
// 
// Параметры:     
// 	ИспользоватьНесколькоАЛЦ - Булево
// 	МассивПЛК - Массив из СправчоникСсылка.гкс_ТочкиМаршрута
// 
// Возвращаемое значение:
// 	СправочникСсылка.гкс_ТочкиМаршрута
Функция ПЛКизНастроекБазыПользователя(ИспользоватьНесколькоАЛЦ, МассивПЛК) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ИспользоватьНесколькоАЛЦ Тогда
		МассивПЛК = РегистрыСведений.гкс_НастройкиПользователейПриемкаНаПЛК
			.НастроенныеТочкиМаршрутаПользователя();
		Если МассивПЛК.Количество() > 0 Тогда
			ПервыйЭлемент = 0;
			ПЛК = МассивПЛК[ПервыйЭлемент]; 
		Иначе
			ПЛК = Справочники.гкс_ТочкиМаршрута.ПустаяСсылка();
		КонецЕсли;	
	Иначе
		ПЛК = Константы.гкс_ТочкаМаршрутаБазы.Получить();	
	КонецЕсли;  
	
	Возврат ПЛК;
	
КонецФункции

// Тип регистрации приемка.
// 
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - 
// 
// Возвращаемое значение:
//  Булево - Истина когда "Тип регистрации" -  "приемка"
//
Функция ТипРегистрацииПриемка(ДокументРегистрации) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРегистрации, "ТипРегистрации")
		= Перечисления.гкс_ТипРегистрации.Приемка;
КонецФункции

// Получает список служебной номенклатуры из сегментации качества по переданной номенклатуре
// 
//	Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура для поиска сегментов
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.Номенклатура - номенклатура из регистра сведений сегментации качества
//
Функция СегментацияКачестваНоменклатуры(Номенклатура) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат РегистрыСведений.гкс_СегментацияКачестваНоменклатуры.
		ПолучитьСлужебнуюНоменклатуруПоСегментации(Номенклатура);
	
КонецФункции

// Проверка разрешения по всем видам допуска вагона.
// 
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - документ регситрации вагона
// 
// Возвращаемое значение:
//  Булево - Есть допуск по контролям статусов
//
Функция ЕстьДопускПоКонтролямСтатусов(ДокументРегистрации) Экспорт
	
	ТекущиеДопуски = ТекущиеДопускиПоРегистрации(ДокументРегистрации); 	
	
	Возврат Не ТекущиеДопуски.ФумигационныйЗапрет 
			И Не ТекущиеДопуски.ФитосанитарныйЗапрет 
			И Не ТекущиеДопуски.ПретензионныйЗапрет;
	
КонецФункции

// Возращает струкутру с текущими значениями допусков по регистрации вагона.
// 
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - Документ регистрации
// 
// Возвращаемое значение:
//  Структура - Текущие допуски по регистрации:
// * ФумигационныйЗапрет - Булево - 
// * ФитосанитарныйЗапрет - Булево - 
// * ПретензионныйЗапрет - Булево - 
//
Функция ТекущиеДопускиПоРегистрации(ДокументРегистрации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ЕСТЬNULL(ФумигационныйСтатус.ВскрытиеЗапрещено, ЛОЖЬ) КАК ФумигационныйЗапрет,
				|	ЛОЖЬ КАК ФитосанитарныйЗапрет,
				|	ЛОЖЬ КАК ПретензионныйЗапрет
				|ПОМЕСТИТЬ ВТ_ЗапретыПоДопуску
				|ИЗ
				|	РегистрСведений.гкс_СтатусыДопускаВагоновКВскрытию.СрезПоследних(
				|			,
				|			ДокументРегистрации = &ДокументРегистрации
				|				И ДопускКВскрытию = ЗНАЧЕНИЕ(Перечисление.гкс_ДопускиКВскрытиюВагонов.Фумигационный)) КАК ФумигационныйСтатус
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ЛОЖЬ,
				|	ЕСТЬNULL(ФитосанитарныйСтатус.ВскрытиеЗапрещено, ЛОЖЬ),
				|	ЛОЖЬ
				|ИЗ
				|	РегистрСведений.гкс_СтатусыДопускаВагоновКВскрытию.СрезПоследних(
				|			,
				|			ДокументРегистрации = &ДокументРегистрации
				|				И ДопускКВскрытию = ЗНАЧЕНИЕ(Перечисление.гкс_ДопускиКВскрытиюВагонов.Фитосанитарный)) КАК ФитосанитарныйСтатус
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ЛОЖЬ,
				|	ЛОЖЬ,
				|	ЕСТЬNULL(ПретензионныйСтатус.ВскрытиеЗапрещено, ЛОЖЬ)
				|ИЗ
				|	РегистрСведений.гкс_СтатусыДопускаВагоновКВскрытию.СрезПоследних(
				|			,
				|			ДокументРегистрации = &ДокументРегистрации
				|				И ДопускКВскрытию = ЗНАЧЕНИЕ(Перечисление.гкс_ДопускиКВскрытиюВагонов.Претензионный)) КАК ПретензионныйСтатус
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	МАКСИМУМ(ВТ_ЗапретыПоДопуску.ФумигационныйЗапрет) КАК ФумигационныйЗапрет,
				|	МАКСИМУМ(ВТ_ЗапретыПоДопуску.ФитосанитарныйЗапрет) КАК ФитосанитарныйЗапрет,
				|	МАКСИМУМ(ВТ_ЗапретыПоДопуску.ПретензионныйЗапрет) КАК ПретензионныйЗапрет
				|ИЗ
				|	ВТ_ЗапретыПоДопуску КАК ВТ_ЗапретыПоДопуску";
	
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = Новый Структура("ФумигационныйЗапрет, ФитосанитарныйЗапрет, ПретензионныйЗапрет", Ложь, Ложь, Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

#Область ЛабАнализ

// Определяет подходит ли лабораторный анализ для заполнения основания движения запасов.
// 
// Централизованная функция для проверки соответствия лабораторного анализа
// критериям заполнения в основания для движения запасов.
// Учитывает функциональную опцию передачи качества входного контроля
// и назначение использования качества анализа.
//
// Параметры:
//   ЛабораторныйАнализ - ДокументСсылка.гкс_ЛабораторныйАнализ - проверяемый анализ
//
// Возвращаемое значение:
//   Булево - Истина, если анализ подходит для заполнения основания
//
Функция ПодходитДляОснованияДвиженияЗапасов(ЛабораторныйАнализ) Экспорт
    
    // Проверка на корректность переданного параметра
    Если Не ЗначениеЗаполнено(ЛабораторныйАнализ) Тогда
        Возврат Ложь;
    КонецЕсли;
    
    ВходнойКонтроль = Ложь;
	КомпозитныйАнализ = Ложь;
	
    // Проверка подходящих условий через отдельную функцию
    ОпределитьТипСтратегииОбновления(ЛабораторныйАнализ, ВходнойКонтроль, КомпозитныйАнализ);
    Возврат ВходнойКонтроль Или КомпозитныйАнализ;
    
КонецФункции

// Перезаполняет основания движения запасов при изменении лабораторного анализа.
//
// Централизованная процедура для обновления связанных документов
// "Основание для движения запасов" при изменении лабораторного анализа.
// Используется в событиях документа "Лабораторный анализ".
//
// Параметры:
//   ЛабораторныйАнализ - ДокументСсылка.гкс_ЛабораторныйАнализ - измененный анализ
//   Отказ - Булево - признак отказа от операции
//
Процедура ПерезаполнитьОснованияПриИзмененииАнализа(ЛабораторныйАнализ, Отказ) Экспорт
    
    // Проверка на отказ от операции
    Если Отказ Тогда
        Возврат;
    КонецЕсли;
    
	ВходнойКонтроль = Ложь;
	КомпозитныйАнализ = Ложь;
	
    // Определение типа стратегии обновления на основе настроек системы
    ОпределитьТипСтратегииОбновления(ЛабораторныйАнализ, ВходнойКонтроль, КомпозитныйАнализ);
    
    // Выполнение обновления согласно определенной стратегии
    Если ВходнойКонтроль Тогда
        // Стратегия для входного контроля
        ВыполнитьОбновлениеВходногоКонтроля(ЛабораторныйАнализ, Отказ);
        
    ИначеЕсли КомпозитныйАнализ Тогда
        // Стратегия для композитного анализа
         Документы.гкс_ОснованиеДляДвиженияЗапасов.
         	ПерезаполнитьОснованиеДвиженияЗапасовКомпозит(ЛабораторныйАнализ, Отказ);
  
    КонецЕсли;

    
КонецПроцедуры
 
// Определить лабораторный анализ.
// 
// Параметры:
//  ДокументРегистрации Документ регистрации
//  НазначениеИспользованияКачества - Неопределено - Назначение использования качества
//  Статус - Неопределено - Статус
// 
// Возвращаемое значение:
//  Неопределено - Определить лабораторный анализ
Функция ОпределитьЛабораторныйАнализ(ДокументРегистрации, НазначениеИспользованияКачества = Неопределено,
	Статус = Неопределено) Экспорт
    
    // Проверка корректности параметра
	Если Не ЗначениеЗаполнено(ДокументРегистрации) Тогда
		Возврат Неопределено;
	КонецЕсли;

	ЛабораторныйАнализ = Неопределено;

	Запрос = Новый Запрос;

	Если ПроверкаДляДляВходногоКонтроля(НазначениеИспользованияКачества, Статус) Тогда
            // Стратегия поиска для входного контроля
		Запрос.Текст = ТекстЗапросаЛабАнализДляВходногоКонтроля();
	ИначеЕсли ПроверкаДляКомпозитногоАнализа(НазначениеИспользованияКачества, Статус) Тогда

        // Стратегия поиска для композитного анализа  
		Запрос.Текст = ТекстЗапросаЛабАнализДляКомпозитногоАнализа();
	ИначеЕсли ПроверкаДляСтатусаВыполнен(НазначениеИспользованияКачества, Статус) Тогда   
		
    	// Стратегия поиска для статуса выполнен) 
		Запрос.Текст = ТекстЗапросаЛабАнализДляСтатусаВыполнен();
	Иначе
    	// Стратегия поиска для входного контроля
		Запрос.Текст = ТекстЗапросаЛабАнализДляРегистрации();
	КонецЕсли;

	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);

	РезультатЗапроса = Запрос.Выполнить();

	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ЛабораторныйАнализ = ВыборкаДетальныеЗаписи.АнализСсылка;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЛабораторныйАнализ;

КонецФункции

#КонецОбласти

#Область Автоприемка

#Область ИспользоватьКонтрольПревышенияВесаАвтомобиля

// Проверяет превышение веса автомобиля по осям при приемке
//
// Параметры:
//  ДокументВзвешивание  - ДокументОбъект.гкс_Взвешивание - документ в котором производится контроль веса
// 
// Возвращаемое значение:
//  Булево - Истина, если допустимый вес превышен
//
Функция ПревышениеДопустимогоВесаАвтомобиля(ДокументВзвешивание) Экспорт

	ДанныеРегистрации = гкс_ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументВзвешивание.ДокументРегистрации, 
		"ТранспортноеСредство, ЕдиницаИзмеренияВеса, ТипРегистрации");
	
	ПараметрыТС = гкс_ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДанныеРегистрации.ТранспортноеСредство, "ВидПеревозки, КоличествоОсей");

	Если Не (ДокументВзвешивание.ТипВзвешивания = Перечисления.гкс_ТипыВзвешивания.Въезд
		И ПараметрыТС.ВидПеревозки = Перечисления.гкс_ТипыТранспортныхСредствДоставки.Автомобиль
		И ЗначениеЗаполнено(ПараметрыТС.КоличествоОсей)
		И ДанныеРегистрации.ТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка) Тогда	
		Возврат Ложь;
	КонецЕсли;
		
	ДопустимыйВес = МаксимальныйДопустимыйВес(ПараметрыТС.ВидПеревозки, ПараметрыТС.КоличествоОсей);
										
	Если Не ЗначениеЗаполнено(ДопустимыйВес) Тогда
		Возврат Ложь;
	КонецЕсли;											
									
	ЕдиницаИзмеренияВесаПриемки = ПолучитьЕдиницуИзмеренияВесаПриемки();
	Коэффициент = гкс_ПриемкаТранспортаПовтИсп.ПолучитьКоэффициентПересчетаЕдиниц(
		ЕдиницаИзмеренияВесаПриемки, ДанныеРегистрации.ЕдиницаИзмеренияВеса);
	
	ДопустимыйВес = ДопустимыйВес * Коэффициент; 									
														
	Возврат ДопустимыйВес < ДокументВзвешивание.Вес;	

КонецФункции 

// Определяет максимально допустимый вес в зависисмости от вида транспорта и количества осей
//
// Параметры:
//  ВидПеревозки - ПеречислениеСсылка.гкс_ТипыТранспортныхСредствДоставки -
//  КоличествоОсей  - ПеречислениеСсылка.гкс_КоличествоОсейТранспортногоСредства -
// 
// Возвращаемое значение:
//  ДопустимыйВес - Число -  значение допустимого веса
//
Функция МаксимальныйДопустимыйВес(ВидПеревозки, КоличествоОсей) Экспорт
	
	Возврат РегистрыСведений.гкс_ДопустимыеМассыТранспортныхСредств
		.МаксимальныйДопустимыйВес(ВидПеревозки, КоличествоОсей);
		
КонецФункции

// Проверяет заполнения данных для проведения контроля превышения максимального разрешенного веса автомобиля
// 
// Возвращаемое значение:
//  Булево - Истина, если проверка прошла успешно
//
Функция ПроверкаЗаполненияДляКонтроляПревышенияВесаАвтомобиля(Автомобиль, ОписаниеОшибок) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("гкс_ИспользоватьКонтрольПревышенияВесаАвтомобиля") Тогда
		Возврат Истина;
	КонецЕсли;	
		
	ДанныеТС = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Автомобиль, "ВидПеревозки, КоличествоОсей");	
	Если ДанныеТС.ВидПеревозки <> Перечисления.гкс_ТипыТранспортныхСредствДоставки.Автомобиль Тогда
		Возврат Истина;
	КонецЕсли;        
	
	Результат = Истина;
	
	Если ЗначениеЗаполнено(ДанныеТС.КоличествоОсей) Тогда
		
		МаксимальныйДопустимыйВес = МаксимальныйДопустимыйВес(ДанныеТС.ВидПеревозки, ДанныеТС.КоличествоОсей); 
		
		Если МаксимальныйДопустимыйВес = Неопределено Тогда
			
			ШаблонВСообщения = НСтр("ru = 'Не указана допустимая масса транспортного средства для %1 количества осей.
			|Превышение допустимого веса проверяться не будет!'");
			
			ОписаниеОшибок = СтрШаблон(ШаблонВСообщения, ДанныеТС.КоличествоОсей);		
			Результат = Ложь;
		КонецЕсли;	
	
	Иначе
		
		ШаблонВСообщения = НСтр("ru = 'У транспортного средства %1 не указано количество осей.
		|Превышение допустимого веса проверяться не будет!'");
		
		ОписаниеОшибок = СтрШаблон(ШаблонВСообщения, Автомобиль);		
		Результат = Ложь;
		
	КонецЕсли;	
	
	Возврат Результат; 
	
КонецФункции

#КонецОбласти

// Возвращает конец Дату БУ производственных суток
// ДатаНачалаПроизводственныхСуток - Дата
// КоличествоМинутДоКонцаДня - Число
// Возвращаемое значение:
// - Дата
Функция ОпределениеДатыБУВПроизводственныхСутках(
		ДатаНачалаПроизводственныхСуток, КоличествоМинутДоКонцаДня) Экспорт
	
	Приращение = гкс_ОбщегоНазначенияКлиентСервер.СекундВМинуте() * КоличествоМинутДоКонцаДня;
	Возврат КонецДня(ДатаНачалаПроизводственныхСуток) - Приращение;

КонецФункции

// Функция определяет дату БУ для транспорта
// Параметры:
// ДатаРазгрузки - Дата
// ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
// ОснованиеДвиженияЗапасов - ДокументСсылка.ОснованиеДляДвиженияЗапасов
// ВозвращаемоеЗначение:
// Дата
Функция ДатаБУПриемкиТранспорта(ДатаРазгрузки, ДокументРегистрации, ОснованиеДвиженияЗапасов) Экспорт
	
	ДатаНачалаПроизводственныхСуток =
		ДатаНачалаПроизводственныхСуток(ДатаРазгрузки);
	Если День(ДатаНачалаПроизводственныхСуток) = День(ДатаРазгрузки) Тогда
		ДатаБУ = ДатаРазгрузки;
	Иначе
		ДатаРеестра = ДатаРеестраНакладныхЗПП_3(
			ДокументРегистрации, ОснованиеДвиженияЗапасов);
		Если ЗначениеЗаполнено(ДатаРеестра) Тогда
			ДатаБУ = ДатаРеестра;
		Иначе
			КоличествоМинутДоКонцаДня  = 9;
			ДатаБУ = ОпределениеДатыБУВПроизводственныхСутках(
				ДатаНачалаПроизводственныхСуток, КоличествоМинутДоКонцаДня);
		КонецЕсли;
	КонецЕсли;
		
	Возврат ДатаБУ;
	
КонецФункции

// Функция определяет дату реестра
// Параметры:
// ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
// ОснованиеДвиженияЗапасов - ДокументСсылка.ОснованиеДляДвиженияЗапасов
// ВозвращаемоеЗначение:
// Дата
Функция ДатаРеестраНакладныхЗПП_3(ДокументРегистрации, ОснованиеДвиженияЗапасов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
		|	ОформленныеПартии.ДатаРеестра КАК ДатаРеестра
		|ИЗ
		|	РегистрСведений.гкс_ОформленныеПартии КАК ОформленныеПартии
		|ГДЕ
		|	ОформленныеПартии.ДокументРегистрации = &ДокументРегистрации
		|	И ОформленныеПартии.ОснованиеДвиженияЗапасов = &ОснованиеДвиженияЗапасов";
		
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	Запрос.УстановитьПараметр("ОснованиеДвиженияЗапасов", ОснованиеДвиженияЗапасов);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Результат = ВыборкаДетальныеЗаписи.ДатаРеестра;
		Иначе
			Результат = Неопределено;
	КонецЕсли;
		
	Возврат Результат;
КонецФункции

// Возвращает число символов для поиска.
// 
// Возвращаемое значение:
//  Число - количество символов для поиска транспортного средства в справочнике
//
Функция КоличествоСимволовДляПоискаПоНомеру() Экспорт
	
	Возврат 6;
	
КонецФункции

// Возвращает строку для подстановки в запрос поиска транспорта по ччасти номера
//
// Параметры:
//  НомерТранспорта - Строка - номер транспорта
//
//  ЧислоСимволов - Число - количество символов для сравнения
// 
// Возвращаемое значение:
//  Строка - строка для подстановки в текст запроса 
//
Функция ПолеПоискаГосНомер(НомерТранспорта, ЧислоСимволов) Экспорт
	
	СоставСтроки = Новый Массив;
	СоставСтроки.Добавить(Лев(НомерТранспорта, ЧислоСимволов));
	СоставСтроки.Добавить("%");
	
	Возврат СтрСоединить(СоставСтроки);
	
КонецФункции

// Направление на разгрузку по регистрации.
// 
// Параметры:
//  Регистрация - ДокументСсылка.гкс_РегистрацияНаПЛК - 
// 
// Возвращаемое значение:
//  ДокументСсылка.гкс_НаправлениеНаРазгрузку - Направление на разгрузку по регистрации
//
Функция НаправлениеНаРазгрузкуПоРегистрации(Знач Регистрация) Экспорт
	
	Возврат Документы.гкс_НаправлениеНаРазгрузку.ПолучитьПоРегистрации(Регистрация);
	
КонецФункции

// Определяет время начала производственный суток для даты
// 
// Параметры:
//  - ДатаРегистрации - Дата - дата приезда на ПЛК
// 
// Возвращаемое значение:
// НачалоПериода   - ДатаВремя 	- дата, время начала произв. суток		
//
Функция ДатаНачалаПроизводственныхСуток(ДатаРегистрации) Экспорт
		
	ДатаНачала = НачалоПроизводственныхСуток(ДатаРегистрации);
	
	Если ДатаНачала > ДатаРегистрации Тогда
		ПредыдущиеСутки = ДатаРегистрации - гкс_ОбщегоНазначенияКлиентСервер.СекундВДне();
		Результат = НачалоПроизводственныхСуток(ПредыдущиеСутки);
	Иначе
		Результат = ДатаНачала;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Определяет время начала и окончания производственный суток для даты
// 
// Параметры:
//  - ДатаРегистрации - Дата - дата приезда на ПЛК
// 
// Возвращаемое значение:
//  Структура 	
//			- НачалоПериода    	- дата, время начала произв. суток
//			- ОкончаниеПериода	- дата, время конца произв. суток		
//
Функция ПериодПроизводственнойСмены(ДатаРегистрации) Экспорт
	
	Результат = Новый Структура("НачалоПериода, ОкончаниеПериода");
	
	ДатаНачала = НачалоПроизводственныхСуток(ДатаРегистрации);
	
	Если ДатаНачала > ДатаРегистрации Тогда
		ПредыдущиеСутки = ДатаРегистрации - гкс_ОбщегоНазначенияКлиентСервер.СекундВДне();
		Результат.НачалоПериода = НачалоПроизводственныхСуток(ПредыдущиеСутки);
		Результат.ОкончаниеПериода = ОкончаниеПроизводственныхСуток(ПредыдущиеСутки);
	Иначе
		Результат.НачалоПериода = ДатаНачала;
		Результат.ОкончаниеПериода = ОкончаниеПроизводственныхСуток(ДатаРегистрации);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Определяет время начала производственный суток для даты
// 
// Параметры:
//  - ДатаРегистрации - Дата - 
// 
// Возвращаемое значение:
//  Дата - дата, время начала произв. суток		
//
Функция НачалоПроизводственныхСуток(ДатаСмены) Экспорт
	
    УстановитьПривилегированныйРежим(Истина);

	Приращение = Константы.гкс_СмещениеПроизводственныхСуток.Получить();
	Результат = НачалоДня(ДатаСмены) + Приращение * гкс_ОбщегоНазначенияКлиентСервер.СекундВЧасе(); 
	
	Возврат НачалоЧаса(Результат);
	
КонецФункции

// Определяет время окончания производственный суток для даты
// 
// Параметры:
//  - ДатаРегистрации - Дата - 
// 
// Возвращаемое значение:
//  Дата - дата, время окончания произв. суток		
//
Функция ОкончаниеПроизводственныхСуток(ДатаСмены) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Приращение = Константы.гкс_СмещениеПроизводственныхСуток.Получить();
	Результат = КонецДня(ДатаСмены) + Приращение * гкс_ОбщегоНазначенияКлиентСервер.СекундВЧасе(); 
	
	Возврат КонецЧаса(Результат);
	
КонецФункции

// Определяет производственные сутки по дате с учетом смещения
// Параметры:
// ДатаСмены - Дата
// Возвращаемое значение
// Дата - прои
Функция ПроизводственныеСуткиПоДата(ДатаСмены) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Приращение = Константы.гкс_СмещениеПроизводственныхСуток.Получить();
	Результат = ДатаСмены - Приращение * гкс_ОбщегоНазначенияКлиентСервер.СекундВЧасе();   
	
	Возврат НачалоДня(Результат);
	
КонецФункции	

// Проверяет, прошли ли производственные сутки для указанной даты операции.
// 
// Параметры:
//  ДатаОперации - Дата - дата выполнения операции
//  ТочкаМаршрута Точка маршрута
// 
// Возвращаемое значение:
//  Булево - Истина, если производственные сутки прошли
Функция ТекущиеПроизводственныеСуткиПрошли(ДатаОперации, ТочкаМаршрута) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаОперации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МестнаяДата = Справочники.гкс_ТочкиМаршрута.ПолучитьМестнуюДатуПоТочкеМаршрута(ТочкаМаршрута);
	Возврат МестнаяДата >= ОкончаниеПроизводственныхСуток(ДатаОперации);
	
КонецФункции
	
// Получить единицу измерения веса по-умолчанию в приемке.
// 
// Возвращаемое значение:
//  Произвольный, СправочникСсылка.УпаковкиЕдиницыИзмерения - установленно значение константы
Функция ПолучитьЕдиницуИзмеренияВесаПриемки() Экспорт
	
	Возврат гкс_ПриемкаТранспортаПовтИсп.ПолучитьЕдиницуИзмеренияВесаПриемки();
	
КонецФункции

// Получить коэффициент пересчета из ЕдиницаДо в ЕдиницаПосле
// см. гкс_ПриемкаТранспортаПовтИсп.ПолучитьКоэффициентПересчетаЕдиниц  
Функция ПолучитьКоэффициентПересчетаЕдиниц(ЕдиницаДо, ЕдиницаПосле) Экспорт
	
	Возврат гкс_ПриемкаТранспортаПовтИсп.ПолучитьКоэффициентПересчетаЕдиниц(ЕдиницаДо, ЕдиницаПосле);
	
КонецФункции

// Пересчитывает весовые показатели в переданной структуре в единицу документа.
// 
// Параметры:
//  ДанныеПоВесу - Структура:
//  	* ЕдиницаИзмеренияВеса - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица веса в структуре
//  	* ВесНетто - Число
//  	* ВесБрутто - Число
//  	* ВесТары - Число
//  ЕдиницаИзмеренияДокумента - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица веса документа
Процедура ПересчитатьВесВЕдиницахДокумента(ДанныеПоВесу, ЕдиницаИзмеренияДокумента) Экспорт
	
	КоэффициентПересчета = гкс_ПриемкаТранспорта.ПолучитьКоэффициентПересчетаЕдиниц(
								ДанныеПоВесу.ЕдиницаИзмеренияВеса, ЕдиницаИзмеренияДокумента);
								
	ДанныеПоВесу.ВесНетто	= ДанныеПоВесу.ВесНетто * КоэффициентПересчета; 
	ДанныеПоВесу.ВесБрутто	= ДанныеПоВесу.ВесБрутто * КоэффициентПересчета; 
	ДанныеПоВесу.ВесТары	= ДанныеПоВесу.ВесТары * КоэффициентПересчета;
	 
КонецПроцедуры

// Получает данные по результатам взвешивания для регистрации процесса приемки
// 
// Параметры:
// 	ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - регистрация транспорта на ПЛК
//																для которой необходимо получить данные о значениях взвешивания
// 
// Возвращаемое значение:
//  Структура - данные о значениях взвешивания
//
Функция ИнформацияОВесахПриемкиПоДокументуРегистрации(ДокументРегистрации) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацияНаПЛК.Ссылка КАК Регистрация,
	|	РегистрацияНаПЛК.ВесБрутто КАК БруттоДок,
	|	РегистрацияНаПЛК.ВесНетто КАК НеттоДок,
	|	РегистрацияНаПЛК.ВесТары КАК ТараДок,
	|	ЕСТЬNULL(ВзвешиваниеНаВъезде.Вес, 0) КАК БруттоФакт,
	|	ЕСТЬNULL(ВзвешиваниеНаВыезде.Вес, 0) КАК ТараФакт,
	|	ВЫБОР
	|		КОГДА ВзвешиваниеНаВыезде.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВзвешиваниеНаВъезде.Вес, 0) - ЕСТЬNULL(ВзвешиваниеНаВыезде.Вес, 0)
	|	КОНЕЦ КАК НеттоФакт,
	|	ВЫБОР
	|		КОГДА ВзвешиваниеНаВыезде.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВзвешиваниеНаВъезде.Вес, 0) - ЕСТЬNULL(ВзвешиваниеНаВыезде.Вес, 0) - РегистрацияНаПЛК.ВесНетто
	|	КОНЕЦ КАК НеттоОтклонение,
	|	ВЫБОР
	|		КОГДА ВзвешиваниеНаВъезде.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВзвешиваниеНаВъезде.Вес, 0) - РегистрацияНаПЛК.ВесБрутто
	|	КОНЕЦ КАК БруттоОтклонение,
	|	ВЫБОР
	|		КОГДА ВзвешиваниеНаВыезде.Ссылка ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВзвешиваниеНаВыезде.Вес, 0) - РегистрацияНаПЛК.ВесТары
	|	КОНЕЦ КАК ТараОтклонение,
	|	ЕСТЬNULL(ПредельноеОтклонениеВесаГруза.Значение, 0) * РегистрацияНаПЛК.ВесБрутто КАК БруттоОтклонениеРазрешенное,
	|	ЕСТЬNULL(ПредельноеОтклонениеВесаГруза.Значение, 0) * РегистрацияНаПЛК.ВесНетто КАК НеттоОтклонениеРазрешенное
	|ИЗ
	|	Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеНаВъезде
	|		ПО (ВзвешиваниеНаВъезде.ДокументРегистрации = РегистрацияНаПЛК.Ссылка)
	|			И (ВзвешиваниеНаВъезде.ТипВзвешивания = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыВзвешивания.Въезд))
	|			И (ВзвешиваниеНаВъезде.Проведен)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_Взвешивание КАК ВзвешиваниеНаВыезде
	|		ПО (ВзвешиваниеНаВыезде.ДокументРегистрации = РегистрацияНаПЛК.Ссылка)
	|			И (ВзвешиваниеНаВыезде.ТипВзвешивания = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыВзвешивания.Выезд))
	|			И (ВзвешиваниеНаВыезде.Проведен),
	|	Константа.гкс_ПредельноеОтклонениеВесаГруза КАК ПредельноеОтклонениеВесаГруза
	|ГДЕ
	|	РегистрацияНаПЛК.Ссылка = &ДокументРегистрации
	|	И РегистрацияНаПЛК.ТипРегистрации = ЗНАЧЕНИЕ(Перечисление.гкс_ТипРегистрации.Приемка)";
	
	Запрос.УстановитьПараметр("ДокументРегистрации", ДокументРегистрации);
	
	РезультатЗапроса = Запрос.Выполнить();
		
	Результат = Неопределено;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ИндексПервойСтроки = 0;
		ДанныеЗапроса = Запрос.Выполнить().Выгрузить();	
		СтрокаТаблицы = ДанныеЗапроса[ИндексПервойСтроки];
			
		Результат = гкс_ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТаблицы);	
		
		Результат.Вставить("БруттоОтклонениеAbs", Макс(-Результат.БруттоОтклонение, Результат.БруттоОтклонение));
		Результат.Вставить("НеттоОтклонениеAbs", Макс(-Результат.НеттоОтклонение, Результат.НеттоОтклонение));
		Результат.Вставить("ТараОтклонениеAbs", Макс(-Результат.ТараОтклонение, Результат.ТараОтклонение));
		Результат.Вставить("ПредельноеОтклонениеВесаГруза", Константы.гкс_ПредельноеОтклонениеВесаГруза.Получить());
		
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

// Определяет является ли перевозка внутригрупповой
// 
// Параметры:
// 	Отправитель - СправочникСсылка.Контрагенты - контрагент отправитель груза
//																 
// Возвращаемое значение:
//  Булево - Истина - когда перевозка внутригрупповая
//
Функция ЭтоВнутригрупповаяПеревозка(Получатель) Экспорт

	ОрганизацияПолучатель = Справочники.Организации.ОрганизацияКонтрагента(Получатель);
	
	Возврат ЗначениеЗаполнено(ОрганизацияПолучатель);
	
КонецФункции		

// Возвращает применение оформления ЖД вагонов без взвешивания
// Параметры: 
// ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
// ВозвращаемоеЗначение
// - Булево
Функция РазрешитьВходнойКонтрольДоВзвешиванияПоРегистрации(ДокументРегистрации) Экспорт
	
	ДанныеРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументРегистрации, "ТипРегистрации, ВидПеревозки");
		
	Возврат РазрешитьВходнойКонтрольДоВзвешивания(ДанныеРегистрации.ТипРегистрации, 
													ДанныеРегистрации.ВидПеревозки);
		
КонецФункции 

// Возвращает применение оформления ЖД вагонов без взвешивания
// Параметры: 
// ТипРегистрации - Перечисления.гкс_ТипРегистрации
// ВидПеревозки - Перечисления.гкс_ТипыТранспортныхСредствДоставки
// ВозвращаемоеЗначение
// - Булево
Функция РазрешитьВходнойКонтрольДоВзвешивания(ТипРегистрации, ВидПеревозки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат
	    ТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка
		И Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЭтоЖДПеревозка(ВидПеревозки)
		И Константы.гкс_РазрешитьВходнойКонтрольДоВзвешиванияЖД.Получить();
		
КонецФункции 	
	
// Возвращает признак наличия документа взвешивания на въезде
//
// Параметры:
//	ДокументРегистрации - ДокументСсылка.гкс_РегистарцияПЛК
//  
// Возвращаемое значение:
//   Булево  - 
//
Функция ЕстьВзвешиваниеБруттоПриемка(ДокументРегистрации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат ЗначениеЗаполнено(
			Документы.гкс_Взвешивание.ПоРегистрацииИТипуВзвешивания(
			ДокументРегистрации, Перечисления.гкс_ТипыВзвешивания.Въезд));

КонецФункции    

// Проверяется возможность создания док-та Направления на разгрузку
//
// Параметры:
//	ДокументРегистрации - ДокументСсылка.гкс_РегистарцияПЛК
//  
// Возвращаемое значение:
//   Булево  - 
//
Функция НаправлятьНаРазгрузкуРазрешение(ДокументРегистрации) Экспорт
	
	ДанныеРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументРегистрации, "ТипРегистрации, ВидПеревозки");
		
	Если ДанныеРегистрации.ТипРегистрации <> Перечисления.гкс_ТипРегистрации.Приемка Тогда
		Результат = Ложь;
	Иначе	
		Результат = ЕстьВзвешиваниеБруттоПриемка(ДокументРегистрации);
	КонецЕсли;
		
	Возврат Результат;

КонецФункции

// Возвращает признак записи состояния документом Взвешивание
// Параметры:
// Взвешивание - ДокументСсылка.гкс_Взвешивание
// ВозвращаемоеЗначение
// - Булево
Функция УстанавливатьСостояниеПоВзвешиванию(Взвешивание) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументРегистрации = Взвешивание.ДокументРегистрации;
	РазрешитьВходнойКонтрольДоВзвешивания = 
		гкс_ПриемкаТранспорта.РазрешитьВходнойКонтрольДоВзвешиванияПоРегистрации(ДокументРегистрации);
		
	Если РазрешитьВходнойКонтрольДоВзвешивания
		И ЭтоВъездПриемкаЖДТранспорт(Взвешивание) Тогда
		Значение = ПроверитьСтатусПрибыл(Взвешивание, ДокументРегистрации);
	Иначе     
		Значение = Истина;
	КонецЕсли;	
		
	Возврат Значение;
		
КонецФункции

Функция ЭтоВъездПриемкаЖДТранспорт(ДокументВзвешивания) Экспорт
	
	РеквизитыВзвешивание = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументВзвешивания, 
		"ТипВзвешивания, ДокументРегистрации");
	РеквизитыРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыВзвешивание.ДокументРегистрации, 
		"ВидПеревозки, ТипРегистрации");
	
	Возврат РеквизитыВзвешивание.ТипВзвешивания = Перечисления.гкс_ТипыВзвешивания.Въезд
		И РеквизитыРегистрации.ТипРегистрации = Перечисления.гкс_ТипРегистрации.Приемка
		И Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЭтоЖДПеревозка(РеквизитыРегистрации.ВидПеревозки);

КонецФункции

// Формирует Направление на разгрузку
// Параметры:
// ДатаДокумента - Дата
// ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
// КачественныеПоказатели - ТаблицаЗначений
// Возвращаемое значение:
Процедура СформироватьНаправлениеНаРазгрузку(ДатаДокумента, ДокументРегистрации, ЛабораторныйАнализ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаправлениеНаРазгрузкуСсылка = 
		Документы.гкс_НаправлениеНаРазгрузку.ПолучитьПоРегистрации(ДокументРегистрации);
		
	Если ЗначениеЗаполнено(НаправлениеНаРазгрузкуСсылка) Тогда
		Возврат;
	КонецЕсли;          
	
	РеквизитыРегистрации = "ВидПеревозки, ТипРегистрации, Номенклатура, ТочкаМаршрута, Организация";
	ДанныеРегистрации = ОбщегоНазначения
	                        .ЗначенияРеквизитовОбъекта(ДокументРегистрации, РеквизитыРегистрации);
	
	СлужебнаяНоменклатура = 
		ПодходящаяСлужебнаяНоменклатураДляФормированияНаправленияНаРагрузку(
			ДатаДокумента, ДанныеРегистрации, ЛабораторныйАнализ);
	Если Не ЗначениеЗаполнено(СлужебнаяНоменклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ЯмаСилосРазгрузки = гкс_ПриемкаТранспорта.ЯмаСилосРазгрузки(
			ДанныеРегистрации.ТочкаМаршрута, ДанныеРегистрации.ВидПеревозки, 
			ДанныеРегистрации.Номенклатура, СлужебнаяНоменклатура);	

	Если Не ЗначениеЗаполнено(ЯмаСилосРазгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
	ДанныеЗаполнения.Вставить("ТочкаМаршрута", ДанныеРегистрации.ТочкаМаршрута);
	ДанныеЗаполнения.Вставить("ДокументРегистрации", ДокументРегистрации);

	ЧасовойПоясПЛК = гкс_ПриемкаТранспорта.ЧасовойПоясПЛК(ДанныеРегистрации.ТочкаМаршрута);
	МестнаяДата = ТекущаяДатаСеанса();
	Если ЗначениеЗаполнено(ЧасовойПоясПЛК) Тогда
		МестнаяДата = МестноеВремя(ТекущаяУниверсальнаяДата(), ЧасовойПоясПЛК);
	КонецЕсли;
	ДанныеЗаполнения.Вставить("МестнаяДата", МестнаяДата);

	ДанныеЗаполнения.Вставить("Склад", ЯмаСилосРазгрузки.Силос);
	ДанныеЗаполнения.Вставить("ЯмаРазгрузки", ЯмаСилосРазгрузки.ЯмаРазгрузки);
	ДанныеЗаполнения.Вставить("СлужебнаяНоменклатура", СлужебнаяНоменклатура);
	ДанныеЗаполнения.Вставить("ПервичныйСтатус", Перечисления.гкс_СостоянияРегистрации.КачествоПринято);
	ДанныеЗаполнения.Вставить("ПринятьКачество", Истина);

	НаправлениеНаРазгрузку = Документы.гкс_НаправлениеНаРазгрузку.СоздатьДокументНаОсновании(ДанныеЗаполнения);	

КонецПроцедуры	   

// Возвращает служебную номенклатуру
// 
//	Параметры:
// 		ДатаДокумента - Дата
// 		ДанныеРегистрации - Структура
// 		ЛабораторныйАнализ - ДокументСсылка.гкс_ЛабораторныйАнализ
// 		
// Возвращаемое значение:
// - СправочникСсылка.Номенклатура
//
Функция ПодходящаяСлужебнаяНоменклатураДляФормированияНаправленияНаРагрузку(
		ДатаДокумента, ДанныеРегистрации, ЛабораторныйАнализ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСлужебнаяНоменклатураДляФормированияНаправленияНаРагрузку();       
	
	Запрос.УстановитьПараметр("ДатаПроверки", КонецДня(ДатаДокумента));
	Запрос.УстановитьПараметр("Номенклатура", ДанныеРегистрации.Номенклатура);
	Запрос.УстановитьПараметр("ТочкаМаршрута", ДанныеРегистрации.ТочкаМаршрута);
	
	Запрос.УстановитьПараметр("КачественныеПоказатели", ЛабораторныйАнализ.КачественныеПоказатели.Выгрузить());
	
	Запрос.УстановитьПараметр("ВидПеревозки", ДанныеРегистрации.ВидПеревозки);
	Запрос.УстановитьПараметр("НазначениеИспользованияКачества", 
	       Перечисления.гкс_НазначенияИспользованияКачества.ПолучитьПоТипуРегистрации(ДанныеРегистрации.ТипРегистрации));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СлужебнаяНоменклатура;
	КонецЕсли;
	
	Возврат Справочники.Номенклатура.ПустаяСсылка();

КонецФункции 
	 
// Записать регистр приемки без входного контроля.
// 
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - ссылка на документ регистрации
//
Процедура ЗаписатьРегистрПриемкиБезВходногоКонтроля(ДокументРегистрации) Экспорт
		
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыРегистрации = "ТочкаМаршрута, Номенклатура, ТранспортноеСредство, Контрагент";
	ДанныеРегистрации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументРегистрации, РеквизитыРегистрации);
	
	МестнаяДата = Справочники.гкс_ТочкиМаршрута
		.ПолучитьМестнуюДатуПоТочкеМаршрута(ДанныеРегистрации.ТочкаМаршрута);
	
	ОписаниеСостояния = РегистрыСведений.гкс_ПриемкаТранспортаБезВходногоКонтроля.СтруктураЗаписи();
	
	ЗаполнитьЗначенияСвойств(ОписаниеСостояния, ДанныеРегистрации);
	
	ОписаниеСостояния.ПроизводственныеСутки = гкс_ПриемкаТранспорта.ПроизводственныеСуткиПоДата(МестнаяДата);	
	ОписаниеСостояния.Поставщик	= ДанныеРегистрации.Контрагент;
	ОписаниеСостояния.ДокументРегистрации = ДокументРегистрации;

	РегистрыСведений.гкс_ПриемкаТранспортаБезВходногоКонтроля
		.ЗарегистрироватьПриемкуТранспортаБезВходногоКонтроля(ОписаниеСостояния);
		
КонецПроцедуры
	
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Автоприемка

// Прояверяет, есть ли назначения по качеству для точки маршрута
// 
// Параметры:
//  СлужебнаяНоменклатура - СправочникСсылка.Номенклатура - 
//  ТочкаМаршрута - СправочникСсылка.гкс_ТочкиМаршрута - 
// 
// Возвращаемое значение:
//  Булево - Номенклатура назначена на яму для точки маршрута
//
Функция НоменклатураНазначенаНаЯмуДляТочкиМаршрута(СлужебнаяНоменклатура, ТочкаМаршрута) Экспорт
				  
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиНазначенияРазгрузки.ЯмаРазгрузки КАК ЯмаРазгрузки
	|ИЗ
	|	РегистрСведений.гкс_НастройкиНазначенияРазгрузки.СрезПоследних(
	|			,
	|			ТочкаМаршрута = &ТочкаМаршрута
	|				И СлужебнаяНоменклатура = &СлужебнаяНоменклатура) КАК НастройкиНазначенияРазгрузки";
	
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.УстановитьПараметр("СлужебнаяНоменклатура", СлужебнаяНоменклатура);
	
	Возврат Не Запрос.Выполнить().Пустой();		  
	
КонецФункции

// Точки маршрута пользователя.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - 
// 
// Возвращаемое значение:
//  Массив - массив из СправочникСсылка.гкс_ТочкиМаршрута - 
//
 Функция ТочкиМаршрутаПользователя(Знач Пользователь = Неопределено) Экспорт
	
	Возврат РегистрыСведений
	             .гкс_НастройкиПользователейПриемкаНаПЛК
	             .НастроенныеТочкиМаршрутаПользователя(Пользователь);
	
КонецФункции

// Получает транспортный документ и транспорт по регистрации.
// 
// Параметры:
//  ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК - 
// 
// Возвращаемое значение:
//  Структура:
// 		* ТранспортныйДокумент - ДокументСсылка.гкс_ТранспортныйДокумент -
// 		* ТраснпортноеСредство - СправочникСсылка.ТранспортныеСредства  
//
Функция ИнформацияОТранспортеПоРегистрации(ДокументРегистрации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат РегистрыСведений.гкс_СвязьРегистрацииИТранспортногоДокумента.ПолучитьСвязьСТранспортнымДокументом(ДокументРегистрации);
	
КонецФункции

// Установить документы регистрации.
// 
// Параметры:
//  СтруктураЗаписи - Структура:
//   * ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
//   * ТранспортныйДокумент - ДокументСсылка.гкс_ТранспортныйДокумент
//   * ТранспортноеСредство - СправочникСсылка.ТранспортныеСредства 
//
Процедура УстановитьСвязьРегистрацииИТранспортногоДокумента(СтруктураЗаписи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.гкс_СвязьРегистрацииИТранспортногоДокумента.ДобавитьЗапись(СтруктураЗаписи);
		
КонецПроцедуры

// Установить документы регистрации.
// 
// Параметры:
//  СтруктураЗаписи - Структура:
//   * ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
//   * ЗаписьВОчередь - ДокументСсылка.гкс_ЗаписьВОчередьПриемкиПЛК
//
Процедура УстановитьСвязьРегистрацииИЗаписиВОчередь(СтруктураЗаписи) Экспорт
	
	РегистрыСведений.гкс_РегистрацииПоЭлектроннойОчереди.ДобавитьЗапись(СтруктураЗаписи);
		
КонецПроцедуры

// Удалить документы регистрации.
// 
// Параметры:
//  СтруктураЗаписи - Структура:
//   * ДокументРегистрации - ДокументСсылка.гкс_РегистрацияНаПЛК
//   * ТранспортныйДокумент - ДокументСсылка.гкс_ТранспортныйДокумент
//   * ТранспортноеСредство - СправочникСсылка.ТранспортныеСредства 
//
Процедура УдалитьСвязьРегистрацииИТранспортногоДокумента(СтруктураЗаписи) Экспорт
	
	РегистрыСведений.гкс_СвязьРегистрацииИТранспортногоДокумента.УдалитьЗапись(СтруктураЗаписи);
		
КонецПроцедуры

// Это пользователь с ролью администратор ПЛК.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи, Неопределено - 
//  Параметры - Структура - 
// 
// Возвращаемое значение:
//  Булево - Это пользователь с ролью администратор ПЛК или нет
//
Функция ЭтоПользовательСРольюАдминистраторПЛК(Пользователь, Параметры) Экспорт
	
	Возврат РегистрыСведений
	              .гкс_НастройкиПользователейПриемкаНаПЛК
	              .ЭтоПользовательСРольюАдминистраторПЛК(Пользователь, Параметры);
КонецФункции

#КонецОбласти     

Функция ПроверитьСтатусПрибыл(ДокументВзвешивания, ДокументРегистрации)
	МассивСостояний = Новый Массив;
	МассивСостояний.Добавить(Перечисления.гкс_СостоянияРегистрации.Прибыл);
	
	ТекущееСостояние = РегистрыСведений.гкс_СостоянияРегистрации.ТекущиеДанныеСостоянияПоРегистрации(
		ДокументРегистрации, ДокументВзвешивания.Дата - 1);
	
	Возврат
		МассивСостояний.Найти(ТекущееСостояние.Состояние) <> Неопределено;
	
КонецФункции  

// Статус убыл присвоен для документа.
// 
// Параметры:
//  ДокументРегистрации Документ регистрации
// 
// Возвращаемое значение:
//  Булево - Истина, если статус "Убыл" присвоен документу регистрации
// Ложь, если статус не присвоен или документ не заполнен
// 
// Пример: СтатусПрисвоен = гкс_ПриемкаТранспорта.СтатусУбылПрисвоенДляДокумента(ДокументРегистрации);
Функция СтатусУбылПрисвоенДляДокумента(ДокументРегистрации) Экспорт

	// Проверка входных параметров
	Если НЕ ЗначениеЗаполнено(ДокументРегистрации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Проверяем статус убыл 
	Возврат РегистрыСведений.гкс_СостоянияРегистрации.УжеУбыл(ДокументРегистрации);
	
КонецФункции  
	
Функция ТекстЗапросаСлужебнаяНоменклатураДляФормированияНаправленияНаРагрузку()

	Возврат
	"ВЫБРАТЬ
	|	КачественныеПоказатели.Значение КАК ЗначениеПоказателя,
	|	КачественныеПоказатели.Показатель КАК ПоказательАнализа,
	|	ИСТИНА КАК ЕстьПересечение
	|ПОМЕСТИТЬ ВТ_КачественныеПоказатели
	|ИЗ
	|	&КачественныеПоказатели КАК КачественныеПоказатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СегментацияКачестваНоменклатуры.СлужебнаяНоменклатура КАК СлужебнаяНоменклатура
	|ПОМЕСТИТЬ ВТ_Номенклатура
	|ИЗ
	|	РегистрСведений.гкс_СегментацияКачестваНоменклатуры КАК СегментацияКачестваНоменклатуры
	|ГДЕ
	|	СегментацияКачестваНоменклатуры.Номенклатура = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатыНормативнойСертификацииНоменклатуры.Регистратор КАК Регистратор,
	|	РезультатыНормативнойСертификацииНоменклатуры.ПоказательАнализа КАК ПоказательАнализа,
	|	МАКСИМУМ(РезультатыНормативнойСертификацииНоменклатуры.ЗначениеПоказателя) КАК ЗначениеПоказателя,
	|	МАКСИМУМ(РезультатыНормативнойСертификацииНоменклатуры.МаксимальноеЗначение) КАК МаксимальноеЗначение,
	|	РезультатыНормативнойСертификацииНоменклатуры.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(РезультатыНормативнойСертификацииНоменклатуры.ПериодС) КАК ПериодС,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА РезультатыНормативнойСертификацииНоменклатуры.ПериодПо = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ДАТАВРЕМЯ(2999, 1, 1, 0, 0, 0)
	|		ИНАЧЕ РезультатыНормативнойСертификацииНоменклатуры.ПериодПо
	|	КОНЕЦ) КАК ПериодПо
	|ПОМЕСТИТЬ ВТ_Показатели
	|ИЗ
	|	РегистрСведений.гкс_РезультатыНормативнойСертификацииНоменклатуры КАК РезультатыНормативнойСертификацииНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Номенклатура КАК ВТ_Номенклатура
	|		ПО РезультатыНормативнойСертификацииНоменклатуры.Номенклатура = ВТ_Номенклатура.СлужебнаяНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КачественныеПоказатели КАК ВТ_КачественныеПоказатели
	|		ПО РезультатыНормативнойСертификацииНоменклатуры.ПоказательАнализа = ВТ_КачественныеПоказатели.ПоказательАнализа
	|ГДЕ
	|	РезультатыНормативнойСертификацииНоменклатуры.ВидПеревозки = &ВидПеревозки
	|	И РезультатыНормативнойСертификацииНоменклатуры.НазначениеИспользованияКачества = &НазначениеИспользованияКачества
	|	И РезультатыНормативнойСертификацииНоменклатуры.ТочкаМаршрута = &ТочкаМаршрута
	|СГРУППИРОВАТЬ ПО
	|	РезультатыНормативнойСертификацииНоменклатуры.Регистратор,
	|	РезультатыНормативнойСертификацииНоменклатуры.ПоказательАнализа,
	|	РезультатыНормативнойСертификацииНоменклатуры.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Показатели.Номенклатура КАК Номенклатура,
	|	ВТ_Показатели.Регистратор КАК Регистратор,
	|	ВТ_Показатели.ПоказательАнализа КАК ПоказательАнализа,
	|	ЕСТЬNULL(ВТ_КачественныеПоказатели.ЕстьПересечение, ЛОЖЬ) КАК ЕстьПересечение
	|ПОМЕСТИТЬ ВТ_ПоискПересечений
	|ИЗ
	|	ВТ_Показатели КАК ВТ_Показатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КачественныеПоказатели КАК ВТ_КачественныеПоказатели
	|		ПО ВТ_Показатели.ПоказательАнализа = ВТ_КачественныеПоказатели.ПоказательАнализа
	|		И ВТ_Показатели.ЗначениеПоказателя <= ВТ_КачественныеПоказатели.ЗначениеПоказателя
	|		И ВТ_Показатели.МаксимальноеЗначение >= ВТ_КачественныеПоказатели.ЗначениеПоказателя
	|		И ВТ_Показатели.ПериодС <= &ДатаПроверки
	|		И ВТ_Показатели.ПериодПо >= &ДатаПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоискПересечений.Номенклатура КАК Номенклатура,
	|	ВТ_ПоискПересечений.Регистратор КАК Регистратор,
	|	ВТ_ПоискПересечений.ПоказательАнализа КАК ПоказательАнализа,
	|	МАКСИМУМ(ВТ_ПоискПересечений.ЕстьПересечение) КАК ЕстьПересечение
	|ПОМЕСТИТЬ ВТ_РезультатПоискаПересечений
	|ИЗ
	|	ВТ_ПоискПересечений КАК ВТ_ПоискПересечений
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПоискПересечений.Номенклатура,
	|	ВТ_ПоискПересечений.Регистратор,
	|	ВТ_ПоискПересечений.ПоказательАнализа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РезультатПоискаПересечений.Номенклатура КАК Номенклатура,
	|	ВТ_РезультатПоискаПересечений.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_РезультатСПересечениями
	|ИЗ
	|	ВТ_РезультатПоискаПересечений КАК ВТ_РезультатПоискаПересечений
	|ГДЕ
	|	ВТ_РезультатПоискаПересечений.ЕстьПересечение
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РезультатПоискаПересечений.Номенклатура,
	|	ВТ_РезультатПоискаПересечений.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РезультатПоискаПересечений.Номенклатура КАК Номенклатура,
	|	ВТ_РезультатПоискаПересечений.ЕстьПересечение КАК ЕстьПересечение,
	|	ВТ_РезультатПоискаПересечений.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_РезультатБезПересечения
	|ИЗ
	|	ВТ_РезультатПоискаПересечений КАК ВТ_РезультатПоискаПересечений
	|ГДЕ
	|	НЕ ВТ_РезультатПоискаПересечений.ЕстьПересечение
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РезультатПоискаПересечений.Номенклатура,
	|	ВТ_РезультатПоискаПересечений.ЕстьПересечение,
	|	ВТ_РезультатПоискаПересечений.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_РезультатСПересечениями.Номенклатура КАК СлужебнаяНоменклатура
	|ИЗ
	|	ВТ_РезультатСПересечениями КАК ВТ_РезультатСПересечениями
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РезультатБезПересечения КАК ВТ_РезультатБезПересечения
	|		ПО ВТ_РезультатСПересечениями.Регистратор = ВТ_РезультатБезПересечения.Регистратор
	|		И ВТ_РезультатСПересечениями.Номенклатура = ВТ_РезультатБезПересечения.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(ВТ_РезультатБезПересечения.ЕстьПересечение, ИСТИНА)";
	
КонецФункции

#Область ЛабАнализ

// Определить тип стратегии обновления.
// 
// Параметры:
//  ЛабораторныйАнализ - ДокументСсылка.гкс_ЛабораторныйАнализ - Лабораторный анализ
//  ВходнойКонтроль - Булево - Входной контроль
//  КомпозитныйАнализ - Булево - Композитный анализ
Процедура ОпределитьТипСтратегииОбновления(ЛабораторныйАнализ, ВходнойКонтроль, КомпозитныйАнализ)
    
    // Получение настройки функциональной опции передачи качества входного контроля
    // Эта опция определяет, как система обрабатывает качественные показатели
    ПередаватьКачествоВходногоКонтроля = ПолучитьФункциональнуюОпцию("гкс_ПередаватьКачествоВходногоКонтроля");
    
    // Получение назначения использования качества из документа лабораторного анализа
    // Назначение указывает, для какого процесса предназначен анализ
    НазначениеИспользованияКачества = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
        ЛабораторныйАнализ, "гкс_НазначениеИспользованияКачества");
    
    // Определение подходящего анализа по матрице решений
    // Логика основывается на двух ключевых параметрах:
    // 1. Функциональная опция передачи качества входного контроля
    // 2. Назначение использования качества анализа
    
    // Случай 1: Опция включена И назначение = Приемка
    // Используется для входного контроля при включенной передаче качества
    ВходнойКонтроль = ПередаватьКачествоВходногоКонтроля 
        И НазначениеИспользованияКачества = Перечисления.гкс_НазначенияИспользованияКачества.Приемка; 
    
    // Случай 2: Опция выключена И назначение = ПриемкаКомпозит 
    // Используется для композитного анализа при отключенной передаче качества
    КомпозитныйАнализ = Не ПередаватьКачествоВходногоКонтроля 
        И НазначениеИспользованияКачества = Перечисления.гкс_НазначенияИспользованияКачества.ПриемкаКомпозит;
КонецПроцедуры

// Стратегия обновления для входного контроля.
//
// Обрабатывает случай, когда включена функциональная опция передачи качества
// входного контроля. Находит связанное основание для движения запасов
// и обновляет его данными из лабораторного анализа.
//
// Параметры:
//   ЛабораторныйАнализ - ДокументСсылка.гкс_ЛабораторныйАнализ - анализ для обновления
//   Отказ - Булево - признак отказа от операции
//
Процедура ВыполнитьОбновлениеВходногоКонтроля(ЛабораторныйАнализ, Отказ) Экспорт
    
    // Получение документа регистрации из лабораторного анализа
    ДокументРегистрации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЛабораторныйАнализ, "гкс_ДокументРегистрации");
    
    // Поиск соответствующего основания для движения запасов
	ОснованиеДляДвиженияЗапасовСсылка 
    	= Документы.гкс_ОснованиеДляДвиженияЗапасов.ПолучитьПоРегистрации(ДокументРегистрации);
    
    Если ЗначениеЗаполнено(ОснованиеДляДвиженияЗапасовСсылка) Тогда
        
        // Получение объекта для изменения
        ОснованиеДляДвиженияЗапасов = ОснованиеДляДвиженияЗапасовСсылка.ПолучитьОбъект();
        
        // Заполнение по единой логике
        // Запись изменений с контролем ошибок
        ОснованиеДляДвиженияЗапасов.ПерезаполнитьПоЛабАнализу(ЛабораторныйАнализ, Отказ);   
    КонецЕсли;
    
КонецПроцедуры

// Текст запроса лаб анализ для входного контроля.
// 
// Возвращаемое значение:
//  Строка - Текст запроса лаб анализ для входного контроля
Функция ТекстЗапросаЛабАнализДляВходногоКонтроля()
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЛабораторныйАнализ.Ссылка КАК АнализСсылка
	|ИЗ
	|	Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|ГДЕ
	|	ЛабораторныйАнализ.Проведен
	|	И ЛабораторныйАнализ.гкс_НазначениеИспользованияКачества = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.Приемка)
	|	И ЛабораторныйАнализ.гкс_ДокументРегистрации = &ДокументРегистрации";
КонецФункции

// Текст запроса лаб анализ для композитного анализа.
// 
// Возвращаемое значение:
//  Строка - Текст запроса лаб анализ для композитного анализа
Функция ТекстЗапросаЛабАнализДляКомпозитногоАнализа()
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЛабораторныйАнализ.Ссылка КАК АнализСсылка
	|ПОМЕСТИТЬ ВТ_ПоФормированиюПроб
	|ИЗ
	|	Документ.гкс_ФормированиеНомераПробы.СписокРегистраций КАК ФормированиеНомераПробыСписокРегистраций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|		ПО ФормированиеНомераПробыСписокРегистраций.Ссылка = ЛабораторныйАнализ.гкс_ДокументРегистрации
	|			И (ЛабораторныйАнализ.Проведен)
	|			И (ЛабораторныйАнализ.гкс_НазначениеИспользованияКачества = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.ПриемкаКомпозит))
	|			И (ФормированиеНомераПробыСписокРегистраций.ДокументРегистрации = &ДокументРегистрации)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЛабораторныйАнализ.Ссылка КАК АнализСсылка
	|ПОМЕСТИТЬ ВТ_ПоРегистрации
	|ИЗ
	|	Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|ГДЕ
	|	ЛабораторныйАнализ.Проведен
	|	И ЛабораторныйАнализ.гкс_НазначениеИспользованияКачества = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.ПриемкаКомпозит)
	|	И ЛабораторныйАнализ.гкс_ДокументРегистрации = &ДокументРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПоРегистрации.АнализСсылка КАК АнализСсылка
	|ИЗ
	|	ВТ_ПоРегистрации КАК ВТ_ПоРегистрации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПоФормированиюПроб.АнализСсылка
	|ИЗ
	|	ВТ_ПоФормированиюПроб КАК ВТ_ПоФормированиюПроб";
КонецФункции

// Текст запроса лаб анализ для статуса выполнен.
// 
// Возвращаемое значение:
//  Строка - Текст запроса лаб анализ для статуса выполнен
Функция ТекстЗапросаЛабАнализДляСтатусаВыполнен()
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЛабораторныйАнализ.Ссылка КАК АнализСсылка
	|ИЗ
	|	Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|ГДЕ
	|	ЛабораторныйАнализ.гкс_ДокументРегистрации = &ДокументРегистрации
	|	И ЛабораторныйАнализ.Статус = ЗНАЧЕНИЕ(Перечисление.гкс_СтатусыЛабораторногоАнализа.Выполнен)
	|	И ЛабораторныйАнализ.гкс_НазначениеИспользованияКачества = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.Приемка)";
КонецФункции

// Текст запроса лаб анализ для регистрации.
// 
// Возвращаемое значение:
//  Строка - Текст запроса лаб анализ для регистрации
Функция ТекстЗапросаЛабАнализДляРегистрации()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ЛабораторныйАнализ.Ссылка КАК АнализСсылка
	|ИЗ
	|	Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|ГДЕ
	|	ЛабораторныйАнализ.гкс_ДокументРегистрации = &ДокументРегистрации
	|	И НЕ ЛабораторныйАнализ.ПометкаУдаления";
КонецФункции  

// Проверка для для входного контроля.
// 
// Параметры:
//  НазначениеИспользованияКачества - Неопределено - Назначение использования качества
//  Статус - Неопределено - Статус
// 
// Возвращаемое значение:
//  Булево - Проверка для для входного контроля
Функция ПроверкаДляДляВходногоКонтроля(НазначениеИспользованияКачества, Статус)
	Возврат ЗначениеЗаполнено(НазначениеИспользованияКачества)
		И НазначениеИспользованияКачества = Перечисления.гкс_НазначенияИспользованияКачества.Приемка 
		И НЕ ЗначениеЗаполнено(Статус);
КонецФункции  
	
// Проверка для композитного анализа.
// 
// Параметры:
//  НазначениеИспользованияКачества - Неопределено - Назначение использования качества
//  Статус - Неопределено - Статус
// 
// Возвращаемое значение:
//  Булево - Проверка для композитного анализа
Функция ПроверкаДляКомпозитногоАнализа(НазначениеИспользованияКачества, Статус)
	Возврат ЗначениеЗаполнено(НазначениеИспользованияКачества) 
		И НазначениеИспользованияКачества = Перечисления.гкс_НазначенияИспользованияКачества.ПриемкаКомпозит 
		И НЕ ЗначениеЗаполнено(Статус);
КонецФункции

// Проверка для статуса выполнен.
// 
// Параметры:
//  НазначениеИспользованияКачества - Неопределено - Назначение использования качества
//  Статус - Неопределено - Статус
// 
// Возвращаемое значение:
//  Булево - Проверка для статуса выполнен
Функция ПроверкаДляСтатусаВыполнен(НазначениеИспользованияКачества, Статус)
	Возврат ЗначениеЗаполнено(НазначениеИспользованияКачества) 
		И НазначениеИспользованияКачества = Перечисления.гкс_НазначенияИспользованияКачества.Приемка
		И ЗначениеЗаполнено(Статус);
КонецФункции
	
#КонецОбласти

#КонецОбласти

