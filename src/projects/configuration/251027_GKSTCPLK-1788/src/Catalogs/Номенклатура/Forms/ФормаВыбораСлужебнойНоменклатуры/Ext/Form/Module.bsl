 
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура = Параметры.Номенклатура;
	ЕстьСегментацияКачестваНоменклатуры = ЕстьСегментацияКачестваНоменклатуры();

	ВидыНоменклатурыЭталоныКачества = гкс_ОбщегоНазначенияВызовСервера.
		ПроизвольныйПредопределенныйОбъект("ВидыНоменклатурыЭталоныКачества");		
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	Если ЕстьСегментацияКачестваНоменклатуры Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокСегментацииКачества, 
			"Номенклатура", Номенклатура);	
	Иначе	
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокНоменклатуры, 
			"ВидНоменклатуры", ВидыНоменклатурыЭталоныКачества);
	КонецЕсли;
	
	Элементы.СписокСегментацииКачества.Видимость = ЕстьСегментацияКачестваНоменклатуры;
	Элементы.СписокНоменклатуры.Видимость = Не ЕстьСегментацияКачестваНоменклатуры;
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНоменклатуры

&НаКлиенте
Процедура СписокНоменклатурыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ОбработкаВыбораНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокСегментацииКачества

&НаКлиенте
Процедура СписокСегментацииКачестваВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
    ОбработкаВыбораНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)	
	
	ОбработкаВыбораНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЕстьСегментацияКачестваНоменклатуры()
	
	СегментацияКачества = гкс_ПриемкаТранспорта.СегментацияКачестваНоменклатуры(Номенклатура);	
	Возврат ЗначениеЗаполнено(СегментацияКачества);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораНоменклатуры()
	
	Если ЕстьСегментацияКачестваНоменклатуры Тогда
		ТекущиеДанные = Элементы.СписокСегментацииКачества.ТекущиеДанные;		
	Иначе	
		ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;	
	КонецЕсли;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьСегментацияКачестваНоменклатуры Тогда
		ВыбранноеЗначение = ТекущиеДанные.СлужебнаяНоменклатура;		
	Иначе	
		ВыбранноеЗначение = ТекущиеДанные.Ссылка;	
	КонецЕсли;
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры	

#КонецОбласти

 

